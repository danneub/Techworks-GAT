	 OPT	   MEX,CEX,NOPCS
*
	 XDEF	   NAV1
	 XDEF	   A1IPTAB
	 XDEF	   A2IPTAB
	 XREF	   MSC1
	 XREF	   MSIN1
	 XREF	   MCOS1
	 XREF	   NAVRCV
	 XREF	   AEPC
	 XREF	   NCDI
	 XREF	   NATAN2
	 XREF	   SQRT
*
	 SECTION   12
****************************************************************************
*
*		       NAV MODULE NUMBER 1
*
*		   NAV ENVIRONMENT AND RADIOS
*
***************************************************************************
~i	 INCLUDE   FSYM.SA
FALPHA	  EQU	$0000147A L 001 L S25  ANGLE OF ATTACK (DEGREES) 2-25,LONG 2-9(
FBALL	  EQU	$0000147E L 001 L S20  IND BALL WIDTHS/1.6  AT 2-20(LONG) 2-4(W
FBETA	  EQU	$00001482 L 001 L S25  SIDE SLIP ANGLE(DEGREES)AT 2-25(LONG) 2-
FHF	  EQU	$00001486 L 001 L S17  GEOMETRIC FIELD ELEV(FT)AT 2-17(LONG WOR
FHGGND	  EQU	$0000148A L 001 L S17  FLT HEIGHT ABOVE GROUND(FT)AT 2-17(LONG
FHFP	  EQU	$0000148E L 001 L S17  FIELD PRESSURE ALTITUDE(FT)AT 2-17(LONG
FHP	  EQU	$00001492 L 001 L S17  PRESSURE ALTITUDE(FT)AT 2-17(LONG WORD)2
FLAMBDA   EQU	$00001496 L 001 L S24  LAMBDA(BALL ANGLE)(DEGREES)AT 2-24(LONG)
FPA	  EQU	$0000149A L 001 L S22  ROLL RATE(DEG/SEC)AT 2-22(LONG WORD) 2-6
FPHI	  EQU	$0000149E L 001 L S22  BANK ANGLE(DEGREES)AT 2-22(LONG WORD) 2-
FPHIFRZ   EQU	$000014A2 L 001 L S22  FROZEN BANK ANGLE AT 2-22(LONG) 2-6(WORD
FPSI	  EQU	$000014A6 L 001 L S22  HEADING ANGLE(DEGREES)AT 2-22(LONG) 2-6(
FQA	  EQU	$000014AA L 001 L S25  PITCH RATE(DEG/SEC)AT 2-25(LONG WORD)2-9
FRA	  EQU	$000014AE L 001 L S25  RATE OF TURN(DEG/SEC)AT 2-25(LONG) 2-9(W
FRCHOLD   EQU	$000014B2 L 001 L S22  RATE OF CLIMB HOLD INTEGRATER AT 2-22
FRCLIMB   EQU	$000014B6 L 001 L S22  INDICATED RATE OF CLIMB/2000 (FT/MIN) AT
FROLLHLD  EQU	$000014BA L 001 L S22  BANK HOLD INTEGRATER AT 2-22
FRCLIMB1  EQU	$000014BE L 001 L S03  RATE OF CLIMB (FEET/MINUTE) AT 2-3
FHG	  EQU	$000014C2 L 001 L S17  GEOMETRIC ALTITUDE AT 2-17
FTEMP3	  EQU	$000014C6 L 001 L S03  FLIGHT SPARE
FTEMP4	  EQU	$000014CA L 001 L S04  FLIGHT SPARE
FTEMP5	  EQU	$000014CE L 001 L S05  FLIGHT SPARE
FTEMP6	  EQU	$000014D2 L 001 L S06  FLIGHT SPARE
FTHETA	  EQU	$000014D6 L 001 L S25  PITCH ANGLE(DEGREES)AT 2-25(LONG WORD) 2
FUEW	  EQU	$000014DA L 001 L S21  NORTH/SOUTH GROUND VEL.(FT/SEC)AT 2-21,2
FVCAL	  EQU	$000014DE L 001 L S22  INDICATED AIRSPEED(FT/SEC)AT 2-22(LONG W
FVEW	  EQU	$000014E2 L 001 L S21  EAST/WEST GROUND VEL.(FT/SEC)AT 2-21,2-5
FVP	  EQU	$000014E6 L 001 L S22  TRUE AIRSPEED(FT/SEC)AT 2-22(LONG WORD 2
FVPGND	  EQU	$000014EA L 001 L S21  TRUE GROUND VELOCITY(FT/SEC)AT 2-21,2-5
FALPHDOT  EQU	$000014EE W 001 L S07  RATE OF CHANGE OF ANGLE OF ATTACK(DEG/SE
FAZA	  EQU	$000014F0 W 001 L S12  VERTICAL ACCELERATION (G'S) 2-12
FAZADOT   EQU	$000014F2 W 001 L S12  RATE OF CHANGE OF AZA/20  (G'S/SEC) AT 2
FBETADOT  EQU	$000014F4 W 001 L S07  RATE OF CHANGE OF SIDE SLIP ANGLE(DEG/SE
FCG	  EQU	$000014F6 W 001 L S09  CENTER OF GRAVITY(%)AT 2-9
FCL	  EQU	$000014F8 W 001 L S13  LIFT COEFFICIENT AT 2-13
FCOSALPH  EQU	$000014FA W 001 L S15  COSINE OF ANGLE OF ATTACK AT 2-31(LONG)2
FCOSPHI   EQU	$000014FC W 001 L S15  COSINE OF BANK ANGLE AT 2-31(LONG)2-15(W
FCOSPSI   EQU	$000014FE W 001 L S15  COSINE OF HEADING ANGLE AT 2-31(LONG)2-1
FCOSPSIW  EQU	$00001500 W 001 L S15  COSINE CORRECTED WIND DIRECTION(FROM)AT
FCOSTHET  EQU	$00001502 W 001 L S15  COSINE OF PITCH ANGLE AT 2-31(LONG) 2-15
FDELAIL   EQU	$00001504 W 001 L S14  AILERON DEFLECTION(NORMALIZED,POS=LEFT W
FDELEV	  EQU	$00001506 W 001 L S14  ELEVATOR DEFLECTION (NORMALIZED,POS=NOSE
FDELRUD   EQU	$00001508 W 001 L S14  RUDDER DEFLECTION(NORMALIZED,POS=NOSE LE
FDELTATK  EQU	$0000150A W 001 L S09  AIR TEMP DELTA TK (DEG C)AT 2-9
FELTRIM   EQU	$0000150C W 001 L S14  STAB TRIM(NORMALIZED)AT 2-14
FFLAPS	  EQU	$0000150E W 001 L S14  FLAP POSITION(NORMALIZED)AT 2-14
FFXA	  EQU	$00001510 W 001 L S03  AERO DRAG(QSCD)(LBS)AT 2-3
FFZA	  EQU	$00001512 W 001 L S02  VERTICAL FORCE(LBS)DOWN IS POSITIVE AT 2
FGAMMA	  EQU	$00001514 W 001 L S10  FLIGHT PATH ELEVATION ANGLE(DEG)AT 2-10
FHDOT	  EQU	$00001516 W 001 L S06  RATE OF CLIMB(FT/SEC)AT 2-6
FICOSPHI  EQU	$00001518 W 001 L S15  COSINE(INDICATER BANK ANGLE)AT 2-15
FISINPHI  EQU	$0000151A W 001 L S15  SINE(INDICATER BANK ANGLE)AT 2-15
FLAMDOT   EQU	$0000151C W 001 L S08  RATE OF CHANGE OF LAMBDA(DEG/SEC)AT 2-8
FMPX	  EQU	$0000151E W 001 L S01  MULTIPLEX COUNTER AT 2-1
FPADOT	  EQU	$00001520 W 001 L S06  ROLL RATE DOT(DEG/SEC. SQ)AT 2-6
FPBAR	  EQU	$00001522 W 001 L S10  TOTAL BARO PRESSURE(INCHES)AT 2-10
FPSIDOT   EQU	$00001524 W 001 L S08  RATE OF CHANGE OF HEADING(DEG/SEC)AT 2-8
FQ	  EQU	$00001526 W 001 L S06  DYNAMIC PRESSURE (LB/FT SQ)AT 2-6
FQADOT	  EQU	$00001528 W 001 L S07  PITCH RATE DOT(DEG/SEC SQ)AT 2-7
FPHIDOT   EQU	$0000152A W 001 L S08  RATE OF CHANGE OF BANK ANGLE(DEG/SEC)AT
FRADOT	  EQU	$0000152C W 001 L S07  TURN RATE DOT(DEG/SEC SQ)AT 2-7
FRAND	  EQU	$0000152E W 001 L S15  RANDOM NUMBER AT 2-15
FRUFAIR   EQU	$00001530 W 001 L S06  VP*ROUGH AIR AT 2-6
FSINALPH  EQU	$00001532 W 001 L S15  SINE OF ANGLE OF ATTACK AT 2-15
FSINGAMA  EQU	$00001534 W 001 L S15  SINE FLIGHT PATH ELEV ANGLE AT 2-15
FSINPHI   EQU	$00001536 W 001 L S15  SINE BANK ANGLE AT 2-15
FSINPSI   EQU	$00001538 W 001 L S15  SINE HEADING ANGLE AT 2-15
FSINPSIW  EQU	$0000153A W 001 L S15  SINE CORRECTED WIND DIRECTION(FROM)AT 2-
FSINTHET  EQU	$0000153C W 001 L S15  SINE PITCH ANGLE AT 2-15
FTHETDOT  EQU	$0000153E W 001 L S08  RATE OF CHANGE OF PITCH ANGLE(DEG/SEC)AT
FTIMER10  EQU	$00001540 W 001 L S00  POWER UP TIMER AT 2-0
FTIMER50  EQU	$00001542 W 001 L S00  INITIALIZATION TIMER AT 2-0
FVPDOT	  EQU	$00001544 W 001 L S08  RATE OF CHANGE OF TRUE AIRSPEED(FT/SEC S
FVPINV	  EQU	$00001546 W 001 L S04  1845/VP AT 2-4
FVW	  EQU	$00001548 W 001 L S08  WIND VELOCITY(KNOTS)AT 2-8
FWG	  EQU	$0000154A W 001 L S03  GROSS WEIGHT(LBS)AT 2-3
FWGINV	  EQU	$0000154C W 001 L S17  1/WT AT 2-17
FWOW	  EQU	$0000154E W 001 L S12  WT ON WHEELS(G'S)AT 2-12
FBAROPR   EQU	$00001550 W 001 L S15  INST BARO PRESS	(A/I) +10V=31,-1029(IN
FRAAMPL   EQU	$00001552 W 001 L S15  INST ROUGH AIR MAGNITUDE  (A/I) +10V=MAX
FENGVOL   EQU	$00001554 W 001 L S15  INST ENG SOUND VOLUME	 (A/I) 0V=MAX,+
FFUELQR   EQU	$00001556 W 001 L S15  INST FUEL QTY (RIGHT)	 (A/I) +10V=MAX
FFUELQL   EQU	$00001558 W 001 L S15  INST FUEL QTY (LEFT)	 (A/I) +10V=MAX
FCYLTP	  EQU	$0000155A W 001 L S15  INST CLL TEMP		 (A/I) +10V=MAX
FOILTP	  EQU	$0000155C W 001 L S15  INST OIL TEMP		 (A/I) +10V=MAX
FOILPR	  EQU	$0000155E W 001 L S15  INST OIL PRESS		 (A/I) +10V=MAX
FSPARE0   EQU	$00001560 W 001 L S15  INST SPARE		 (A/I)
FSPARE1   EQU	$00001562 W 001 L S15  INST SPARE		 (A/I)
FWINDVEL  EQU	$00001564 W 001 L S15  INST WIND VELOCITY	 (A/I) +10V=100
FWINDC	  EQU	$00001566 W 001 L S15  INST COS WIND DIRECTION	 (A/I) +10V=COS
FWINDS	  EQU	$00001568 W 001 L S15  INST SINE WIND DIRECTION  (A/I) +10V=SIN
FOAT	  EQU	$0000156A W 001 L S15  INST OUTSIDE AIR TEMP  (A/I) +10V=50,-10
FGRWT	  EQU	$0000156C W 001 L S15  INST GROSS WT INPUT(A/I) +10V=2100,0V=16
FCGIN	  EQU	$0000156E W 001 L S15  INST CG INPUT	 (A/I) +10V=35,0V=25,-1
FALTFRZ   EQU	$00001570 B 001 L H    1=FREEZE ALTITUDE
FALTSDN   EQU	$00001571 B 001 L H    1=SLEW ALTITUDE DOWN
FALTSUP   EQU	$00001572 B 001 L H    1=SLEW ALTITUDE UP
FBFIELD   EQU	$00001573 B 001 L H    1=B FIELD SELECTED
FBRAKE	  EQU	$00001574 B 001 L H    1=PARK BRAKE APPLIED
FFREEZE   EQU	$00001575 B 001 L H    1=FLIGHT FREEZE
FINIT	  EQU	$00001576 B 001 L H    1=LOAD INITIALIZED
FMODE1	  EQU	$00001577 B 001 L H    1=MODE 1 (PA,RA,BETA,PHI DOT,PHI,PSI DOT
FMODE2	  EQU	$00001578 B 001 L H    1=MODE 2 (ALTITUDE HOLD COMMAND)
FMODE3	  EQU	$00001579 B 001 L H    1=MODE 3 (BANK HOLD)
FSTALL	  EQU	$0000157A B 001 L H    1=STALLED
FWOG	  EQU	$0000157B B 001 L H    1=ON GROUND
~i	 INCLUDE   NSYM.SA
NADF1FRQ  EQU	$00001594 L 001 L H   ADF 1 FREQUENCY
NADF1STA  EQU	$00001598 L 001 L H   ADF 1 STATION BUFFER ADDRESS
NA1IDNT1  EQU	$0000159C L 001 L H   ADF 1 IDENT WORD 1
NA1IDNT2  EQU	$000015A0 L 001 L H   ADF 1 IDENT WORD 2
NADF2FRQ  EQU	$000015A4 L 001 L H   ADF 2 FREQUENCY
NADF2STA  EQU	$000015A8 L 001 L H   ADF 2 STATION BUFFER ADDRESS
NA2IDNT1  EQU	$000015AC L 001 L H   ADF 2 IDENT WORD 1
NA2IDNT2  EQU	$000015B0 L 001 L H   ADF 2 IDENT WORD 2
NAV1FREQ  EQU	$000015B4 L 001 L H   NAV 1 FREQUENCY		    HZ
NAV2FREQ  EQU	$000015B8 L 001 L H   NAV 2 FREQUENCY		    HZ
NAV1STA   EQU	$000015BC L 001 L H   NAV 1 STATION BUFFER ADDRESS
NAV2STA   EQU	$000015C0 L 001 L H   NAV 2 STATION BUFFER ADDRESS
NDIST2	  EQU	$000015C4 L 001 L S24 DISTANCE FROM CLOSEST FIELD   DEG2
NGLAT	  EQU	$000015C8 L 001 L S23 AIRCRAFT LATITUDE 	    DEG       +
NGLON	  EQU	$000015CC L 001 L S23 AIRCRAFT LONGITUDE	    DEG       +
NV1IDNT1  EQU	$000015D0 L 001 L H   NAV 1 IDENT WORD 1
NV1IDNT2  EQU	$000015D4 L 001 L H   NAV 1 IDENT WORD 2
NV2IDNT1  EQU	$000015D8 L 001 L H   NAV 2 IDENT WORD 1
NV2IDNT2  EQU	$000015DC L 001 L H   NAV 2 IDENT WORD 2
ND1IDNT1  EQU	$000015E0 L 001 L H   DME 1 IDENT WORD 1
ND1IDNT2  EQU	$000015E4 L 001 L H   DME 1 IDENT WORD 2
ND2IDNT1  EQU	$000015E8 L 001 L H   DME 2 IDENT WORD 1
ND2IDNT2  EQU	$000015EC L 001 L H   DME 2 IDENT WORD 2
NADF1ERR  EQU	$000015F0 L 001 L H   ADF 1 FREQUENCY ERROR	    HZ	      +
NADF2ERR  EQU	$000015F4 L 001 L H   ADF 2 FREQUENCY ERROR	    HZ	      +
NRAGAS	  EQU	$000015F8 L 001 L S08 DIST ALONG RNWY HEADING	    FEET      +
NRBGAS	  EQU	$000015FC L 001 L S08 DIST ACROSS RNWY HEADING	    FEET      +
NRGAS2	  EQU	$00001600 L 001 L S11 GROUND RANGE TO STATION SQUARED N.M.**2 +
NRRAS2	  EQU	$00001604 L 001 L S11 SLANT RANGE TO STATION SQUARED N.M.**2  +
NSACF	  EQU	$00001608 L 001 L H   CLOSEST AIRPORT ADDRESS
NMKRSA	  EQU	$0000160C L 001 L H   ADDRESS OF NEAREST MARKER BEACON
NDME1STA  EQU	$00001610 L 001 L H   DME1 STATION ADDRESS
NDME2STA  EQU	$00001614 L 001 L H   DME2 STATION ADDRESS
NAPTPSA   EQU	$00001618 L 001 L H   NEAREST AIRPORT PRESELECT STATION ADDR
NAPTRNG2  EQU	$0000161C L 001 L S24 NEAREST AIRPORT RANGE SQUARED
NMKRRNG2  EQU	$00001620 L 001 L S24 NEAREST MARKER RANGE SQUARED
NTABNAV1  EQU	$00001624 L 001 L H   STATION ADDR OF GOOD STA
NTABNAV2  EQU	$00001628 L 001 L H   STATION ADDR OF GOOD STA
NCENLAT   EQU	$0000162C L 001 L S23 MAP CENTER LAT
NCENLON   EQU	$00001630 L 001 L S23 MAP CENTER LON
NRSLEW	  EQU	$00001634 L 001 L S23 CHART RECORDER SLEW RATE
NRXGASL   EQU	$00001638 L 001 L S23 NRXGASL IS DELTA LAT IN LONG WORD
NRYGASL   EQU	$0000163C L 001 L S23 NRYGASL IS DELTA LON IN LONG WORD
N3KBAND   EQU	$00001640 L 001 L H   3000 * ADF BAND NUMBER
NENGFREQ  EQU	$00001644 L 001 L H
NSPARE1   EQU	$00001648 L 001 L H   NAV SPARE LONG WORD 1
NSPARE2   EQU	$0000164C L 001 L H   NAV SPARE LONG WORD 2
NSPARE3   EQU	$00001650 L 001 L H   NAV SPARE LONG WORD 3
NSPARE4   EQU	$00001654 L 001 L H   NAV SPARE LONG WORD 4
NSPARE5   EQU	$00001658 L 001 L H   NAV SPARE LONG WORD 5
NSPARE6   EQU	$0000165C L 001 L H   NAV SPARE LONG WORD 6
NSPARE7   EQU	$00001660 L 001 L H   NAV SPARE LONG WORD 7
NSPARE8   EQU	$00001664 L 001 L H   NAV SPARE LONG WORD 8
NADF1BRG  EQU	$00001668 W 001 L S06 ADF MAG BEARING TO STATION    DEG
NADF1CTR  EQU	$0000166A W 001 L H   ADF TONE KFG COUNTER	    .1 SEC
NADF1DEV  EQU	$0000166C W 001 L S06 ADF 1 DEVIATION		    DEG
NADF1RNG  EQU	$0000166E W 001 L S00 ADF 1 STATION RANGE	    NAUTICAL MI
NADF2RNG  EQU	$00001670 W 001 L S00 ADF 2 STATION RANGE	    NAUTICAL MI
NADF2BRG  EQU	$00001672 W 001 L S06 ADF 2 MAG BEARING TO STATION  DEG
NADF2CTR  EQU	$00001674 W 001 L H   ADF 2 TONE KFG COUNTER	    .1 SEC
NADF2DEV  EQU	$00001676 W 001 L S06 ADF 2 DEVIATION		    DEG
NAV1BRG   EQU	$00001678 W 001 L S06 NAV 1 BEARING TO STATION	    DEG
NAV2BRG   EQU	$0000167A W 001 L S06 NAV 2 BEARING TO STATION	    DEG
NAV1CTR   EQU	$0000167C W 001 L H   NAV 1 KFG COUNTER 	    .1 SEC
NAV2CTR   EQU	$0000167E W 001 L H   NAV 2 KFG COUNTER 	    .1 SEC
NAV1OBS   EQU	$00001680 W 001 L S06 NAV 1 OMNIBEARING SELECT	    DEG
NAV2OBS   EQU	$00001682 W 001 L S06 NAV 2 OMNIBEARING SELECT	    DEG
NCDI1DEG  EQU	$00001684 W 001 L S06 NAV 1 COURSE DEVIATION	    DEG
NCDI2DEG  EQU	$00001686 W 001 L S06 NAV 2 COURSE DEVIATION	    DEG
NGCLAT	  EQU	$00001688 W 001 L S15 COSINE OF LATITUDE
NGCLATIN  EQU	$0000168A W 001 L S07 INVERSE OF NGCLAT
NGFE	  EQU	$0000168C W 001 L S00 FIELD ELEVATION		    FEET
NGGA	  EQU	$0000168E W 001 L S00 NAV GEOMETRIC ALTITUDE	    FEET
NGS	  EQU	$00001690 W 001 L S04 NAV GROUND SPEED		    KNOTS     +
NHRA	  EQU	$00001692 W 001 L S00 NAV HEIGHT ABOVE GROUND	    FEET      +
NGSD1DEG  EQU	$00001694 W 001 L S06 AIRCRAFT GLIDESLOPE	    DEG       +
NIMCTR	  EQU	$00001696 W 001 L H   IM COUNTER		    .1SEC     +
NMAGVAR   EQU	$00001698 W 001 L S06 MAGNETIC VARIATION	    DEG       +
NMMCTR	  EQU	$0000169A W 001 L H   MM COUNTER KFG		    .1SEC     +
NOMCTR	  EQU	$0000169C W 001 L H   OM COUNTER KFG		    .1SEC     +
NPSIGA	  EQU	$0000169E W 001 L S06 NAV AIRCRAFT TRUE HEADING      DEG      +
NPSIMH	  EQU	$000016A0 W 001 L S06 NAV AIRCRAFT MAG HEADING	     DEG      +
NPSIIH	  EQU	$000016A2 W 001 L S06 NAV AIRCRAFT INDICATED HEADING DEG      +
NUEW	  EQU	$000016A4 W 001 L S04 N-S VELOCITY WITH WIND	     FPS      +
NVEW	  EQU	$000016A6 W 001 L S04 E-W VELOCITY WITH WIND	     FPS      +
NADF1RAW  EQU	$000016A8 W 001 L H   ADF N-1 RAW FREQUENCY		      +
NRASIN	  EQU	$000016AA W 001 L S15 SINE OF AVERAGE LATITUDE		      +
NRACOS	  EQU	$000016AC W 001 L S15 COSINE OF AVERAGE LATITUDE	      +
NRXGAS	  EQU	$000016AE W 001 L S12 DELTA LAT BETWEEN STATION & A/C DEG     +
NRYGAS	  EQU	$000016B0 W 001 L S12 DELTA LON BETWEEN STATION & A/C DEG     +
NRBRG	  EQU	$000016B2 W 001 L S06 MAGNETIC BEARING TO STATION   DEG       +
NRRGAS	  EQU	$000016B4 W 001 L S06 GROUND RANGE TO STATION	    N.M.      +
NRZGAS	  EQU	$000016B6 W 001 L S00 HEIGHT ABOVE STATION	    FEET      +
NRRRAS	  EQU	$000016B8 W 001 L S06 SLANT RANGE TO STATION	    N.M.      +
NGBIAS	  EQU	$000016BA W 001 L S06 GROUND BIAS
NSPSIB	  EQU	$000016BC W 001 L S15 SIN BIAS HEADING
NCPSIB	  EQU	$000016BE W 001 L S15 COS BIAS HEADING
NGVNS	  EQU	$000016C0 W 001 L S04 N-S GROUND VELOCITY
NGVEW	  EQU	$000016C2 W 001 L S04 E-W GROUND VELOCITY
NDME1CTR  EQU	$000016C4 W 001 L H   DME1 COUNTER
NDME2CTR  EQU	$000016C6 W 001 L H   DME2 COUNTER
NVTC1CTR  EQU	$000016C8 W 001 L H   VTC 1 COUNTER
NVTC2CTR  EQU	$000016CA W 001 L H   VTC 2 COUNTER
NK1	  EQU	$000016CC W 001 L H   CONSTANT USED IN NAV1
NK2	  EQU	$000016CE W 001 L H   CONSTANT USED IN NAV1
NK3	  EQU	$000016D0 W 001 L H   CONSTANT USED IN NAV1
NE	  EQU	$000016D2 W 001 L H   TEMP STORE USED IN NAV1
NCRS1COS  EQU	$000016D4 W 001 L S15 NAV 1 SEL COURSE COSINE
NCRS1SIN  EQU	$000016D6 W 001 L S15 NAV 1 SEL COURSE SINE
NCRS2COS  EQU	$000016D8 W 001 L S15 NAV 2 SEL COURSE COSINE
NCRS2SIN  EQU	$000016DA W 001 L S15 NAV 2 SEL COURSE SINE
NSYNCS	  EQU	$000016DC W 001 L S15 COMPASS SYNC POT SINE
NSYNCC	  EQU	$000016DE W 001 L S15 COMPASS SYNC POT COSINE
NSYNCDEG  EQU	$000016E0 W 001 L S06 COMPASS SYNC POT DEGREES
NRSCALE   EQU	$000016E2 W 001 L S00 CHART RECORDER SCALE
USETHD	  EQU	$000016E4 W 001 L S06 SET NEW HEADING
NCONRAD   EQU	$000016E6 W 001 L S06 CONE OF CONFUSION RADIUS IN NM	      +
NAV1GS	  EQU	$000016E8 W 001 L S15 NAV1 GLIDE SLOPE AO		      +
NAV2GS	  EQU	$000016EA W 001 L S15 NAV2 GLIDE SLOPE AO		      +
NAV1LR	  EQU	$000016EC W 001 L S15 NAV1 CDI NEEDLE AO		      +
NAV2LR	  EQU	$000016EE W 001 L S15 NAV2 CDI NEEDLE AO		      +
NGSTAN	  EQU	$000016F0 W 001 L S12 GLIDE SLOPE TAN
NBASEREG  EQU	$000016F2 W 001 L S06 BASE REGISTER FOR NATAN2 ROUTINE
NITCOUNT  EQU	$000016F4 W 001 L H   ITERATION COUNTER
NSPAREW1  EQU	$000016F6 W 001 L H   NAV SPARE WORD 1
NSPAREW2  EQU	$000016F8 W 001 L H   NAV SPARE WORD 2
NSPAREW3  EQU	$000016FA W 001 L H   NAV SPARE WORD 3
NSPAREW4  EQU	$000016FC W 001 L H   NAV SPARE WORD 4
NSPAREW5  EQU	$000016FE W 001 L H   NAV SPARE WORD 5
NSPAREW6  EQU	$00001700 W 001 L H   NAV SPARE WORD 6
NSPAREW7  EQU	$00001702 W 001 L H   NAV SPARE WORD 7
NSPAREW8  EQU	$00001704 W 001 L H   NAV SPARE WORD 8
NICOM1SQ  EQU	$00001706 W 001 L H   TEMP STORE OF ICOM1SQ INPUT
NICOM2SQ  EQU	$00001708 W 001 L H   TEMP STORE OF ICOM2SQ INPUT
NMKRLVL   EQU	$0000170A B 001 L H   MARKER LEVEL
NLF1LOS   EQU	$0000170B B 001 L H   ADF 1 IN LINE OF SIGHT
NADF1TUN  EQU	$0000170C B 001 L H   ADF 1 STATION ID ENABLE
NADF2TUN  EQU	$0000170D B 001 L H   ADF 2 STATION ID ENABLE
NAV1TUN   EQU	$0000170E B 001 L H   NAV 1 STATION ID ENABLE
NAV2TUN   EQU	$0000170F B 001 L H   NAV 2 STATION ID ENABLE
NAVFRZ	  EQU	$00001710 B 001 L H   NAV FREEZE
NBWOW	  EQU	$00001711 B 001 L H   NAV WEIGHT ON WHEELS
NIMTONE   EQU	$00001712 B 001 L H   IM TONE ENABLE
NIN1GSF   EQU	$00001713 B 001 L H   NAV 1 GLIDESLOPE FLAG
NINIT	  EQU	$00001714 B 001 L H   NAV INITIALIZATION FLAG
NMMTONE   EQU	$00001715 B 001 L H   MM TONE ENABLE
NOMTONE   EQU	$00001716 B 001 L H   OM TONE ENABLE
NRWR1	  EQU	$00001717 B 001 L H   IN RANGE 1 FLAG
NRWR2	  EQU	$00001718 B 001 L H   IN RANGE 2 FLAG
NRWR3	  EQU	$00001719 B 001 L H   IN RANGE 3 FLAG
NRLOS	  EQU	$0000171A B 001 L H   IN LINE OF SIGHT FLAG
NMKRVL	  EQU	$0000171B B 001 L H   MARKER BEACON STATION VALIDITY
NDME1TUN  EQU	$0000171C B 001 L H   DME1 TUNED FLAG
NDME2TUN  EQU	$0000171D B 001 L H   DME2 TUNED FLAG
NIMKEY	  EQU	$0000171E B 001 L H   INNER MARKER KEY
NAMRA	  EQU	$0000171F B 001 L H   INNER MARKER RANGE
NMMRA	  EQU	$00001720 B 001 L H   MIDDLE MARKER RANGE
NOMRA	  EQU	$00001721 B 001 L H   OUTER MARKER RANGE
NVTC1	  EQU	$00001722 B 001 L H
NVTC2	  EQU	$00001723 B 001 L H
NAV1VOL   EQU	$00001724 B 001 L H   NAV1 VOLUME
NAV2VOL   EQU	$00001725 B 001 L H   NAV2 VOLUME
NLF1VOL   EQU	$00001726 B 001 L H
NCOM1OP   EQU	$00001727 B 001 L H   COMM1 OPERABLE FLAG
NCOM2OP   EQU	$00001728 B 001 L H   COMM2 OPERABLE FLAG
NMKRSNS   EQU	$00001729 B 001 L H   MARKER SENSE
NIPFLAG   EQU	$0000172A B 001 L H   INITIAL POS FLAG
UHDSET	  EQU	$0000172B B 001 L H   INITIAL POS HEADING FLAG
NMMKEY	  EQU	$0000172C B 001 L H   MIDDLE MARKER KEY
NOMKEY	  EQU	$0000172D B 001 L H   OUTER MARKER KEY
NAV1KY	  EQU	$0000172E B 001 L H   NAV 1 KEY
NAV2KY	  EQU	$0000172F B 001 L H   NAV 2 KEY
NDM1KY	  EQU	$00001730 B 001 L H   DME 1 KEY
NDM2KY	  EQU	$00001731 B 001 L H   DME 2 KEY
NLF1KY	  EQU	$00001732 B 001 L H   ADF 1 KEY
NLF2KY	  EQU	$00001733 B 001 L H   ADF 1 KEY
NNV1VOL   EQU	$00001734 B 001 L H   TEMP STORE OF ONV1VOL
NNV2VOL   EQU	$00001735 B 001 L H   TEMP STORE OF ONV2VOL
NCDI1DO   EQU	$00001736 B 001 L H   TEMP STORE OF OCDI1DO
NCDI2DO   EQU	$00001737 B 001 L H   TEMP STORE OF OCDI2DO
NDMECTL   EQU	$00001738 B 001 L H
NENGAMP   EQU	$00001739 B 001 L H
NPROPAMP  EQU	$0000173A B 001 L H
NRUMBLE   EQU	$0000173B B 001 L H   RUMBLE
NSTALL	  EQU	$0000173C B 001 L H   STALL WARNING
NCOM1SQ   EQU	$0000173D B 001 L H   TEMP STORE OF OCOM1SQ
NCOM2SQ   EQU	$0000173E B 001 L H   TEMP STORE OF OCOM2SQ
NCOM1VOL  EQU	$0000173F B 001 L H TEMP STORE OF OCOM1VOL
NCOM2VOL  EQU	$00001740 B 001 L H TEMP STORE OF OCOM2VOL
NASCONT1  EQU	$00001741 B 001 L H TEMP STORE OF OASCONT1
NASCONT2  EQU	$00001742 B 001 L H TEMP STORE OF OASCONT2
NASCONT3  EQU	$00001743 B 001 L H TEMP STORE OF OASCONT3
NMKRDOB   EQU	$00001744 B 001 L H TEMP STORE FOR OMKRDO
NDS1DO15  EQU	$00001745 B 001 L H TEMP STORE FOR ODS1DO15 ON C006-4
NSPAREB1  EQU	$00001746 B 001 L H   SPARE BYTE 1
NSPAREB2  EQU	$00001747 B 001 L H   SPARE BYTE 2
NSPAREB3  EQU	$00001748 B 001 L H   SPARE BYTE 3
NSPAREB4  EQU	$00001749 B 001 L H   SPARE BYTE 4
NSPAREB5  EQU	$0000174A B 001 L H   SPARE BYTE 5
NSPAREB6  EQU	$0000174B B 001 L H   SPARE BYTE 6
NSPAREB7  EQU	$0000174C B 001 L H   SPARE BYTE 7
NSPAREB8  EQU	$0000174D B 001 L H   SPARE BYTE 8
NSPAREB9  EQU	$0000174E B 001 L H   SPARE BYTE 9
NSPAREBA  EQU	$0000174F B 001 L H   SPARE BYTE 10
~i	 INCLUDE   IO.SA
OFUELLT      EQU    $00000205		LEFT FUEL QTY		 0V=0,+10V=25 G
OFUELRT      EQU    $00000209		RIGHT FUEL QTY		 0V=0,+10V=25 G
OOILP	     EQU    $00000211		OIL PRESSURE		 0V=0,+10V=100
OCYLT	     EQU    $00000215		CYL. HEAD TEMP. 	 0V=0,+10V=460
OOILT	     EQU    $00000201		OIL TEMP		 0V=0,+10V=260
*
ITHR1	     EQU    $00000221		THROTTLE		 +10V=FULL,+4.6
IMIX1	     EQU    $00000225		MIXTURE 		 +3.3V=FULL RIC
ICARBH1      EQU    $00000229		CARB. HEAT		 -3.3V=ON,-10V=
IPROP1	     EQU    $00000231		PROP.			 -4.93V=MAX,-10
*
IELEV	     EQU    $00000241		ELEVATOR CONTROL	  +7.33V=UP,-7V
IAIL	     EQU    $00000245		AILERON CONTROL 	  +6.5V=RT,-6.5
IRUD	     EQU    $00000249		RUDDER CONTROL		  +3.5V=RT,-3.5
IELEVT	     EQU    $00000251		ELEVATOR TRIM		  +7V=UP,-2.3V=
*
OTURNP	     EQU    $00000261		TURN NEEDLE		  +10V=6 NW RT,
OBALLP	     EQU    $00000265		BALL			  +10V=1.6 BW R
*
OIRCP	     EQU    $00000271		R/C			  +10V=2000 FT
*
OALTMP	     EQU    $00000281		ALTIMETER		  0V=-1000 FT,-
*
OIASP	     EQU    $00000291		INDICATED AIRSPEED	  0V=0 MPH,+10V
*
OIRPM	     EQU    $000002A1		RPM			  0V=0 RPM,+10V
*
OIMAP1	     EQU    $000002B1		MAP			  0V=29.92 IN H
*
OATTPIT      EQU    $000002C9		ATTITUDE PITCH		  +10V=15 DEG U
OATTROLS     EQU    $000002C5		ATTITUDE ROLL SIN	  +10V=SIN 90 D
OATTROLC     EQU    $000002C1		ATTITUDE ROLL COS	  +10V=COS 0 DE
*
IMPSDI1      EQU    $000002D1		PANEL SWITCHES #1
IMSTSW	     EQU    7			MASTER SWITCH ON
IIGNST	     EQU    6			IGNITION SWITCH START
IIGNBT	     EQU    5			IGNITION SWITCH BOTH
IIGNRT	     EQU    4			IGNITION SWITCH RIGHT
IIGNLT	     EQU    3			IGNITION SWITCH LEFT
IRMI1ADF     EQU    2			RMI #1 PTR ADF
ILINKPWR     EQU    0			1=LINKAGE POWER ON
IMPSDI2      EQU    $000002D3		PANEL SWITCHES #2
IPARKBK      EQU    7			PARKING BRAKE ON
IFLAPUP      EQU    6			FLAP SWITCH UP
IFLAPHLF     EQU    5			FLAP SWITCH 1/2
IFLAPDN      EQU    4			FLAP SWITCH DOWN
IMONNAV1     EQU    3			MONITOR NAV1
IMONNAV2     EQU    2			MONITOR NAV2
IMONADF1     EQU    1			MONITOR ADF1
IXRCOM1      EQU    0			XMIT/REC COMM1
*
INAV1F1      EQU    $0000040D		NAV1 FREQ WORD 1
I117NAV1     EQU    7			NAV1 117
I116NAV1     EQU    6			NAV1 116
I115NAV1     EQU    5			NAV1 115
I114NAV1     EQU    4			NAV1 114
I113NAV1     EQU    3			NAV1 113
I112NAV1     EQU    2			NAV1 112
I111NAV1     EQU    1			NAV1 111
I110NAV1     EQU    0			NAV1 110
INAV1F2      EQU    $00000411		NAV1 FREQ WORD 2
I109NAV1     EQU    7			NAV1 109
I108NAV1     EQU    6			NAV1 108
I9THNAV1     EQU    5			NAV1 .9
I8THNAV1     EQU    4			NAV1 .8
I7THNAV1     EQU    3			NAV1 .7
I6THNAV1     EQU    2			NAV1 .6
I5THNAV1     EQU    1			NAV1 .5
I4THNAV1     EQU    0			NAV1 .4
INAV1F3      EQU    $00000413		NAV1 FREQ WORD 3
I3THNAV1     EQU    7			NAV1 .3
I2THNAV1     EQU    6			NAV1 .2
I1THNAV1     EQU    5			NAV1 .1
I0THNAV1     EQU    4			NAV1 .0
INAV1ILS     EQU    3			NAV1 ILS SELECTED
INAV1ON      EQU    2			NAV1 POWER ON
INAV1ID      EQU    1			NAV1 ID ON
INAV1VOL     EQU    $00000401		NAV1 VOLUME
ICOM1SQ      EQU    $00000409		COMM1 SQUELCH
ICOM1VOL     EQU    $00000405		COMM1 VOLUME
*
INAV2F1      EQU    $0000042D		NAV2 FREQ WORD 1
I117NAV2     EQU    7			NAV2 117
I116NAV2     EQU    6			NAV2 116
I115NAV2     EQU    5			NAV2 115
I114NAV2     EQU    4			NAV2 114
I113NAV2     EQU    3			NAV2 113
I112NAV2     EQU    2			NAV2 112
I111NAV2     EQU    1			NAV2 111
I110NAV2     EQU    0			NAV2 110
INAV2F2      EQU    $00000431		NAV2 FREQ WORD 2
I109NAV2     EQU    7			NAV2 109
I108NAV2     EQU    6			NAV2 108
I9THNAV2     EQU    5			NAV2 .9
I8THNAV2     EQU    4			NAV2 .8
I7THNAV2     EQU    3			NAV2 .7
I6THNAV2     EQU    2			NAV2 .6
I5THNAV2     EQU    1			NAV2 .5
I4THNAV2     EQU    0			NAV2 .4
INAV2F3      EQU    $00000433		NAV2 FREQ WORD 3
I3THNAV2     EQU    7			NAV2 .3
I2THNAV2     EQU    6			NAV2 .2
I1THNAV2     EQU    5			NAV2 .1
I0THNAV2     EQU    4			NAV2 .0
INAV2ILS     EQU    3			NAV2 ILS SELECTED
INAV2ON      EQU    2			NAV2 POWER ON
INAV2ID      EQU    1			NAV2 ID ON
INAV2VOL     EQU    $00000421		NAV2 VOLUME
ICOM2SQ      EQU    $00000429		COMM2 SQUELCH
ICOM2VOL     EQU    $00000425		COMM2 VOL
*
OADF1TM      EQU    $00000449		ADF1 TUNING METER
IADF1VOL     EQU    $00000441		ADF1 VOLUME
IADF1FRQ     EQU    $00000445		ADF1 FREQ
IADF1DI      EQU    $0000044D		ADF1 CONTROL DISCRETE IN
IADF1B1      EQU    7			ADF BAND 1 190 TO 430
IADF1B2      EQU    6			ADF BAND 2 420 TO 850
IADF1B3      EQU    5			ADF BAND 3 840 TO 1750
IADF1TST     EQU    4			ADF1 TEST
IADF1REC     EQU    3			ADF1 RECEIVE MODE
IADF1ADF     EQU    2			ADF1 ADF MODE
IADF1MKR     EQU    1			ADF1 MARKER ON
*
ORMIHS	     EQU    $000004B5		RMI SIN MAG HEADING
ORMIHC	     EQU    $000004B1		RMI COS MAG HEADING
*
ONAV1LR      EQU    $00000451		NAV1 COURSE DEVIATION
ONAV1GS      EQU    $00000459		NAV1 GLIDE SLOPE DEV.
ONAV1TF      EQU    $00000455		NAV1 TO/OFF/FROM
ICRS1SC      EQU    $00000461		COURSE SEL 1 SIN/COS
OCDI1DO      EQU    $00000465		CDI 1 DISCRETE OUT
OCRS1SEL     EQU    0			COURSE SEL 1 EXCITATION
ONAV1GSF     EQU    1			NAV1 G/S FLAG 00V
ICRS1DI      EQU    $00000467		CDI 1 DISCRETE IN
INAV1TST     EQU    7			NAV 1 TEST
*
ONAV2LR      EQU    $00000471		NAV2 COURSE DEVIATION
ONAV2GS      EQU    $00000479		NAV2 GLIDE SLOPE DEV.
ONAV2TF      EQU    $00000475		NAV2 TO/OFF/FROM
ICRS2SC      EQU    $00000481		COURSE SEL 2 SIN/COS
OCDI2DO      EQU    $00000485		CDI 2 DISCRETE OUT
OCRS2SEL     EQU    0			COURSE SEL 2 EXCITATION
ONAV2GSF     EQU    1			NAV2 G/S FLAG 00V
ICRS2DI      EQU    $00000487		CDI 2 DISCRETE IN
INAV2TST     EQU    7			NAV 2 TEST
*
ORMI1S	     EQU    $000004A9		RMI SIN #1 PTR
ORMI1C	     EQU    $000004A5		RMI COS #1 PTR
ORMI2S	     EQU    $000004A1		RMI SIN #2 PTR
ORMI2C	     EQU    $00000499		RMI COS #2 PTR
*
IINSTAI1     EQU    $00000601		INSTRUCTOR #1 AI
IINSTAI2     EQU    $00000605		INSTRUCTOR #2 AI
IAI1MPX      EQU    $00000609		MULTIPLEXER #1
IAI2MPX      EQU    $0000060B		MULTIPLEXER #2
IINSTDI1     EQU    $0000061B		INSTRUCTOR #1 DI
IBFIELD      EQU    7			B FIELD SELECTED
IENGFAIL     EQU    3			ENGINE FAIL
IPITON	     EQU    2			PITCH MOTION ON
IROLLON      EQU    1			ROLL MOTION ON
IYAWON	     EQU    0			YAW MOTION ON
IFAIL1	     EQU    $00000619		INSTRUCTER FAIL #1
IDGFAIL      EQU    7			DG FAIL
IATGFAIL     EQU    6			ATTITUDE IND FAIL
IIASFAIL     EQU    5			AIRSPEED IND FAIL
IALTFAIL     EQU    4			ALTIMETER FAIL
IRCFAIL      EQU    3			RATE OF CLIMB IND FAIL
ITNFAIL      EQU    2			TURN NEEDLE FAIL
IVL2FAIL     EQU    1			VOR/LOC 2  FAIL
IVL1FAIL     EQU    0			VOR/LOC 1  FAIL
IRCDRDI      EQU    $0000061D		RECORDER DI
ISLEWN	     EQU    7			SLEW NORTH
ISLEWS	     EQU    6			SLEW SOUTH
ISLEWE	     EQU    5			SLEW EAST
ISLEWW	     EQU    4			SLEW WEST
IAPPR2	     EQU    3			APPROACH 2 SELECTED
IAPPR1	     EQU    2			APPROACH 1 SELECTED
ICCMODE      EQU    1			CROSS COUNTRY SELECTED
IPOSFRZ      EQU    0			POSITION FREEZE
ORCDNSD      EQU    $00000611		(A/0)+10V=7.5 IN NORTH,-10V=7.5 IN SOUT
ORCDEWD      EQU    $00000615		(A/0)+10V=7.5 IN EAST,-10V=7.5 IN WEST
IFAIL2	     EQU    $0000061F		INSTRUCTER FAIL #2
IGSFAIL      EQU    7			GLIDESLOPE FAIL
ILF1FAIL     EQU    6			ADF FAIL
*
OMOTPITR     EQU    $00000621		MOTION PITCH RATE     +10V=30 DEG/SEC U
OMOTROLR     EQU    $00000625		MOTION ROLL RATE      +10V=60 DEG/SEC R
OMOTTRNR     EQU    $00000629		MOTION TURN RATE      +10V=30 DEG/SEC R
ICPHDGS      EQU    $00000639		SIN COCKPIT HEADING   +10V=SINE 90 DEG
ICPHDGC      EQU    $0000063D		COS COCKPIT HEADING   +10V=COS 0 DEG
ICPPITS      EQU    $00000631		SIN COCKPIT PITCH     +10V=SIN 90 DEG
ICPROLS      EQU    $00000635		SIN COCKPIT ROLL      +10V=SIN 90 DEG
IINSTMDI     EQU    $0000062D		INSTRUCTER CONTROL DI	  CARD A81
IALTFRZ      EQU    7			ALTITUDE FREEZE
IALTSUP      EQU    6			ALTITUDE SLEW UP
IALTSDN      EQU    5			ALTITUDE SLEW DOWN
IFLTFRZ      EQU    4			FLIGHT FREEZE
IENGFIR      EQU    3			ENGINE FIRED BYPASS
IMODE1	     EQU    2			MODE 1
IMODE2	     EQU    1			MODE 2
IMODE3	     EQU    0			MODE 3
*
ONV1RNKY     EQU    $00000651		NAV1 RNG ATT/KEY
ONV1WNL      EQU    $00000655		NAV1 WHITE NOISE LEVEL
ODM1RNKY     EQU    $00000659		DME1 RNG ATT/KEY
ONV1VOL      EQU    $0000065D		NAV1 VOLUME LEVEL
ODMECTL      EQU    $0000064D		NAV  DME CONTROL
ODM1SEP      EQU    7			NAV1 DME SEPARATE
ODM2SEP      EQU    6			NAV2 DME SEPARATE
ONV2RNKY     EQU    $00000653		NAV2 RNG ATT/KEY
ONV2WNL      EQU    $00000657		NAV2 WHITE NOISE LEVEL
ODM2RNKY     EQU    $0000065B		DME2 RNG ATT/KEY
ONV2VOL      EQU    $0000065F		NAV2 VOLUME LEVEL
OLF1RNKY     EQU    $00000641		ADF1 RNG ATT/KEY
OLF1WNL      EQU    $00000645		ADF1 WHITE NOISE LEVEL
OLF1VOL      EQU    $00000649		ADF1 VOLUME LEVEL
OLF2RNKY     EQU    $00000643		ADF2 RNG ATT/KEY
OLF2WNL      EQU    $00000647		ADF2 WHITE NOISE LEVEL
OLF2VOL      EQU    $0000064B		ADF2 VOLUME LEVEL
*
OOMRAKY      EQU    $00000663		OM RANGE/KEY
OMMRAKY      EQU    $00000665		MM RANGE/KEY
OAMRAKY      EQU    $00000667		AM RANGE/KEY
OMKRTST      EQU    $00000669		MARKER TEST
OMKRVOL      EQU    $0000066B		MKR VOLUME
OMKRDO	     EQU    $00000661		MKR DISCRETE OUT
OOMFAIL      EQU    7			OM FAIL
OOMRAATT     EQU    6			OM RANGE ATT.
OMMFAIL      EQU    5			MM FAIL
OMMRAATT     EQU    4			MM RANGE ATT.
OAMFAIL      EQU    3			AM FAIL
OAMRAATT     EQU    2			AM RANGE ATT.
OMKRLTST     EQU    1			MKR LAMP TEST
*
OATTEN1      EQU    $0000068B		ATTEN 1 EXHAUST NOISE
OFUNCTA      EQU    $00000681		CARD 23 HIGH BYTE
OFUNCT1      EQU    $00000683		FUNCT 1 RPM
OFUNCT5      EQU    $00000685		FUNCT 5 SPARE
OATTEN5      EQU    $0000068D		ATTEN 5 SPARE
OFUNCT6      EQU    $00000687		FUNCT 6 STALL WARN
OATTEN6      EQU    $00000691		ATTEN 6 STALL WARN
OFUNCT8      EQU    $00000689		FUNCT 8 TIRE SCREECH
OATTEN8      EQU    $00000693		ATTEN 8 TIRE SCREECH
OATTEN9      EQU    $00000695		ATTEN 9 INST. VOL
OATTEN10     EQU    $00000697		ATTEN 10 PROP HISS
OATTEN11     EQU    $00000699		ATTEN 11 AERO HISS
*
OFUNCTB      EQU    $000006E1		CARD 24 HIGH BYTE
OFUNCT2      EQU    $000006E3		FUNCT 2 HISS
OATTEN2      EQU    $000006EB		ATTEN 2 HISS
OFUNCT3      EQU    $000006E5		FUNCT 3 RUMBLE
OATTEN3      EQU    $000006ED		ATTEN 3 RUMBLE
OFUNCT4      EQU    $000006E7		FUNCT 4 SPARE
OATTEN4      EQU    $000006F1		ATTEN 4 SPARE
OFUNCT7      EQU    $000006E9		FUNCT 7 AERO HISS
OATTEN7      EQU    $000006F3		ATTEN 7 AERO HISS
*
OCOM1SQ      EQU    $000006B1		COMM1 SQ CONTROL
OCOM1IVL     EQU    $000006B5		COMM1 INST. VOICE LEVEL
OC1SXMIT     EQU    3			COMM1 STUDENT XMIT
OCOM1VOL     EQU    $000006B9		COMM1 VOLUME LEVEL
OCOM2SQ      EQU    $000006B3		COMM2 SQ CONTROL
OCOM2IVL     EQU    $000006B7		COMM2 INST. VOICE LEVEL
OC2SXMIT     EQU    2			COMM2 STUDENT XMIT
OCOM2VOL     EQU    $000006BB		COMM2 VOLUME CONTROL
OSMIKEVL     EQU    $000006BD		STUDENT MIKE COMM LEVEL
IMICPTT      EQU    $000006CF		MIKE PTT
ISMICPTT     EQU    7			STUDENT MIKE PTT
IIMICPTT     EQU    6			INSTRUCTOR MIKE PTT
OSINPHVL     EQU    $000006C1		STUDENT INPH. LEVEL
OIINPHVL     EQU    $000006C3		INSTRUCTOR INPH. LEVEL
OSLISTVL     EQU    $000006C5		STUDENT VOLUME LEVEL
OASCONT1     EQU    $000006C9		AUDIO SELECT CONTROL 1
OSSCOM1N     EQU    7			STUDENT SEL COMM1 (NOT)
OSSCOM2N     EQU    6			STUDENT SEL COMM2 (NOT)
OSSINPHN     EQU    5			STUDENT SEL INPH.(NOT)
OSSMKRN      EQU    4			STUDENT SEL MARKER (NOT)
OSSNAV1N     EQU    3			STUDENT SEL NAV1   (NOT)
OSSNAV2N     EQU    2			STUDENT SEL NAV2   (NOT)
OSSADF1N     EQU    1			STUDENT SEL ADF1   (NOT)
OSSADF2N     EQU    0			STUDENT SEL ADF2   (NOT)
OASCONT2     EQU    $000006CB		AUDIO SELECT CONTROL 2
OSSDME1N     EQU    7			STUDENT SEL DME 1  (NOT)
OSSDME2N     EQU    6			STUDENT SEL DME 2  (NOT)
OSSSPA1N     EQU    5			STUDENT SEL SPARE  (NOT)
OSSSPA2N     EQU    4			STUDENT SEL SPARE  (NOT)
OILISTVL     EQU    $000006C7		INSTRUCTOR VOLUME LEVEL
OASCONT3     EQU    $000006CD		AUDIO SELECT CONTROL 3
OISCOM1N     EQU    7			INST. SEL COMM1   (NOT)
OISCOM2N     EQU    6			INST. SEL COMM2   (NOT)
OISSTN	     EQU    5			INST. SEL SIDE TONE (NOT)
OISMONTN     EQU    4			INST. SEL MONITOR   (NOT)
OISINPHN     EQU    3			INST. SEL INPH.     (NOT)
OISSPA1N     EQU    2			INST. SEL SPARE     (NOT)
OISSPA2N     EQU    1			INST. SEL SPARE     (NOT)
OISSPA3N     EQU    0			INST. SEL SPARE     (NOT)
*
ZFPDEG	 DC.W	   22780	       -04	 FEET PER DEG
ZDGPFT	 DC.W	   9		       +26	 1.37085*10-7 DEG/FT PER ITER.
ZLATMAX  DC.L	   12582912	       +23	 MAX LAT 1.5 DEG
ZLONMAX  DC.L	   12582912	       +23	 MAX LON 1.5 DEG
*
CCTAB	 DC.L	   00		       +23	 CROSS COUNTRY CENTER LAT
	 DC.L	   00		       +23	 CROSS COUNTRY CENTER LON
	 DC.L	   27962	       +23	 CROSS COUNTRY SLEW/ITERATION
	 DC.W	   64		       +06	 CC SCALE (10 VOLT/DEG)
*
AP1TAB	 DC.L	   -3143538	       +23	 APPR 1 CEN LAT SEAPORT BEACH
	 DC.L	   4200824	       +23	 APPR 1 CEN LON
	 DC.L	   8144 	       +23	 APPR 1 SLEW/ITERATION
	 DC.W	   220		       +06	 APPR 1 SCALE (10 VOLT/DEG)
*
AP2TAB	 DC.L	   3728694	       +23	 APPR 2 CEN LAT MOUNTAIN DALE
	 DC.L	   -4901338	       +23	 APPR 2 CEN LON
	 DC.L	   8144 	       +23	 APPR 2 SLEW/ITERATION
	 DC.W	   220		       +06	 APPR 2 SCALE (10 VOLT/DEG)
*
A1IPTAB  DC.L	   -3485586	       +23	 INITIAL POS. LAT SEAPORT BEACH
	 DC.L	   3671320	       +23	 INITIAL POS. LON
	 DC.W	   -4608	       +06	 INITIAL POS. HDG. -72 DEG.
*
A2IPTAB  DC.L	   4126117	       +23	 INITIAL POS. LAT MOUNTAIN DALE
	 DC.L	   -4819899	       +23	 INITIAL POS. LON
	 DC.W	   3392 	       +06	 INITIAL POS. HDG. 53 DEG.
*
NAV1	 EQU	   *
	 MOVE.L    #$FE6000,A6			 LOAD I/O BASE ADDR
*
	 TST.B	   NINIT			 TEST INIT FLAG
	 BNE.S	   ENDINIT			 BRANCH IF TRUE
*
	 MOVE.W    #10,NITCOUNT 		 TO STOP THUMPING
	 ST.B	   NIPFLAG			 SET INITIAL POS. FLAG
	 ST.B	   NINIT			 SET INITIALIZED FLAG
ENDINIT  EQU	   *
*
*	 CHECK FOR INITIAL POSITION
*
	 TST.B	   NIPFLAG			 TEST IP FLAG
	 BEQ.S	   ENDIP			 BRANCH IF FALSE
*
	 SF.B	   NIPFLAG			 CLEAR IP FLAG
	 ST.B	   UHDSET			 SET NEW HEADING FLAG
	 MOVE.L    #A1IPTAB,A0			 LOAD APPROACH 1 ADDR
	 BTST.B    #IENGFIR,IINSTMDI(A6)	 TEST VISUAL RESET
	 BNE.S	   APPR1IP			 BRANCH IF TRUE
*
	 BTST.B    #IAPPR1,IRCDRDI(A6)		 TEST APPROACH 1 SWITCH
	 BNE.S	   APPR1IP			 BRANCH IF TRUE
*
	 MOVE.L    #A2IPTAB,A0			 LOAD APPROACH 2 ADDR
APPR1IP  MOVE.L    (A0)+,NGLAT			 SET IP LAT
	 MOVE.L    (A0)+,NGLON			 SET IP LON
	 MOVE.W    (A0)+,USETHD 		 SET IP HEADING
ENDIP	 EQU	   *
*
*			     NAV INTERFACE
*
	 TST.B	   NAVFRZ	       LB	 NAV FREEZE ?
	 BNE	   ENDVEL	       .	 YES GO TO END FREEZE
*
*     NPSIGA   =FPSI + NGBIAS			 HEADING
*     NGS      =FVPGND				 GROUND SPEED
*
*     NGVNS = NUEW * NCPSIB - NVEW * NSPSIB	 N-S GROUND VEL
*     NGVEW = NVEW * NCPSIB + NUEW * NSPSIB	 E-W GROUND VEL      FT/SEC
*
	 MOVE.W    NGBIAS,D0	       +06
	 BSR	   MSC1
	 MOVE.W    D0,NSPSIB	       +15
	 MOVE.W    D1,NCPSIB	       +15
	 MULS	   NUEW,D1	       4+15=+19  NUEW * NCPSIB
	 MULS	   NVEW,D0	       4+15=+19  NVEW * NSPSIB
	 SUB.L	   D0,D1	       +19
	 ASL.L	   #1,D1	       +20
	 SWAP	   D1		       +04	 RESCALE
	 MOVE.W    D1,NGVNS	       +04	 NORTH-SOUTH VELOCITY FT/SEC
*
	 MOVE.W    NSPSIB,D0	       +15
	 MOVE.W    NCPSIB,D1	       +15
	 MULS	   NVEW,D1	       4+15=+19  NVEW * NCPSIB
	 MULS	   NUEW,D0	       4+15=+19  NUEW * NSPSIB
	 ADD.L	   D0,D1	       +19
	 ASL.L	   #1,D1	       +20
	 SWAP	   D1		       +04	 RESCALE
	 MOVE.W    D1,NGVEW	       +04	 EAST-WEST VELOCITY FT/SEC
*
*		   CHECK FOR HEADING INITIAL POS.
*
*    IF UHDSET THEN
*	      NGBIAS = AEPC(NGBIAS + USETHD - NPSIGA)
*	      UHDSET = .FALSE.
*    END IF
*
	 TST.B	   UHDSET	       .	 IP HEADING?
	 BEQ.S	   ENDHDSET	       .	 NO: BRANCH
*
	 MOVE.W    NGBIAS,D0	       +06	 YES: RECALCULATE NGBIAS
	 ADD.W	   USETHD,D0	       +06
	 BSR	   AEPC 	       +06	 LIMIT TO +/- 180 DEG
*
	 SUB.W	   NPSIGA,D0	       +06
	 BSR	   AEPC 	       +06	 LIMIT TO +/- 180 DEG
	 MOVE.W    D0,NGBIAS	       +06
	 SF.B	   UHDSET	       .	 CLEAR FLAG
ENDHDSET EQU	   *
*
*	 NPSIGA  =  AEPC(FPSI  +  NGBIAS)      LIMIT TO +/-  180 DEG
*
	 MOVE.W    FPSI,D0	       +06	 FLT TRUE HDG  WORD	  DEG
	 ADD.W	   NGBIAS,D0	       +06	 ADD NGBIAS
	 BSR	   AEPC 	       +06	 LIMIT TO +/- 180 DEG
	 MOVE.W    D0,NPSIGA	       +06	 NAV TRUE HDG		   DEG
*
	 MOVE.W    FVPGND,D1	       +05	 FLT GND SPD		   FPS
	 ASR.W	   #1,D1	       +05-01+04 FLT GND SPD		   FPS
	 MOVE.W    D1,NGS	       +04	 NAV GND SPD		   FPS
*
*						 MAG HEADING
*      NPSIMH  =AEPC(NPSIGA  -NMAGVAR ,0,360.)
*
	 MOVE.W    NMAGVAR,D1	       +06	 MAG VAR		   DEG
	 SUB.W	   D1,D0	       +06	 SUB MAG VAR FROM TRUE HDG DEG
	 BSR	   AEPC 	       .	 LIMIT TO 0- 360 DEG
	 MOVE.W    D0,NPSIMH	       +06	 STORE IT		   DEG
*
*		   DRIVE RMI HEADING  OR DIRECTIONAL GYRO (DG) IN TEST BED
*
	 BSR	   MSC1 	      . 	 GET SINE/COSINE OF NPSIMH
	 BCHG	   #15,D0	       .	 COMPLEMENT SIGN BIT
	 BCHG	   #15,D1	       .	 COMPLEMENT SIGN BIT
	 MOVEP.W   D1,ORMIHC(A6)       +15	 COSINE OF MAG HEADING
	 MOVEP.W   D0,ORMIHS(A6)       +15	 SINE OF MAG HEADING
*
*		   WIND DIRECTION WITH RESPECT TO NAV HEADING
*
*
	 MOVE.W    FWINDS,D1	       +15	 SIN(WIND DIRECTION)
	 MOVE.W    FWINDC,D0	       +15	 COS(WIND DIRECTION)
	 EXT.L	   D1		       +15	 PREPARE FOR NATAN2
	 EXT.L	   D0		       +15	 PREPARE FOR NATAN2
	 BSR	   NATAN2			 RESOLVE SIN/COS TO DEG
*
	 SUB.W	   NGBIAS,D0	       +06	 SUB INITIAL HEADING
	 BSR	   AEPC 	       +06	 LIMIT TO +/- 180 DEG
	 SUB.W	   NMAGVAR,D0	       +06	 SUBTRACT MAG VARIATION
	 BSR	   AEPC 	       +06	 LIMIT TO +/- 180 DEG
*
	 BSR	   MSC1
	 MOVE.W    D0,FSINPSIW	       +15	 SINE WIND DIRECTION W.R.T. NAV
	 MOVE.W    D1,FCOSPSIW	       +15	 COSE WIND DIRECTION W.R.T. NAV
*
*     NGGA    =FHGGND  +NGFE	       .	 NAV GEOM ALT ABOVE SEA LEVEL
*
	 MOVE.W    NGFE,D0	       +00	 FIELD ELEV		   FT
	 MOVE.W    FHGGND,D1	       +01	 FLT HT ABOVE GND	   FT
	 ASR.W	   #1,D1	       +01-01+00 FLT HT ABOVE GND	   FT
	 ADD.W	   D1,D0	       +00	 NAV GEOM ALT		   FT
	 MOVE.W    D0,NGGA	       +00	 STORE IT		   FT
*
*						 NAV HT ABOVE GND
*     NHRA    =FHGGND  -CONST(ASSUME 0. FOR PFT )
*
	 SUB.W	   #0,D1	       +00	 NAV HT ABOVE GND	   FT
	 MOVE.W    D1,NHRA	       +00	 STORE IT		   FT
*
*     NBWOW    =FWOG			LB	  NAV WT ON WHEELS
*
	 MOVE.B    FWOG,D0	       LB	 FLT WOW
	 MOVE.B    D0,NBWOW	       LB	 NAV WOW
*
*			     VELOCITIES INCLUDING WIND
*     IF ( FVPGND  .LT..25FPS.AND.NBWOW ) THEN
*	 NUEW	 =0.
*	 NVEW	 =0.
*      ELSE
*	 NUEW	 =FUEW
*	 NVEW	 =FVEW
*    END IF
*
	 BEQ.S	   MOVING	       LB	 WOW  N0
	 MOVE.W    NGS,D1	       +04	 YES			   FPS
	 CMP.W	   #$0004,D1	       +04	 IS IT MOVING ?      .25   FPS
	 BPL.S	   MOVING	       .	 YES
	 CLR.W	   NUEW 	       +04	 NAV N/S VEL WITH WIND	   FPS
	 CLR.W	   NVEW 	       +04	 NAV E/W VEL WITH WIND	   FPS
	 BRA.S	   ENDVEL	       .	 JMP TO END
MOVING	 MOVE.W    FUEW,D2	       +05	 FLT N/S VEL WITH WIND	   FPS
	 MOVE.W    FVEW,D3	       +05	 FLT E/W VEL WITH WIND	   FPS
	 ASR.W	   #1,D2	       +05-01+04 FLT N/S VEL WITH WIND	   FPS
	 ASR.W	   #1,D3	       +05-01+04 FLT E/W VEL WITH WIND	   FPS
	 MOVE.W    D2,NUEW	       +04	 STORE IT		   FPS
	 MOVE.W    D3,NVEW	       +04	 STORE IT		   FPS
ENDVEL	 EQU	   *
*
*		   LOAD CHART RECORDER VARIABLES DEPENDING ON MODE SWITCHES
*
	 MOVE.L    #CCTAB,A0	       .	 LOAD CROSS COUNTRY ADDR
	 BTST.B    #ICCMODE,IRCDRDI(A6) .	 CROSS COUNTRY MODE?
	 BNE.S	   LOADEND	       .	 YES: BRANCH
*
	 MOVE.L    #AP1TAB,A0	       .	 NO: LOAD APPROACH 1 ADDR
	 BTST.B    #IAPPR1,IRCDRDI(A6) .	 APPROACH 1 MODE?
	 BNE.S	   LOADEND	       .	 YES: BRANCH
*
	 MOVE.L    #AP2TAB,A0	       .	 NO: LOAD APPROACH 2 ADDR
LOADEND  MOVE.L    (A0)+,NCENLAT       +23	 STORE MAP CENTER LAT
	 MOVE.L    (A0)+,NCENLON       +23	 STORE MAP CENTER LON
	 MOVE.L    (A0)+,NRSLEW        +23	 STORE RECORDER SLEW RATE
	 MOVE.W    (A0)+,NRSCALE       +00	 STORE RECORDER MAP SCALE
*
*		   CHECK FOR CHART RECORDER SLEW
*
*	 IF SLEW SWITCH ON, SLEW .5 "/SEC ON RECORDER
*	    (.5"/SEC)*(1/20HZ)=.025"
*	    .025" = .00333333 DEG LAT/LON CROSS COUNTRY MODE
*	    .025" = .00097083 DEG LAT/LON APPROACH MODE
*
	 MOVE.L    NRSLEW,D2	       +23	 LOAD SLEW RATE
	 BTST.B    #ISLEWN,IRCDRDI(A6) .	 CHECK SLEW NORTH SWITCH
	 BEQ.S	   SLEWS	       .	 NO: BRANCH
*
	 BSR	   ADDLAT	       +23	 YES: SLEW (ADD D2 TO LAT)
SLEWS	 EQU	   *
	 BTST.B    #ISLEWS,IRCDRDI(A6) .	 CHECK SLEW SOUTH SWITCH
	 BEQ.S	   SLEWE	       .	 NO: BRANCH
*
	 NEG.L	   D2		       +23	 YES: SLEW
	 BSR	   ADDLAT	       +23	 SUB D2 FROM LAT
SLEWE	 EQU	   *
	 MOVE.L    NRSLEW,D2	       +23	 LOAD SLEW RATE
	 BTST.B    #ISLEWE,IRCDRDI(A6) .	 CHECK SLEW EAST SWITCH
	 BEQ.S	   SLEWW	       .	 NO: BRANCH
*
	 BSR	   ADDLON	       +23	 YES: SLEW (ADD D2 TO LON)
SLEWW	 EQU	   *
	 BTST.B    #ISLEWW,IRCDRDI(A6) .	 CHECK SLEW WEST SWITCH
	 BEQ.S	   SLEWEND	       .	 NO: BRANCH
*
	 NEG.L	   D2		       +23	 YES: SLEW
	 BSR	   ADDLON	       +23	 SUB D2 FROM LON
SLEWEND  EQU	   *
*
*
*			     UPDATE LAT
*
	 BTST.B    #IPOSFRZ,IRCDRDI(A6) .	 POSITION FREEZE?
	 BNE.S	   RCDR 	       .	 YES: DON'T CHANGE LAT/LON
*
	 MOVE.W    ZDGPFT,D1	       +26	 DEG TO FEET CONST.B
	 MOVE.W    NGVNS,D2	       +04	 N-S VEL WITH WIND   FT/SEC
	 MULS	   D1,D2	       +26+04+30 MULTIPLY  D2 = DEG/SEC/ITER
	 ASR.L	   #7,D2	       +30-07+23 RESCALE
*
*
	 BSR.S	   ADDLAT	       +23	 ADD D2 TO LAT AND LIMIT
*
*
*
*			     UPDATE LONG
*
	 MOVE.W    ZDGPFT,D1	       +26	 DEG TO FT CONST.B
	 MOVE.W    NGVEW,D2	       +04	 E-W VEL WITH WIND   FT/SEC
	 MULS	   D1,D2	       +26+04+30 MULTIPLY  D2 = DEG/SEC/ITER
	 ASR.L	   #7,D2	       +23	 RESCALE
*
	 MOVE.W    NGCLATIN,D3	       +07	 COS LAT INV =ABS(1/COS(NGLAT))
	 MULS	   D3,D2	       +23+07+30 MULTIPLY
	 ASR.L	   #7,D2	       +30   +23 RESCALE
*
	 BSR.S	   ADDLON	       +23	 ADD D2 TO LON AND LIMIT
*
*
*
*			     UPDATE CHART RECORDER
*
RCDR	 EQU	   *
	 MOVE.L    NGLAT,D0	       +23	 GET LAT
	 SUB.L	   NCENLAT,D0	       +23	 LAT DEVIATION FROM CENTER
*
*
	 BSR.S	   RCDRCALC	       +23-08+15 CALC RCDR DEV VOLTAGE
*
	 MOVEP.W   D0,ORCDNSD(A6)      +15	 OUTPUT N-S DEV
*
	 MOVE.L    NGLON,D0	       +23	 GET LON
	 SUB.L	   NCENLON,D0	       +23	 LON DEVIATION FROM CENTER
	 BSR.S	   RCDRCALC	       +23-08+15 CALC RCDR DEV VOLTAGE
	 MOVEP.W   D0,ORCDEWD(A6)      +15	 OUTPUT E-W DEV
	 BRA.S	   RCDREND	       .
*
*		   CALCULATE CHART RECORDER VOLTAGE
*	       INPUTS:
*		   D0  -  LAT OR LON DEVIATION FROM CENTER	     +23
*	       OUTPUTS:
*		   D0  -  RECORDER DEVIATION VOLTAGE		     +15
*
RCDRCALC EQU	   *
	 ASR.L	   #8,D0	       +23-08+15 RESCALE
	 ASR.L	   #1,D0	       +15-01+14 RESCALE
	 MULS	   NRSCALE,D0	       +06+14=20 MULT BY SCALE
	 ASR.L	   #5,D0	       +20-05=15 RESCALE
	 CMP.L	   #32767,D0	       +15	 COMPARE WITH +1
	 BLT.S	   INLIM1	       .
	 MOVE.L    #32767,D0	       +15	 LIMIT TO +1
INLIM1	 EQU	   *
	 CMP.L	   #-32767,D0	       +15	 COMPARE WITH -1
	 BGT.S	   INLIM2	       .
	 MOVE.L    #-32767,D0	       +15	 LIMIT TO -1
INLIM2	 EQU	   *
	 BCHG.L    #15,D0	       +15	 SET UP FOR AO
	 RTS			       .	 RETURN WITH WORD IN D0
*		       ADD TO LAT
*		       ----------
*	 INPUT:
*	       D2.L - NUMBER TO BE ADDED TO LAT 	   +23
*
ADDLAT	 MOVE.L    NGLAT,D1	       +23	 GET OLD LAT
	 BSR.S	   ADDLTLN	       .	 CALC NEW LAT
	 MOVE.L    D1,NGLAT	       +23	 STORE NEW LAT
	 RTS
*		       ADD TO LON
*		       ----------
*	 INPUT:
*	       D2.L - NUMBER TO BE ADDED TO LON 	   +23
*
ADDLON	 MOVE.L    NGLON,D1	       +23	 GET OLD LONGITUDE
	 BSR.S	   ADDLTLN	       .	 CALC NEW LON
	 MOVE.L    D1,NGLON	       +23	 STORE NEW LONGITUDE
	 RTS
*
*		       ADD TO LAT/LON
*		       --------------
*	 INPUTS:
*	       D2.L - NUMBER TO BE ADDED TO LAT/LON	   +23
*	       D1.L - LAT OR LON			   +23
*	 OUTPUT:
*	       D1.L - UPDATED LAT OR LON		   +23
*
ADDLTLN  ADD.L	   D2,D1	       +23   +23 CALC NEW LAT/LON
	 MOVE.L    ZLONMAX,D0	       +23	 CHECK FOR MAX LAT/LON
	 CMP.L	   D0,D1	       +23
	 BGT.S	   LIMPOS1	       .	 BRA IF BEYOND MAX
	 NEG.L	   D0		       +23	 MAKE MAX LAT/LON NEG
	 CMP.L	   D0,D1	       +23	 CHECK FOR MIN LAT/LON
	 BLT.S	   LIMNEG1	       .	 BRA IF BEYOND MIN
	 RTS
LIMPOS1  MOVE.L    ZLONMAX,D1	       +23	 LIMIT TO MAX
	 RTS
LIMNEG1  MOVE.L    ZLONMAX,D1	       +23
	 NEG.L	   D1		       +23	 LIMIT TO MIN
	 RTS
*
RCDREND  EQU	   *
*
*
*
*			     FIELD ELEVATION AND MAGVAR
*
	 MOVE.L    NSACF,A0			  ADDR OF CLOSEST FIELD
*
*			     FIELD ELEVATION
*
*     NGFE    =NGFE    -1/10*QESIK*(NGFE    -NVTNN18 )
*
	 MOVE.W    NGFE,D0	       +00	 OLD FIELD ELEV 	   FT
	 MOVE.W    D0,D1	       +00	 SAVE IT HERE TOO	   FT
	 SUB.W	   18(A0),D0	       +00	 SUB DEM FROM OLD ACTUAL   FT
	 MULS	   #$80,D0	       +00+08+08 INTEGRATE IT (0.5)	   FT
	 ASR.L	   #8,D0	       +08-08+00 RESCALE		   FT
	 SUB.W	   D0,D1	       +00	 OLD - DELTA =NEW	   FT
	 MOVE.W    D1,NGFE	       +00	 STORE IT		   FT
*
*			     MAG VAR
*      NMAGVAR =NMAGVAR + ( NVTNN34 - NMAGVAR ) ! 0.15
*
	 MOVE.W    34(A0),D0	       +06	 STATION MAGVAR (APPROX)   DEG
	 SUB.W	   NMAGVAR,D0	       +06	 DELTA MAGVAR
	 CMPI.W    #$000A,D0	       +06	 > 0.15?
	 BLE.S	   SKIPMV	       .	 WITHIN LIMIT
	 MOVE.W    #$000A,D0	       +06	 0.15 DEG/ITERATION
SKIPMV	 EQU	   *
	 ADD.W	   D0,NMAGVAR	       +06	 STORE FILTERED MAGVAR
*
*     NGCLAT  =ALIML(ABS(COS(NGLAT   )),.25)
*
	 MOVE.L    NGLAT,D0	       +23	 LAT COARSE		   DEG
	 ASR.L	   #8,D0	       +23-08+15 RESCALE
	 ASR.L	   #8,D0	       +15-08+07 RESCALE
	 ASR.L	   #1,D0	       +07-01+06 RESCALE TO WORD
	 TST.W	   D0		       +06	 IS D0 NEGATIVE ?
	 BPL.S	   ABS1
	 NEG.W	   D0				 MAKE IT POSITIVE
ABS1	 EQU	   *
	 BSR	   MCOS1	       +06	 D0 = COS(LAT D0) AT +15
	 CMPI.W    #$2000,D0	       +15	 GT .25 ?		   NORM
	 BGT.S	   LIM1 	       .	 YES
	 MOVE.W    #$2000,D0	       +15	 NO LIMIT TO .25	   NORM
LIM1	 MOVE.W    D0,NGCLAT	       +15	 STORE IT		   NORM
*
*     NGCLATIN=1/NGCLAT
*
	 MOVE.L    #$7FFFFFFF,D1       +31	 .99999 APPROX		   1
	 ASR.L	   #8,D1	       +31-08+23 .99999 APPROX		   1
	 DIVU	   D0,D1	       +23-15+08 COS LAT INV		 RATIO
	 ASR.W	   #1,D1	       +08-01+07 COS LAT INV		 RATIO
	 MOVE.W    D1,NGCLATIN	       +07	 COS LAT INV		 RATIO
*
*
	 PAGE
*************************************************************
*
*			     NAV1 (VOR/ILS)
*
*************************************************************
	 BTST.B    #IMSTSW,IMPSDI1(A6) LB	 MASTER ON?		   LB
	 BEQ	   CLRALL1	       .	 NO CLEAR INDICATORS
*
	 BTST.B    #INAV1ON,INAV1F3(A6) LB	  NAVCOM 1 ON?		    LB
	 BEQ	   CLRALL1	       .	 NO
*
	 BTST.B    #INAV1ID,INAV1F3(A6) .	 SEE IF IDENT ON
	 BNE.S	   NV1IDON
	 MOVE.B    #$FF,ONV1VOL(A6)    .	 SET VOLUME OFF
	 BRA.S	   NV1IDOFF	       .	 CONTINUE
*
*		   NAV1 VOLUME CONTROL
*
NV1IDON  MOVEP.W   INAV1VOL(A6),D0     +15	 XFER AUDIO VOLUME  OFFSET BIN
	 MOVE.B    D0,INAV1VOL(A6)     .	 START NEXT A/D CONVERSION
	 BSR	   CONATT	       .	 MAKE VOLUME CONTROL LINEAR
	 MOVE.B    D0,NNV1VOL	       .	 STORE IN TEMP CORE FOR DEBUG
	 MOVE.B    D0,ONV1VOL(A6)      .	 OUTPUT NAV1 VOLUME
*
*
*
*			 STATION RANGE
*
NV1IDOFF TST.B	   NAV1TUN	       LB	 STATION TUNED?
	 BEQ	   CLRILS1	       .	 NO CLR THE NEEDLES
	 MOVE.L    NAV1STA,A1	       .	 YES FIND STA NO
	 BSR	   NAVRCV	       .	 CALL NAV RCVR SUBROUTINE
*
*	 TEST NEW NATAN2 ROUTINE & DOUBLE PRECEION MUL	DSMUL
*
	 MOVE.L    NRXGASL,NSPARE1     +23	 SAVE XGASL
	 MOVE.L    NRYGASL,NSPARE2     +23	 SAVE YGASL
	 MOVE.L    NRAGAS,NSPARE3      +08	 NEW AGAS STORED FOR DEBUG
	 MOVE.L    NRBGAS,NSPARE4      +08	 NEW BGAS STORED FOR DEBUG
*
	 MOVE.L    NSPARE5,NSPARE7     +10	 OLD AGAS STORED FOR DEBUG
	 MOVE.L    NSPARE6,NSPARE8     +10	 OLD BGAS STORED FOR DEBUG
*
	 MOVE.W    NSPAREW2,NSPAREW3   +06	 BRG TO STATION FROM NATAN2
	 MOVE.W    NBASEREG,NSPAREW4   +06	 BASE ANGLE FROM NATAN2
*
	 MOVE.W    NRXGAS,NSPAREW7     +12	 XGAS TEMP STORE
	 MOVE.W    NRYGAS,NSPAREW8     +12	 YGAS TEMP STORE
*
	 MOVE.W    NSPAREW5,NSPAREW6   +12	 YGASL/XGASL
*
	 TST.B	   NRLOS	       .	 LINE OF SIGHT?
	 BEQ	   CLRILS1	       .	 NO? THEN CLEAR SYSTEM
*
	 TST.B	   NRWR1	       .	 STATION IN RANGE ?
	 BEQ	   CLRILS1
*
*
*			     SIGNAL LEVEL NAV 1 ILS/VTC
*     INAV1VOL =ALIMU((NRRGAS/COR ALT RNG)**2 LIM.99)
*
	 MOVE.W    NRRGAS,D5	       +06	 RANGE TO STATION
	 MOVE.W    20(A1),D7	       +00	 RANGE 1
	 EXT.L	   D5		       +06	 MAKE TO LONG WORD
	 TST.W	   D7		       .	 CHECK FOR ZERO
	 BNE.S	   NAV1Z	       .	 NOT ZERO, GO TO DIVIDE
	 MOVE.W    #1,D7	       +00	 MAKE RANGE 1 NON-ZERO
NAV1Z	 DIVS	   D7,D5	       +06	 % OF MAX RANGE
	 MULS	   D5,D5	       +12	 % RANGE SQUARED      (RATIO)
	 MULS	   #07,D5	       +12+04+16 MULT BY .44 (FUDGE FACTOR)
	 ASR.L	   #8,D5	       +16-08+08 RESCALE
	 CMP.L	   #$80,D5	       +08	 CHECK FOR LIMIT
	 BMI.S	   NAV1Z1	       .	 BRANCH IF OK
	 MOVE.B    #$80,D5	       +08	 LIMIT TO $80 (MUTE)
NAV1Z1	 MOVE.B    D5,NAV1VOL	       +08	 SAVE NAV 1 VOLUME
*
	 SUB.B	   #$80,D5	       +08	 D5-$80    ($80=MUTE)
	 NEG.B	   D5		       +08	 $80-D5 = WHITE NOISE LEVEL
	 MOVE.B    D5,ONV1WNL(A6)      +08	 OUTPUT WHITE NOISE LEVEL
*
*
*			     FIND STA TYPE (VTC/ILS)
*
	 MOVE.W    2(A1),D2	       HEX	 STA TYP
	 CMP.W	   #$0068,D2	       HEX	 VTC ?
	 BEQ	   VOR1 	       .	 YES
	 PAGE
***********************************************************
*
*			     NAV1 ILS
*
***********************************************************
*
*			     MAG BEARING TO STATION (+/- 180 DEG)
*     NAV1BRG =NRBRG=(ATAN(NRYGAS/NRXGAS) - 36(A0) = -DEV
*     36(A0)= RNWY MAG HEADING FROM ADDP TABLE
*     DEV =(RNWY MAG HEADING - AIRCRAFT TO ILS STATION BEARING) BY DEFINTION
*     A/C TO ILS STA BEARING=(ATAN(NRYGAS/NRXGAS))=ATAN(DELTA LON/DELTA LAT)
*
	 MOVE.W    NRBRG,NAV1BRG       +06	 -DEV FOR ILS STA
*
*			     ANGULAR DIFFERENCE(DEVIATION)
*    -DEV = NCDI1DEG = NAV1BRG	  FOR TUNED ILS STATIONS
*
	 MOVE.L    NAV1STA,A0	       .	 FIND STA SA		   ADDR
*
*	 MOVE.W    36(A0),D0	       +06	 RNWY MAG HDG		   DEG
*	 SUB.W	   NAV1BRG,D0	       +06	 DIFFERENCE= +DEV	   DEG
*	 BSR	   AEPC 	       +06	 LIMIT DEV TO +/-180 DEG
*	 NEG.W	   D0		       +06	 -DIFFERENCE=-DEV	   DEG
*
	 MOVE.W    NAV1BRG,D0	       +06	 -DEV
	 BSR	   NCDI 	       +06	 COURSE DEVIATION	   DEG
	 MOVE.W    D0,NCDI1DEG	       +06	 +DEV
*
*			     LOCALIZER INDICATOR DRIVE
*     NCDI1DEG =ALIM(NCDI1DEG ,5)
*
	 CMP.W	   #319,D0	       +06	 GT.4.99		   DEG
	 BPL.S	   PLUS31	       +06	 YES
	 CMP.W	   #-319,D0	       +06	 GT -4.99		   DEG
	 BPL.S	   LMTILS1	       .	 YES
	 MOVE.W    #-319,D0	       +06	 LIMIT TO -4.99
	 BRA.S	   LMTILS1	       .
PLUS31	 MOVE.W    #319,D0	       +06	 LIMIT TO 4.99		   DEG
*
*     ONAV1LR =DEFLECTION/5.	       NEEDLE DRIVE
LMTILS1  EXT.L	   D0
	 DIVS	   #$0005,D0	       +06-00+06 DIV BY 5		   NORM
	 ASL.W	   #7,D0	       +06+07+13			   NORM
	 ASL.W	   #2,D0	       +13+02+15			   NORM
	 NEG.W	   D0		       +15	 FOR DRIVING NEEDLE
*	 ASR.W	   #1,D0	       +15	 DIVIDE BY 2(NOT NEEDED)
*						 i.e. 2.5 DEG = EDGE OF YELLOW
*							 = 5 V = 3 DOTS VOA9
	 BCHG.L    #15,D0	       +15	 CHANGE BIT 15 FOR OUTPUT
	 MOVE.W    NAV1LR,D1	       +15	 GET OLD AO
	 BSR	   INTEG1	       .	 INTEGRATE TO NEW AO
	 MOVE.W    D0,NAV1LR	       +15	 STORE NEW AO
	 MOVEP.W   D0,ONAV1LR(A6)      +15	 OUTPUT NEW CDI AO	   NORM
*
*			     "ON"("TO") FLAG
*
	 MOVE.W    #$FFFF,D0	       +15	 ONLY "TO" FLAG FOR ILS
	 MOVEP.W   D0,ONAV1TF(A6)
*
*			     GLIDE SLOPE
*			      (ASSUME IN RNG SINCE ILS IN RNG) AN APPROX
*
	 BTST.B    #IGSFAIL,IFAIL2(A6) LB	 GS MALF?
	 BNE	   GSOFF1	       .	 YES
*
	 MOVE.L    NRAGAS,D2	       +08	 DISTANCE ALONG RUNWAY	  FEET
	 MOVE.W    42(A0),D3	       +00	 RUNWAY LENGTH		  FEET
	 EXT.L	   D3		       +00	 RUNWAY LENGTH		  FEET
	 ASL.L	   #8,D3	       +08	 RESCALE
	 SUB.L	   D3,D2	       +08	 D2=DIST TO TOUCH DOWN	  FEET
*						 ON BACKCOURSE IF NEGATIVE
	 BMI	   GSOFF1
*
*	 MOVE.W    NRRGAS,D0	       +06	 RANGE TO STATION	   NM
*	 MULS	   #6076,D0	       +06-00+06 CHANGE TO FEET 	   FEET
*	 ASR.L	   #4,D0	       +06-04+02 RESCALE
*
*	 MOVE.L    NAV1STA,A0	       .	 SA OF STA
*	 MOVE.W    42(A0),D1	       +00	 RUNWAY LENGTH		   FEET
*	 EXT.L	   D1		       +00	 RUNWAY LENGTH
*	 ASL.L	   #2,D1	       +02	 RESCALE
*	 SUB.L	   D1,D0	       +02	 DIST TO TOUCHDOWN 4 RES   FEET
*
	 MOVE.L    D2,D0	       +08	 DIST TO TOUCH DOWN	   FEET
	 ASR.L	   #6,D0	 +08-06+02	 RESCALE TO WORD	   FEET
	 ASR.L	   #4,D0	 +02-04-02	 RESCALE TO WORD       FEET
*
	 TST.L	   D0		       -02	 CHECK FOR ZERO
	 BNE.S	   DIVOK	       .	 BRANCH IF OK TO DIVIDE
	 MOVE.L    #1,D0	       .	 MAKE NON ZERO FOR DIVIDE
*
DIVOK	 MOVE.W    NRZGAS,D1	       +00	 HT. ABOVE STATION	   FEET
	 EXT.L	   D1		       +00	 HT. ABOVE STATION	   FEET
	 ASL.L	   #8,D1	       +00+08+08 RESCALE
	 ASL.L	   #2,D1	       +08+02+10 RESCALE
*
	 DIVS	   D0,D1	       +10+02+12 CALC TAN		  RATIO
*
	 BVS.S	   GSPLUS14			 LIMIT TO TAN(1.39) IF OVFLOW
	 MOVE.W    D1,D0	       +12
	 MOVE.W    D0,NGSTAN	       +12	 TEMP STORE FOR DEBUG
	 SUB.W	   #215,D0	       +12	 SUBTRACT TAN(3)
*
*	 FLAIR = .999 IF(NRAGAS - RNWY) > 3040 FEET   , 3040 FT = 0.5 NM
*	 FLAIR = 1/6  +  (NRAGAS - RNWY)/3648	      , 3648 FT = 0.6 NM
*
*   NOTE: D4.W = FLAIR IS ALWAYS LESS TNAN OR EQAUL TO .999  AT SCALE OF +10
*
*	INPUTS:     D0.W = (NGSTAN - TAN(3))	+12    = DELTA GS
*		    D2.L = (NRAGAS - RNWY)	+08  IN FEET
*	OUTPUTS:    D0.L = (NGSTAN - TAN(3)) * FLAIR   +22  =(DELTA GS)*FLAIR
*
	 CMP.L	   #778240,D2	       +08	 IS D2 > 3040 FT ?
	 BPL.S	   FLAIR1
	 DIVS	   #912,D2	 +08+02+10	 D2.W = D2.L/3648     RATIO
	 ADD.W	   #170,D2	       +10	 D2.W = D2 + 0.166
	 MOVE.W    D2,D4	       +10
	 BRA.S	   FLAIR2
FLAIR1	 MOVE.W    #1023,D4	       +10	 FLAIR = D4.W = 0.999
*
FLAIR2	 EQU	   *
	 MULS	   D4,D0	 +10+12+22	 D0.L = D0.W * D4.W
*
	 CMP.L	   #102390,D0	       +22	 GT TAN(1.398) ?
	 BPL.S	   GSPLUS14	       .	 BRANCH IF GT
	 CMP.L	   #-102390,D0	       +22	 LT TAN(-1.398) ?
	 BPL.S	   CONT3	       +22
	 MOVE.L    #-102390,D0	       +22	 LIMIT TO TAN(-1.398)
	 BRA.S	   CONT3
GSPLUS14 MOVE.L    #102390,D0	       +22	 LIMIT TO TAN(1.398)
*
*LT14D1   CMP.W     #07,D0		+12	  CHECK FOR > TAN(.1)
*	 BPL.S	   CONT1	       .	 IF GREATER CONTINUE
*	 CMP.W	   #-07,D0	       +12	 CHECK FOR < TAN(-.1)
*	 BMI.S	   CONT2	       .	 IF LESS CONTINUE
*	 MOVE.W    #0,D0	       +12	 ELSE LIMIT TO 0 DEVIATION
*	 BRA.S	   CONT3
*CONT1	  SUB.W     #07,D0		+12	  SUB TAN(.1) FOR POS FULL SCAL
*	 BRA.S	   CONT3
*CONT2	  ADD.W     #07,D0		+12	  ADD TAN(.1) FOR NEG FULL SCAL
*
CONT3	 EQU	   *
	 ASL.L	   #2,D0	  +22+02+24	 RESCALE
	 DIVS	   #25,D0	  +24-10+14	 DIV BY TAN(1.4)	  RATIO
	 ASL.W	   #1,D0	  +14+01+15	 RESCALE FOR OUTPUT
*
	 NEG.W	   D0			+15	  FOR NEEDLE DRIVE POLARITY
	 BCHG.L    #15,D0		+15	  CHANGE TO OFFSET BINARY
	 MOVE.W    NAV1GS,D1	       +15	 GET OLD AO
	 BSR	   INTEG1	       .	 INTEGRATE TO NEW AO
	 MOVE.W    D0,NAV1GS	       +15	 STORE NEW AO
	 MOVEP.W   D0,ONAV1GS(A6)      +15	 OUTPUT NEW AO (GS NEEDLE)
	 BCLR.B    #ONAV1GSF,NCDI1DO   .	 SET GS FLAG "ON"
	 BRA	   ENDVRIL1
*
*
*			     GS FLAG "OFF"
*
GSOFF1	 BSET.B    #ONAV1GSF,NCDI1DO   .	 GS FLAG "OFF"
	 MOVE.W    #$8000,D0
	 MOVEP.W   D0,ONAV1GS(A6)      +15	 CENTRE THE GS NDLE "AO" 0 VOLT
	 BRA	   ENDVRIL1
*
*
	 PAGE
**************************************************************
*
*			     NAV1 VOR
*
**************************************************************
VOR1	 EQU	   *
	 MOVEP.W   ICRS1SC(A6),D0      +15	 SIN/COS OF SET COURSE
	 BCHG.L    #15,D0	       +15	 CONVERT TO 2'S COMPLEMENT
*
*      SCALE SIN/COS INPUT
*
	 BTST.B    #OCRS1SEL,NCDI1DO   .	 READING SINE OR COS?
	 BEQ.S	   SINE1	       .
*
	 MOVE.W    D0,NCRS1COS	       +15	 SAVE COSINE
	 BCLR.B    #OCRS1SEL,NCDI1DO   .	 CLR TO READ SIN NEXT TIME
	 BRA.S	   CON1 	       .
*
SINE1	 MOVE.W    D0,NCRS1SIN	       +15	 SAVE THE SINE
	 BSET.B    #OCRS1SEL,NCDI1DO   .	 SET TO READ COS NEXT TIME
*
CON1	 MOVE.B    NCDI1DO,OCDI1DO(A6) .	 OUTPUT TEMP TO OUTPUT
*
	 MOVE.W    NCRS1COS,D0	       +15	 PREPARE FOR NATAN2
	 MOVE.W    NCRS1SIN,D1	       +15	 PREPARE FOR NATAN2
	 EXT.L	   D1		       +15
	 EXT.L	   D0		       +15
	 BSR	   NATAN2			 RESOLVE SIN/COS TO DEG
	 MOVE.W    D0,NAV1OBS	       +06	 SAVE NAV1 SELECTED BEARING
*
*			     TEST IF NAV1 PUSH-TO-TEST ON
*			     IF ON THEN NRBRG=180 DEGREES
*
	 BTST.B    #INAV1TST,ICRS1DI(A6)	 TEST IF PUSH TO TEST PRESSED
	 BEQ.S	   NAV1CON1
	 MOVE.W    #11520,NRBRG        +06	 NRBRG=180 DEG
*
*
*
*			     MAG BEARING FROM STATION(0-360)
*     NAV1BRG =TRUE BRG +NMAGVAR
*
NAV1CON1 MOVE.W    NRBRG,NAV1BRG       +06	 MAG BRG FROM STA	   DEG
*
*		   DRIVE RMI VOR POINTER #1
*
*	 BTST.B    #IRMI1ADF,IMPSDI1(A6)
*	 BEQ.S	   NOPTR	       .	 PTR 1 NOT VOR
*	 MOVE.W    NAV1BRG,D0	       +06	 DRIVE RMI PTR 1 W/ VOR 1 BRG
*	 SUB.W	   NPSIMH,D0	       +06	 SUBTRACT MAG HDG
*	 BSR	   AEPC 	       +06	 LIMIT 0-360
*	 BSR	   MSC1 	      . 	GET SIN/COS OF BEARING
*	 BCHG.L    #15,D0	       .	 CHANGE BIT 15 FOR OUTPUT
*	 BCHG.L    #15,D1	       .	 CHANGE BIT 15 FOR OUTPUT
*	 MOVEP.W   D0,ORMI1S(A6)       +15	 SINE OF PTR 1
*	 MOVEP.W   D1,ORMI1C(A6)       +15	 COSINE OF PTR 1
*NOPTR	  EQU	    *
*
*			     ANGULAR DIFFERENCE(DEVIATION)
*     DEVIATION  =NAV1OBS -NAV1BRG
*
	 MOVE.W    NRBRG,D0	       +06
	 SUB.W	   NAV1OBS,D0	       +06	 -DEVIATION		   DEG
	 BSR	   AEPC 	       +06	 LIMIT -DEV TO +/- 180 DEG
*	 BSR	   NCDI 	       +06	  DEVIATION		   DEG
	 MOVE.W    D0,NCDI1DEG	       +06	 DEVIATION		   DEG
*
*			     VTC       INDICATOR DRIVE
*     NCDI1DEG =ALIM(NCDI1DEG ,20.)
*
*	 CMP.W	   #1279,D0	       +06	 GT 19.99		   DEG
*	 BPL.S	   PLUS121	       .	 YES
*	 CMP.W	   #-1279,D0	       +06	 GT -19.99		   DEG
*	 BPL.S	   LMTVOR1	       .	 YES
*	 MOVE.W    #-1279,D0	       +06	 LIMIT -19.99		   DEG
*	 BRA.S	   LMTVOR1	       .
*PLUS121  MOVE.W    #1279,D0		+06	  LIMIT TO 19.99	    DEG
*
*     ONAV1LR =DEFLECTION/20,	       NEEDLE DRIVE
*LMTVOR1  EXT.L     D0
*	 DIVS	   #20,D0	       +06-00+06 DIV BY 20		   NORM
*	 ASL.W	   #7,D0	       +06+07+13			   NORM
*	 ASL.W	   #2,D0	       +13+02+15			   NORM
*	 NEG.W	   D0		       +15	 FOR NEEDLE DRIVE
**	 ASR.W	   #1,D0	       +15	 DIVIDE BY 2(NOT NEEDED)
*						 10 DEG = EDGE OF YELLOW
*						     = 5 V = 3 DOTS VOA9
*
*	 CDI NEEDLE = SIN(-DEV) LIMIT TO +/- 4 DOTS, +/- 2 DOTS=YELLOW EDGE
*	  INPUT = -DEV IN D0.W AT +06  = NCDI1DEG
*
	 BSR	   MSC1 			 SIN IN D0, COS IN D1 AT +15
	 MULS	   #737,D0	 +15+06+21	 D0 = 11.517472(SIN(-DEV)) DOTS
*
*     LIMIT # OF DOTS TO +/- 4 BEFORE OUTPUTTING = +/- 10 VOLTS
*			 +/- 2 DOTS = 5 V =(EDGE OF YELLOW = 3 DOTS ON VOA9)
*
	 CMPI.L    #8388607,D0	       +21	 IS D0 < 3.999 DOTS ?
	 BLT.S	   VOR1LM1
	 MOVE.L    #8388607,D0	       +21	 LIMIT TO +3.999 DOTS
	 BRA.S	   VOR1LM2
VOR1LM1  CMPI.L    #-8388607,D0        +21	 IS D0 < -3.999 DOTS ?
	 BGT.S	   VOR1LM2
	 MOVE.L    #-8388607,D0        +21	 LIMIT TO -3.999 DOTS
VOR1LM2  EQU	   *
*
	 DIVS	   #256,D0	 +21-06+15	 DIV BY 4 TO NORMALISE
*
	 BCHG.L    #15,D0	       +15	 CONVERT TO OFFSET BINARY
	 MOVEP.W   D0,ONAV1LR(A6)      +15	 CDI "AO"  IN VOLTS        NORM
*
*	 TO-OFF-FROM FLAG CALCULATIONS
*	 -DEV IN NCDI1DEG = (NAV1BRG - NAV1OBS)  LIMITED TO +/- 180 DEG
*	 D1.W = COS(-DEV) AT +15
*	     -75 < -DEV < 75 DEG       FLAG = "TO"
*	    -105 > -DEV >105 DEG       FLAG = "FROM"
*  75 < -DEV <105 DEG	OR -105 < -DEV <-75 DEG FLAG BETWEEN TO-OFF-FROM
*
	 MOVE.W    D1,D2	       +15	 SAVE COS FOR SIGN
*
	 BCHG.L    #15,D1	       +15	 CONVERT FROM BTC TO BOB
	 MOVEP.W   D1,ONAV1TF(A6)      +15	 SET "TO/FROM" FLAG        VOLT
*
*			     CONE OF CONFUSION
*
*
*
*			     CONE RADIUS
*
*	 CONE RADIUS = (NARZGAS X TAN 30)/6080	IN NAUTICAL MILES
*	 TAN 30 = 0.5773503 = 591 AT SCALE +10
*	 1 N.M. = 6080 FT.  = 6080  AT SCALE +00
*
*
	 MOVE.W    NRZGAS,D7	       +00	 HEIGHT ABOVE STATION	   FEET
	 MULS	   #591,D7	       +10	 CONE RADIUS IN D7.L IN    FEET
	 DIVS	   #6080,D7	       +10	 CONE RADIUS IN D7.W IN    N.M.
	 ASR.W	   #4,D7	       +10-04+06 RESCALE
	 MOVE.W    D7,NCONRAD	       +06	 SAVE CONE RADIUS IN NCONRAD
	 CMP.W	   NRRGAS,D7	       +06	 CONE RADIUS <= A/C DIST ? N.M.
	 BLE.S	   OUTCONE1	       .	 YES
	 MOVE.W    #$8000,D0	       .
	 MOVEP.W   D0,ONAV1TF(A6)      +15	 NO SET FLAG "OFF"   0     VOLT
OUTCONE1 EQU	   *
*
*			     MALF LOC/VTC (CDI)
*
ENDVRIL1 BTST.B    #IVL1FAIL,IFAIL1(A6) LB	  MALF VTC/ILS ?
	 BEQ.S	   ENDVL1	       .	 NO
	 MOVE.W    #$8000,D0	       .
	 MOVEP.W   D0,ONAV1LR(A6)      +15	 YES CENTER CDI      0	   VOLT
	 MOVEP.W   D0,ONAV1TF(A6)      +15	 YES SET "OFF" FLAG  0     VOLT
	 BRA.S	   ENDVL1
*
*			     ILS/VTC NOT IN RANGE
*
CLRILS1  BSET.B    #ONAV1GSF,NCDI1DO   LB	 GLIDE SLOPE FLAG "OFF"    LB
	 SF.B	   NAV1TUN	       LB	 CLEAR NAV1 TUNED	   LB
	 CLR.L	   NAV1FREQ	       +00	 CLEAR FREQ
	 MOVE.W    #$8000,D0	       .
	 MOVEP.W   D0,ONAV1TF(A6)      +15	 SET LOC/ILS FLAG "OFF" 0  VOLT
	 MOVEP.W   D0,ONAV1LR(A6)      +15	 CENTER CDI 1 NEEDLE	0  VOLT
	 MOVEP.W   D0,ONAV1GS(A6)      +15	 CENTER GSDI  NEEDLE	0  VOLT
	 MOVE.B    #$FF,ONV1WNL(A6)    +08	 MUTE WHITE NOISE LEVEL NAV1
	 ST.B	   NAV1VOL	       +08	 SET NAV1 IDENT TO MAX ATTEN
	 PAGE
**********************************************************************
*
*	 MARKER BEACONS (NAV1 ONLY)
*
*	 NE = (64*NRZGAS*.2)/[8*(NRAGAS**2)+(NRBGAS**2)+(NRZGAS**2)]
*
*	 NMKRLVL = 1 - [((NE*NK1 LIM.12)+(NE-NK2 LIM0)*NK3) LIM.999]
*
*		   HI SENSE	       LO SENSE
*	 NK1	    600 	       187
*	 NK2	   .0002	       .002
*	 NK3	    352 	       63
*
ENDVL1	 TST.B	   NMKRVL	       .	 MARKER VALID?
	 BEQ	   CLRMKRS	       .	 NO, IT IS NOT VALID
*				       .	 OF EVERY MKR STA
	 MOVE.L    NMKRSA,A1	       .	 LOAD ADDRESS OF MKR STA
	 BSR	   NAVRCV	       .	 CALL NAV RCVR SUBROUTINE
*
	 TST.B	   NRWR1	       .	 MARKER IN RANGE?
	 BEQ	   CLRMKRS	       .	 YES IT IS OUT OF RANGE
*
	 BTST.B    #IADF1MKR,IADF1DI(A6)	 MKRS ON?
	 BEQ	   CLRMKRS	       .
*
	 MOVE.W    #63,NK3	       +00	 CONSTANT K3=63
	 MOVE.W    #8389,NK2	       +22	 CONSTANT K2=.002
	 MOVE.W    #187,NK1	       +00	 CONSTANT K1=187
	 TST.B	   NMKRSNS	       .	 LO SENSE ?
	 BEQ.S	   LOSNS	       .	 YES, SKIP HI SENSE K'S
*
	 MOVE.W    #839,NK2	       +22	 CONSTANT K2 .0002
	 MOVE.W    #600,NK1	       +00	 CONSTANT K1
LOSNS	 MOVE.L    NRAGAS,D0	       +08	 AGAS		     FEET
	 ASR.L	   #8,D0	       +08-08+00 RESCALE TO WORD
	 MULS	   D0,D0	       +00+00+00 AGAS SQUARED	     FEET SQ
	 ASL.L	   #3,D0	       +00	 MULT BY 8
	 MOVE.L    NRBGAS,D1	       +08	 BGAS		     FEET
	 ASR.L	   #8,D1	       +08-00+00 RESCALE TO WORD
	 MULS	   D1,D1	       +00+00+00 BGAS SQUARED	     FEET SQ
	 ADD.L	   D1,D0	       +00	 ADD TO TOTAL
	 MOVE.W    NRZGAS,D1	       +00	 ZGAS		     FEET
	 MULS	   D1,D1	       +00	 ZGAS SQUARED	     FEET SQ
	 ADD.L	   D1,D0	       +00	 ADD TO TOTAL
	 ASR.L	   #8,D0	       +00-08-08 RESCALE TO WORD
	 ASR.L	   #6,D0	       -08-06-14
	 TST.W	   D0		       .	 IS IT ZERO
	 BNE.S	   ROK		       .	 NO, OK TO DIVIDE
*
	 ADDQ.W    #1,D0	       -14	 MAKE IT NON-ZERO
ROK	 MOVE.W    NRZGAS,D1	       +00	 ZGAS		     FEET
	 MULS	   #3277,D1	       +08	 64*ZGAS*.2
	 DIVS	   D0,D1	       +08+14+22
	 BVC.S	   NOTOVER	       .	 CHECK FOR OVERFLOW
*
	 MOVE.W    #32767,D1	       +22	 LIMIT IF OVERFLOW
NOTOVER  CMP.W	   #16777,D1	       +22	 GT .004  ?
	 BMI.S	   NOLIM4	       .	 NO: BRANCH
*
	 MOVE.B    #0,D1	       +08	 YES: FULL VOLUME
	 BRA.S	   MKRLVL1	       .	 OUTPUT VOLUME LEVEL
*
NOLIM4	 MOVE.W    D1,NE	       +22	 SAVE NE
	 MULS	   NK1,D1	       +22+00+22 MULT BY CONST 1
	 CMP.L	   #1568670,D1	       +22	 IS D1 <= .374
	 BLE.S	   MLMT1
*
	 MOVE.L    #1568670,D1	       +22	 LIMIT TO .374
MLMT1	 MOVE.W    NE,D0	       +22
	 SUB.W	   NK2,D0	       +22	 NK2=.002 BREAK POINT
	 BPL.S	   MENDC1	       .	 BRANCH IF NEG
*
	 MOVE.W    #0,D0	       +22	 LIMIT TO 0
MENDC1	 MULS	   NK3,D0	       +22+00+22
	 ADD.L	   D1,D0	       +22
	 CMP.L	   #261724,D0	       +22	 GT .0624?
	 BPL.S	   MENDC	       .	 BRANCH IF GT
*
	 MOVE.B    #$FF,D1	       +08	 ELSE MUTE
	 BRA.S	   MKRLVL1	       .	 OUTPUT
*
MENDC	 MOVE.L    #2097151,D1	       +22	 .4999
	 SUB.L	   D0,D1	       +22	 .4999 - D0
	 ASR.L	   #6,D1	       +22-06+16 CHANGE TO BYTE
	 ASR.L	   #8,D1	       +16-08+08
MKRLVL1  MOVE.B    D1,NMKRLVL	       +08	 STORE MKR LEVEL
	 MOVE.L    NMKRSA,A0	       .	 ADDRESS OF MKR
	 MOVE.W    2(A0),D0	       .	 STA TYPE
	 CMP.W	   #$008C,D0	       .	 OM ?
	 BEQ.S	   OTRMKR	       .
*
	 CMP.W	   #$008B,D0	       .	 MM ?
	 BEQ.S	   MIDMKR	       .
*
	 MOVE.B    NMKRLVL,NAMRA       .	 MUST BE INNER MARKER
	 ST.B	   NIMTONE
	 SF.B	   NMMTONE
	 SF.B	   NOMTONE
	 ST.B	   NMMRA
	 ST.B	   NOMRA
	 BRA.S	   ENDMKRS
*
MIDMKR	 EQU	   *
	 MOVE.B    NMKRLVL,NMMRA       .	 MIDDLE MARKER
	 ST.B	   NMMTONE
	 SF.B	   NIMTONE
	 SF.B	   NOMTONE
	 ST.B	   NAMRA
	 ST.B	   NOMRA
	 BRA.S	   ENDMKRS
*
OTRMKR	 EQU	   *
	 MOVE.B    NMKRLVL,NOMRA       .	 OUTER MARKER
	 ST.B	   NOMTONE
	 SF.B	   NIMTONE
	 SF.B	   NMMTONE
	 ST.B	   NAMRA
	 ST.B	   NMMRA
	 BRA.S	   ENDMKRS
*
CLRMKRS  EQU	   *
	 ST.B	   NAMRA	       .	 TURN OFF ALL MARKERS
	 ST.B	   NMMRA
	 ST.B	   NOMRA
	 BRA.S	   ENDMKRS
*
*
*			     POWER NAV 1 CLEAR EVERYTHING
*
CLRALL1  EQU	   *
	 SF.B	   NAV1TUN	       LB	 CLR NAV1 IN TUNE
	 CLR.L	   NAV1FREQ	       +00	 CLR NAV1 FREQ
	 BSET.B    #ONAV1GSF,NCDI1DO   LB	 CLR NAV1 GS FLAG "OFF"
	 ST.B	   NAV1VOL	       +15	 CLR NAV1 IDENT LEVEL  0   VOL
	 MOVE.B    #$FF,ONV1WNL(A6)    +08	 MUTE WHITE NOISE LEVEL NAV1
	 MOVE.B    #$FF,ONV1VOL(A6)    +08	 TURN NAV1 VOL OFF
	 MOVE.W    #$8000,D0
	 MOVEP.W   D0,ONAV1TF(A6)      +15	 CLR NAV1 ILS/LOC "OFF"0   VOLT
	 MOVEP.W   D0,ONAV1LR(A6)      +15	 CTR NAV1 ILS/LOC NDLE 0   VOLT
	 MOVEP.W   D0,ONAV1GS(A6)      +15	 CTR NAV1 GS NDLE      0   VOLT
ENDMKRS  EQU	   *
	 MOVE.B    #$FF,OMKRTST(A6)
	 MOVE.B    #$20,OMKRVOL(A6)		 SET MARKER VOLUME LEVEL
	 BCLR.B    #OOMFAIL,NMKRDOB
	 BCLR.B    #OOMRAATT,NMKRDOB
	 BCLR.B    #OMMFAIL,NMKRDOB
	 BCLR.B    #OMMRAATT,NMKRDOB
	 BCLR.B    #OAMFAIL,NMKRDOB
	 BCLR.B    #OAMRAATT,NMKRDOB
	 BTST.B    #IMSTSW,IMPSDI1(A6)
	 BNE.S	   MSTON			 JUMP IF MASTER SW ON
*
	 BCLR.B    #OMKRLTST,NMKRDOB		 CLR MARKER LAMP TEST
	 BRA.S	   CONTIN1
*
MSTON	 BSET.B    #OMKRLTST,NMKRDOB		 SET MARKER LAMP TEST
*
CONTIN1  MOVE.B    NMKRDOB,OMKRDO(A6)		  OUTPUT MARKER DO'S
	 PAGE
*****************************************************************
*
*			NAV2	  (VOR/ILS)
*
*****************************************************************
*
	 BTST.B    #IMSTSW,IMPSDI1(A6) LB	 MASTER ON?		   LB
	 BEQ	   CLRILS2	       .	 NO CLEAR INDICATORS
*
	 BTST.B    #INAV2ON,INAV2F3(A6) LB	  NAVCOM 2 ON?		    LB
	 BEQ	   CLRILS2	       .	 NO
*
	 BTST.B    #INAV2ID,INAV2F3(A6) .	 SEE IF IDENT ON
	 BNE.S	   NV2IDON
	 MOVE.B    #$FF,ONV2VOL(A6)    .	 SET VOLUME OFF
	 BRA.S	   NV2IDOFF	       .	 CONTINUE
*
*		   NAV2 VOLUME CONTROL
*
NV2IDON  MOVEP.W   INAV2VOL(A6),D0     .	 XFER AUDIO VOLUME OFFSET BIN
	 MOVE.B    D0,INAV2VOL(A6)     +15	 START A/D CONVERSION
	 BSR	   CONATT	       .	 ATTEN CONTROL
	 MOVE.B    D0,NNV2VOL	       .	 STORE IN TEMP CORE FOR DEBUG
	 MOVE.B    D0,ONV2VOL(A6)
*
*
*				       STATION RANGE
*
NV2IDOFF TST.B	   NAV2TUN	       LB	 STATION TUNED?
	 BEQ	   CLRILS2	       .	 NO CLR THE NEEDLES
	 MOVE.L    NAV2STA,A1	       .	 YES FIND STA NO
	 BSR	   NAVRCV	       .	 CALL NAV RCVR SUBROUTINE
*
*
	 TST.B	   NRLOS	       .	 IN RANGE & LINE OF SIGHT?
	 BEQ	   CLRILS2	       .	 NO? THEN CLEAR SYSTEM
*
*
*
*			     SIGNAL LEVEL NAV 2 ILS/VTC
*     INAV2VOL =ALIMU((NRRGAS/COR ALT RNG)**2,.99)
*
	 MOVE.W    20(A1),D7	       .	 RANGE 1
	 MOVE.W    NRRGAS,D5	       .	 RANGE TO STATION
	 EXT.L	   D5		       .	 PREPARE TO DIVIDE
	 TST.W	   D7		       .	 CHECK FOR ZERO
	 BNE.S	   NAV2Z	       .	 NOT ZERO, GO TO DIVIDE
	 MOVE.W    #1,D7	       +00	 MAKE RANGE 1 NON-ZERO
NAV2Z	 DIVS	   D7,D5	       +06	 % OF MAX RANGE
	 MULS	   D5,D5	       +12	 % RANGE SQUARED
	 MULS	   #07,D5	       +12+04+16 MULT BY .44 (FUDGE FACTOR)
	 ASR.L	   #8,D5	       +16-08+08 RESCALE
	 CMP.L	   #$80,D5	       +08	 CHECK FOR LIMIT
	 BMI.S	   NAV2Z1	       .	 BRANCH IF OK
	 MOVE.B    #$80,D5	       +08	 LIMIT TO $80 (MUTE)
NAV2Z1	 MOVE.B    D5,NAV2VOL	       +08	 SAVE NAV 2 VOLUME
*
	 SUB.B	   #$80,D5	       +08	 D5-$80    ($80=MUTE)
	 NEG.B	   D5		       +08	 $80-D5 = WHITE NOISE LEVEL
	 MOVE.B    D5,ONV2WNL(A6)      +08	 OUTPUT WHITE NOISE LEVEL
*
*
*			     FIND STA TYPE (VTC/ILS)
*
	 MOVE.W    2(A1),D2	       HEX	 STA TYP
	 CMP.W	   #$0068,D2	       HEX	 VTC ?
	 BEQ	   VOR2 	       .	 YES
	 PAGE
************************************************************
*
*			     NAV2 ILS
*
************************************************************
*
*			     MAG BEARING FROM STATION (+/- 180 DEG)
*     NAV2BRG =NRBRG=(ATAN(NRYGAS/NRXGAS) - 36(A0) = -DEV
*     36(A0)= RNWY MAG HEADING FROM ADDP TABLE
*     DEV =(RNWY MAG HEADING - AIRCRAFT TO ILS STATION BEARING)
*     A/C TO ILS STA BEARING=(ATAN(NRYGAS/NRXGAS))=ATAN(DELTA LON/DELTA LAT)
*
*
*
	 MOVE.W    NRBRG,NAV2BRG       +06	 MAG BRG FROM STATION
*
*			     ANGULAR DIFFERENCE(DEVIATION)
*    -DEV = NCDI2DEG = NAV2BRG	  FOR TUNED ILS STATIONS
*
	 MOVE.L    NAV2STA,A0	       .	 FIND STA SA		   ADDR
*
*	 MOVE.W    36(A0),D0	       +06	 RNWY MAG HDG		   DEG
*	 SUB.W	   NAV2BRG,D0	       +06	 -HDG DIFF		   DEG
*	 NEG.W	   D0		       +06	 CORRECT SIGN		   DEG
*
	 MOVE.W    NAV2BRG,D0	       +06	 -DEV
	 BSR	   NCDI 	       +06	 COURSE DEVIATION	   DEG
	 MOVE.W    D0,NCDI2DEG	       +06	 +DEV			   DEG
*
*			     LOCALIZER INDICATOR DRIVE
*     NCDI2DEG =ALIM(NCDI2DEG ,5)
*
	 CMP.W	   #319,D0	       +06	 GT.4.99		   DEG
	 BPL.S	   PLUS32	       +06	 YES
	 CMP.W	   #-319,D0	       +06	 GT -4.99		   DEG
	 BPL.S	   LMTILS2	       .	 YES
	 MOVE.W    #-319,D0	       +06	 LIMIT TO -4.99 	   DEG
	 BRA.S	   LMTILS2
PLUS32	 MOVE.W    #319,D0	       +06	 LIMIT TO 4.99		   DEG
*
*			     NEEDLE DRIVE
*	      ONAV2LR =DEFLECTION/5.
LMTILS2  EXT.L	   D0
	 DIVS	   #5,D0	       +06-00+06 DIV BY 5		   NORM
	 ASL.W	   #7,D0	       +06+07+13			   NORM
	 ASL.W	   #2,D0	       +13+02+15			   NORM
	 NEG.W	   D0		       .	 FOR NEEDLE DRIVE
*	 ASR.W	   #1,D0	       .	 DIVIDE BY 2(NOT NEEDED)
	 BCHG.L    #15,D0	       .	 SET UP FOR OUTPUT
	 MOVE.W    NAV2LR,D1	       +15	 GET OLD AO
	 BSR	   INTEG1	       .	 INTEGRATE TO NEW AO
	 MOVE.W    D0,NAV2LR	       +15	 STORE NEW AO
	 MOVEP.W   D0,ONAV2LR(A6)      +15	 CDI "AO"                  NORM
*
*			     "ON"("TO") FLAG
*
	 MOVE.W    #$FFFF,D0	       +15	 "TO" FLAG ON FOR ILS
	 MOVEP.W   D0,ONAV2TF(A6)
*
*			     GLIDE SLOPE
*			      (ASSUME IN RNG SINCE ILS IN RNG) AN APPROX
*
	 BTST.B    #IGSFAIL,IFAIL2(A6) LB	 GS MALF?
	 BNE.S	   GSOFF2	       .	 YES
	 MOVE.W    NRRGAS,D0	       +16	 RANGE TO STATION	   DEG
*
	 MULS	   ZFPDEG,D0	       +16-04+12 364566 FT PER DEG	   FEET
	 ASR.L	   #8,D0	       +12-08+04 DIST TO STATION	   FEET
	 MOVE.L    NAV2STA,A0	       .	 SA OF STA
	 ASR.L	   #6,D0	       +04-06-02 DIST.B 		   FEET
	 MOVE.W    42(A0),D1	       +00	 RUNWAY LENGTH		   FEET
	 ASR.W	   #2,D1	       +00-02-02 RUNWAY LENGTH		   FEET
*
*			     GS-DIST TO TOUCHDOWN POINT
*
	 SUB.W	   D1,D0	       -02	 DIST TO TOUCHDOWN 4 RES   FEET
	 MOVE.W    NRZGAS,D1	       +00	 HT. ABOVE STATION	   FEET
	 EXT.L	   D1		       +00	 HT. ABOVE STATION	   FEET
	 ASL.L	   #8,D1	       +00+08+08 HT. ABOVE STATION	   FEET
	 ASL.L	   #8,D1	       +00+08+16 HT. ABOVE STATION	   FEET
	 DIVS	   D0,D1	       +16+02+18 GS PATH ATAN		  RATIO
	 ASR.W	   #8,D1	       +18-08+10 GS PATH ATAN		  RATIO
	 MOVE.W    D1,D0	       +10	 GS PATH ATAN		  RATIO
**	 BSR	   ATAN2	       +10   +06 GS PATH		   DEG
**	 MOVE.W    D0,NGSD2DEG	       +06	 A/C GS 		   DEG
	 CMP.W	   #$0059,D0	       +06	 1.4			   DEG
	 BMI.S	   LT14D2	       +06	 LT 1.4 		   DEG
	 MOVE.W    #$0059,D0	       +06	 NO LIMIT TO 1.4	   DEG
LT14D2	 SUB.W	   #$002C,D0	       +06	 SUB .7 FOR NDL CTR	   DEG
*
*			     GS NEEDLE DRIVE AND FLAG
*     ONAV2GS =DEFLECTION/.7
*
	 MULS	   #$026C,D0	       +06+09+15 DIV BY .7		  RATIO
	 MOVEP.W   D0,ONAV2GS(A6)      +15	 GS NEEDLE DRIVE "AO"      NORM
	 BSET.B    #ONAV2GSF,NCDI2DO   .	 SET GS FLAG "ON"
	 BRA	   ENDVRIL2
*
**			     GS FLAG "OFF"
*
GSOFF2	 BCLR.B    #ONAV2GSF,NCDI2DO   .	 GS FLAG "OFF"
	 MOVE.W    #$8000,D0
	 MOVEP.W   D0,ONAV2GS(A6)      +15	 CENTRE THE GS NDLE "AO" 0 VOLT
	 BRA	   ENDVRIL2
	 PAGE
*************************************************************
*
*			     NAV2 VOR
*
*************************************************************
VOR2	 EQU	   *
	 MOVEP.W   ICRS2SC(A6),D0      +15	 SIN/COS OF SET COURSE
	 BCHG.L    #15,D0	       .	 CONVERT TO 2'S COMPLEMENT
*
*      SCALE SIN/COS INPUT
*
	 BTST.B    #OCRS2SEL,NCDI2DO   .	 READING SINE OR COS?
	 BEQ.S	   SINE2	       .
*
	 MOVE.W    D0,NCRS2COS	       +15	 SAVE COSINE
	 BCLR.B    #OCRS2SEL,NCDI2DO   .	 TOGGLE THE SIN/COS BIT
	 BRA.S	   CON2
*
SINE2	 MOVE.W    D0,NCRS2SIN	       +15	 SAVE THE SINE
	 BSET.B    #OCRS2SEL,NCDI2DO   .	 TOGGLE THE SIN/COS BIT
*
CON2	 MOVE.B    NCDI2DO,OCDI2DO(A6) .	 OUTPUT TEMP TO OUTPUT
*
	 MOVE.W    NCRS2COS,D0	       +15	 PREPARE FOR NATAN2
	 MOVE.W    NCRS2SIN,D1	       +15	 PREPARE FOR NATAN2
	 EXT.L	   D1		       +15
	 EXT.L	   D0		       +15
	 BSR	   NATAN2			 RESOLVE SIN/COS TO DEG
	 MOVE.W    D0,NAV2OBS	       +06	 SAVE NAV2 SELECTED BEARING
*
*
*			     TEST IF NAV2 PUSH-TO-TEST ON
*			     IF ON THEN NRBRG=180 DEGREES
*
	 BTST.B    #INAV2TST,ICRS2DI(A6)	 TEST IF PUSH TO TEST PRESSED
	 BEQ.S	   NAV2CON1
	 MOVE.W    #11520,NRBRG        +06	 NRBRG=180 DEG
*
*			     MAG BEARING FROM STATION(0-360)
*     NAV2BRG =TRUE BRG +NMAGVAR
*
NAV2CON1 MOVE.W    NRBRG,NAV2BRG       +06	 MAG BRG FROM STA	   DEG
*
*   DRIVE RMI VOR POINTER #2.(THIS OUTPUT NOT USED PRESENTLY IN TEST BED)
*			     (WHERE RMI IS REPLACED BY DG.)
*
	 MOVE.W    NAV2BRG,D0	       +06	 DRIVE RMI PTR 2 W/ VOR 2 BRG
	 SUB.W	   NPSIMH,D0	       +06	 SUBTRACT MAG HDG
	 BSR	   AEPC 	       +06	 LIMIT 0-360
	 BSR	   MSC1 	      . 	GET SIN/COS OF BEARING
	 BCHG.L    #15,D0	       +15	 CHANGE MSB FOR OUTPUT
	 BCHG.L    #15,D1	       +15	 CHANGE MSB FOR OUTPUT
	 MOVEP.W   D0,ORMI2S(A6)       +15	 SINE OF PTR 2
	 MOVEP.W   D1,ORMI2C(A6)       +15	 COSINE OF PTR 2
*
*			     ANGULAR DIFFERENCE(DEVIATION)
*     DEVIATION  =NAV2OBS -NAV2BRG
*
	 MOVE.W    NRBRG,D0	       +06
	 SUB.W	   NAV2OBS,D0	       +06	 -DEVIATION		   DEG
	 BSR	   AEPC
*	 BSR	   NCDI 	       +06	  DEVIATION		   DEG
	 MOVE.W    D0,NCDI2DEG	       +06	 DEVIATION		   DEG
*
*			     VTC       INDICATOR DRIVE
*     NCDI2DEG =ALIM(NCDI2DEG ,12.)
*
*	 CMP.W	   #1279,D0	       +06	 GT.19.99		   DEG
*	 BPL.S	   PLUS122	       .	 YES
*	 CMP.W	   #-1279,D0	       +06	 GT -19.99		   DEG
*	 BPL.S	   LMTVOR2	       .	 YES
*	 MOVE.W    #-1279,D0	       +06	 LIMIT -19.99		   DEG
*	 BRA.S	   LMTVOR2	       .
*PLUS122  MOVE.W    #1279,D0		+06	  LIMIT TO 19.99	    DEG
*
*			     NEEDLE DRIVE
*		ONAV2LR =DEFLECTION/12,
*LMTVOR2  EXT.L     D0
*	 DIVS	   #20,D0	       +06-00+06 DIV BY 20		   NORM
*	 ASL.W	   #7,D0	       +06+07+13			   NORM
*	 ASL.W	   #2,D0	       +13+02+15			   NORM
*	 NEG.W	   D0		       +15	 FOR DRIVING NEEDLE
**	 ASR.W	   #1,D0	       +15	 DIVIDE BY 2(NOT NEEDED)
*
*
*	 CDI NEEDLE = SIN(-DEV) LIMIT TO +/- 4 DOTS, +/- 2 DOTS=YELLOW EDGE
*	  INPUT = -DEV IN D0.W AT +06  = NCDI2DEG
*
	 BSR	   MSC1 			 SIN IN D0, COS IN D1 AT +15
	 MULS	   #737,D0	 +15+06+21	 D0 = 11.517472(SIN(-DEV)) DOTS
*
*     LIMIT # OF DOTS TO +/- 4 BEFORE OUTPUTTING = +/- 10 VOLTS
*			 +/- 2 DOTS = 5 V =(EDGE OF YELLOW = 3 DOTS ON VOA9)
*
	 CMPI.L    #8388607,D0	       +21	 IS D0 < 3.999 DOTS ?
	 BLT.S	   VOR2LM1
	 MOVE.L    #8388607,D0	       +21	 LIMIT TO +3.999 DOTS
	 BRA.S	   VOR2LM2
VOR2LM1  CMPI.L    #-8388607,D0        +21	 IS D0 < -3.999 DOTS ?
	 BGT.S	   VOR2LM2
	 MOVE.L    #-8388607,D0        +21	 LIMIT TO -3.999 DOTS
VOR2LM2  EQU	   *
*
	 DIVS	   #256,D0	 +21-06+15	 DIV BY 4 TO NORMALISE
*
	 BCHG.L    #15,D0	       +15	 CHANGE BIT 15 FOR OUTPUT
	 MOVEP.W   D0,ONAV2LR(A6)      +15	 CDI "AO"                  NORM
*
*
*	 TO-OFF-FROM FLAG CALCULATIONS
*	 -DEV IN NCDI2DEG = NAV2BRG - NAV2OBS
*	 D1.W = COS(-DEV) AT +15
*	     -75 < -DEV < 75 DEG       FLAG = "TO"
*	    -105 > -DEV >105 DEG       FLAG = "FROM"
*  75 < -DEV <105 DEG	OR -105 < -DEV <-75 DEG FLAG BETWEEN TO-OFF-FROM
*
*
	 MOVE.W    D1,D2	       +15	 SAVE COS FOR SIGN
*
*
*
	 BCHG.L    #15,D1	       +15	 FROM BTC TO BOB
	 MOVEP.W   D1,ONAV2TF(A6)      +15	 SET "TO/FROM" FLAG        VOLT
*
*			     CONE OF CONFUSION
*
*
*
*			     CONE RADIUS
*
	 MOVE.W    NRZGAS,D7	       +00	 HEIGHT ABOVE STATION	   FEET
	 MULS	   #591,D7	       +10	 CONE RADIUS		   FEET
	 DIVS	   #6080,D7	       +10-00+10 CONE RADIUS		   NM
	 ASR.W	   #4,D7	       +10-04+06 RESCALE
	 CMP.W	   NRRGAS,D7	       +04	 CONE RADIUS <= A/C DIST ? FEET
	 BLE.S	   OUTCONE2	       .	 YES
	 MOVE.W    #$8000,D0
	 MOVEP.W   D0,ONAV2TF(A6)      +15	 NO SET FLAG "OFF"   0     VOLT
OUTCONE2 EQU	   *
*
*			     MALF LOC/VTC (CDI)
*
ENDVRIL2 BTST.B    #IVL2FAIL,IFAIL1(A6) LB	  MALF VTC/ILS ?
	 BEQ.S	   ENDVL2	       .	 NO
	 MOVE.W    #$8000,D0
	 MOVEP.W   D0,ONAV2LR(A6)      +15	 YES CENTER CDI      0	   VOLT
	 MOVEP.W   D0,ONAV2LR(A6)      +15	 YES SET "OFF" FLAG  0     VOLT
	 BRA.S	   ENDVL2
*
*			     ILS/VTC NOT IN RANGE
*
CLRILS2  BCLR.B    #ONAV2GSF,NCDI2DO   .	 GLIDE SLOPE FLAG "OFF"    LB
	 SF.B	   NAV2TUN	       LB	 CLEAR NAV1 TUNED	   LB
	 CLR.L	   NAV1FREQ	       +00	 CLEAR NAV2 FREQ
	 MOVE.W    #$8000,D0
	 MOVEP.W   D0,ONAV2TF(A6)      +15	 SET LOC/ILS FLAG "OFF" 0  VOLT
	 MOVEP.W   D0,ONAV2LR(A6)      +15	 CENTER CDI 1 NEEDLE	0  VOLT
	 MOVEP.W   D0,ONAV2GS(A6)      +15	 CENTER GSDI  NEEDLE	0  VOLT
	 MOVE.B    #$FF,ONV2WNL(A6)    +08	 MUTE WHITE NOISE LEVEL
	 MOVE.B    #$FF,ONV2VOL(A6)    +08	 TURN OFF NAV2 VOL ATTN
	 ST.B	   NAV2VOL	       +06	 SET IDENT LEVEL TO MAX ATTEN
ENDVL2	 EQU	   *
	 PAGE
**********************************************************
*
*			     ADF
*
**********************************************************
	 BTST.B    #IMSTSW,IMPSDI1(A6) LB	 MASTER ON?		   DI
	 BEQ	   CLRADF	       .	 NO CLEAR INDICATORS
	 BTST.B    #IADF1ADF,IADF1DI(A6) LB	   ADF	    ON? 	     DI
	 BNE.S	   ADFON
	 BTST.B    #IADF1REC,IADF1DI(A6) LB	   NO TRY "REC" SW POS       DI
	 BEQ	   CLRADF	       .	 NO
*
*				       STATION RANGE
*
ADFON	 MOVE.B    #$FF,OLF2VOL(A6)    +08	 ADF2 IS ALWAYS OFF
	 MOVEP.W   IADF1VOL(A6),D0     ADDR	 READ IADF1VOL OFFSET BINARY
	 MOVE.B    D0,IADF1VOL(A6)     .	 START A/D CONVERSION
	 BSR	   CONATT	       .	 ATTEN CONTROL
	 MOVE.B    D0,OLF1VOL(A6)      +00	 OUTPUT OLF1VOL
*
	 TST.B	   NADF1TUN	       .	 ADF TUNED ?
	 BEQ	   NOTTUN1	       .	 YES: BRANCH
	 MOVE.L    NADF1STA,A1	       .	 NO:  FIND STA NO
	 BSR	   NAVRCV
*
	 SF.B	   NLF1LOS	       .	 CLEAR ADF 1 LOS FLAG
	 TST.B	   NRLOS	       .	 IN RANGE?
	 BEQ	   CLRADF	       .	 OUT OF LON RANGE
	 ST.B	   NLF1LOS	       .	 SET ADF1 LOS FLAG
*
*
*
*
*			     SIGNAL LEVEL ADF
*
*	 NLF1VOL = (( NRRGAS / 120.0 ) **2 + NADF1ERR / 2000.0 ) LIM 0.9999
*
	 MOVE.W    NRRGAS,D0	       +06	 RANGE TO STATION
	 EXT.L	   D0		       +06	 EXTEND FOR DIVIDE
	 DIVS	   #120,D0	       +06-00+06 DIVIDE BY MAX RANGE
	 MULS	   D0,D0	       +06+06+12 SQUARE IT
	 MOVE.L    NADF1ERR,D1	       +00	 GET FREQ DIFF
	 ASL.L	   #8,D1	       +00+08+08 RESCALE
	 ASL.L	   #4,D1	       +08+04+12 RESCALE
	 DIVS	   #2000,D1	       +12-00+12
	 CMP.W	   #4095,D1	       +12	 > 1.0
	 BLT.S	   NOLIM1
	 MOVE.W    #4095,D1	       +12	 LIMIT TO .999
*
NOLIM1	 ADD.W	   D1,D0	       +12
	 CMP.W	   #4095,D0	       +12	 > 1.0
	 BLT.S	   NOLIM2
	 MOVE.W    #4095,D0	       +12	 LIMIT TO .999
*
NOLIM2	 ASR.W	   #4,D0	       +08	 RESCALE TO BYTE
	 MOVE.B    D0,NLF1VOL
	 MOVE.W    #$00FF,D1	       +08	 .999
	 SUB.W	   D0,D1	       +08	 .999 - NLF1RNKY
	 MOVE.B    D1,OLF1WNL(A6)      +08	 ADF1 WHITE NOISE LEVEL
*
*
*
*
*			     "REC" SW POSITION ?
*
	 BTST.B    #IADF1REC,IADF1DI(A6) LB	   "REC"  SW POS ?           DI
	 BNE.S	   ENDADF	       .	 YES SKIP NEEDLE DRIVE
*			     TEST ?
*
	 BTST.B    #IADF1TST,IADF1DI(A6) LB	   TEST SW   ?		     DI
	 BEQ.S	   RELBRG	       .	 NO
	 MOVE.W    NADF1BRG,D0	       +06	 BRG TO STA		   DEG
	 ADD.W	   #$0080,D0	       +06	 INCRS POINTER AT 2D/ITER  DEG
	 BRA.S	   LIM360R
*
*
RELBRG	 EQU	   *
*
*			     ADF RELATIVE BEARING TO STATION (POINTER
*     NADF1BRG = NRBRG - NPSIMH  (0-360)
*
	 MOVE.W    NRBRG,D0	       +06	 GET STATION BEARING	   DEG
	 SUB.W	   NPSIMH,D0	       +06	 BRG FROM - HDG 	   DEG
LIM360R  BSR	   AEPC 	       +06	 LIMIT TO +/- 180 DEG	   DEG
	 MOVE.W    D0,NADF1BRG	       +06	 ADF REL BRG TO STA (PTR)  DEG
*
*				       NEEDLE DRIVE
*			     MALF ADF
*
	 BTST.B    #ILF1FAIL,IFAIL2(A6) LB	  MALF ADF ?
	 BNE.S	   ENDADF	       .	 YES
*
*   DRIVE RMI VOR POINTER #1 OR ADF POINTER ON ADF CONTROL HEAD FOR TEST BED
*
*	 BTST.B    #IRMI1ADF,IMPSDI1(A6)
*	 BNE.S	   NADFPTR	       .	 PTR 1 NOT ADF
	 MOVE.W    NADF1BRG,D0	       +06	 DRIVE RMI PTR 1 W/ ADF 1 BRG
	 BSR	   MSC1 	      . 	GET SIN/COS OF BEARING
	 BCHG.L    #15,D0
*	 BCHG.L    #15,D1
	 MOVEP.W   D0,ORMI1S(A6)       +15	 SIN OF PTR 1
*
	 MOVE.W    NADF1BRG,D0	       +06
	 ADD.W	   #3840,D0	       +06	 ADD 60 DEG
	 BSR	   AEPC 	       +06	 LIMIT TO +/- 180 DEG
	 BSR	   MSC1
	 BCHG.L    #15,D0	       +15
	 MOVEP.W   D0,ORMI1C(A6)       +15	 OUTPUT COSINE
*
*	 MOVEP.W   D1,ORMI1C(A6)       +15	 COS OF PTR 1
NADFPTR  EQU	   *
*
	 BRA.S	   ENDADF
*
NOTTUN1  MOVE.B    #0,OLF1WNL(A6)      .	 SET MAX WHITE NOISE IF NOT TUN
	 BRA.S	   ENDADF
*
*			     ADF     NOT IN RANGE
*			 NO  POWER ADF	 CLEAR/FRZ EVERYTHING
*
CLRADF	 EQU	   *
	 SF.B	   NADF1TUN	       LB	 CLR ADF  IN TUNE
	 MOVE.B    #$FF,NLF1VOL       +15	CLR ADF  IDENT LEVEL  0   NOR
	 MOVE.B    #$FF,OLF1WNL(A6)
ENDADF	 EQU	   *
	 MOVE.B    NCDI1DO,OCDI1DO(A6) .	 SEND TEMP CORE TO OUTPUT
	 MOVE.B    NCDI2DO,OCDI2DO(A6) .	 SEND TEMP CORE TO OUTPUT
	 MOVE.B    D0,ICRS1SC(A6)      .	 START NEXT A/D CONVERSION
	 MOVE.B    D0,ICRS2SC(A6)      .	 START NEXT A/D CONVERSION
*
*
	 TRAP	   #0
	 PAGE
*
	 XDEF	   CONATT
*
*		   MAKE VOLUME CONTROL LINEAR
*	 INPUT:    D0.W      OFFSET BINARY INPUT FROM AI
*	 OUTPUT:   D0.B      BYTE OUTPUT FOR MAO (7111)
*
CONATT	 EQU	   *
	 BCHG.L    #15,D0	       +15	 CONVERT TO 2'S COMPLEMENT
	 BTST.L    #15,D0	       +15	 CHECK FOR VALID AI
	 BEQ.S	   VOLOK1	       .	 BRANCH IF VALID
	 MOVE.W    #0,D0	       +15	 ELSE LIMIT TO 0
VOLOK1	 EXT.L	   D0		       +15	 SET UP FOR DIVIDE
	 CMP.L	   #32700,D0	       +15	 CHECK FOR UPPER LIMIT
	 BMI.S	   BREAK1	       .
*
	 MOVE.B    #$FF,D0	       +08	 SET AT MAX ATTEN
	 RTS
*
BREAK1	 CMP.L	   #16000,D0	       +15	 CHECK FOR BREAKPOINT
	 BMI.S	   BREAK2	       .	 BRANCH IF <16000
*						 16001 < X < 32700
	 SUB.L	   #16000,D0	       +15	 SUBTRACT 16000
	 DIVS	   #87,D0	       +15-05+10 DIVIDE BY 2.7
	 ASR.W	   #2,D0	       +08	 CHANGE TO BYTE
	 ADD.W	   #$20,D0	       +08
	 RTS
*						 0 < X < 16000
BREAK2	 DIVS	   #125,D0	       +15-05+10 DIVIDE BY 3.9
	 ASR.W	   #2,D0	       +08	 CHANGE TO BYTE
	 RTS
	 PAGE
***************************************************
*
*		   INTEGRATION ROUTINE
*
***************************************************
*
*	 INPUTS:   D1.W - OLD VALUE (CURRENT AO)   +15
*		   D0.W - NEW VALUE (DESIRED AO)   +15
*
*	 OUTPUT:   D0.W - NEW AO		   +15
*
INTEG1	 SUB.W	   D1,D0	       +15	 NEW-OLD
	 MULS	   #102,D0	       +15+10+25 MULT BY INTEG CONSTANT
	 ASR.L	   #8,D0	       +25-08+17 RESCALE
	 ASR.L	   #2,D0	       +17-02+15 RESCALE
	 ADD.W	   D1,D0	       +15	 UPDATE OLD
	 RTS
~~ ...M68K D2.0C  3/12/85   ...Run on Dec 4, 2025  13:48:41
	 END
ON ROUTINE
*
***************************************************
*
*	 INPUTS:   D1.W - OLD VALUE (CURRENT AO)   +15
*		   D0.W - NEW VALUE (DESIRED AO)   +15
*
*	 OUTPUT:   D0.W - NEW AO		   +15
*
INTEG1	 SUB.W	   D1,D0	       +15	 NEW-OLD
	 MULS	   #102,D0	       +15+10+25