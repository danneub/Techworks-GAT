	 OPT	   MEX,CEX,NOPCS
	 XDEF	   NAVFREQ
*
	 INCLUDE   NSYM.SA
	 INCLUDE   IO.SA
	 INCLUDE   ADDP.SA
*
	 SECTION   12
**********************************************************************
*								     *
*	 NAV, ADF, DME, AND MARKER DECODE AND SEARCH ROUTINES	     *
*								     *
**********************************************************************
NAVFREQ  EQU	   *
	 MOVE.L    #$FE6000,A6			 LOAD I/O BASE ADDR
	 BTST.B    #IMSTSW,IMPSDI1(A6)		 DON'T DECODE OR TUN IF
	 BEQ	   MSTOFF			 MASTER SWITCH OFF
*
* NAV1 DECODE
*
	 BTST.B    #INAV1ON,INAV1F3(A6) 	 SKIP DECODE AND SEARCH
	 BNE.S	   NAV1DEC			 CONTINUE IF NAV1 SW ON
*
	 SF.B	   NAV1TUN			 CLEAR NAV1TUN FLAG
	 CLR.L	   NAV1FREQ			 CLEAR NAV1 FREQ
	 BRA	   SKIP 			 SKIP
*
NAV1DEC  MOVE.L    #117000000,D0
	 BTST.B    #I117NAV1,INAV1F1(A6)	 JUMP IF 117 BIT SET
	 BNE.S	   DECODE2
*
	 MOVE.L    #116000000,D0
	 BTST.B    #I116NAV1,INAV1F1(A6)	 JUMP IF 116 BIT SET
	 BNE.S	   DECODE2
*
	 MOVE.L    #115000000,D0
	 BTST.B    #I115NAV1,INAV1F1(A6)	 JUMP IF 115 BIT SET
	 BNE.S	   DECODE2
*
	 MOVE.L    #114000000,D0
	 BTST.B    #I114NAV1,INAV1F1(A6)	 JUMP IF 114 BIT SET
	 BNE.S	   DECODE2
*
	 MOVE.L    #113000000,D0
	 BTST.B    #I113NAV1,INAV1F1(A6)	 JUMP IF 113 BIT SET
	 BNE.S	   DECODE2
*
	 MOVE.L    #112000000,D0
	 BTST.B    #I112NAV1,INAV1F1(A6)	 JUMP IF 112 BIT SET
	 BNE.S	   DECODE2
*
	 MOVE.L    #111000000,D0
	 BTST.B    #I111NAV1,INAV1F1(A6)	 JUMP IF 111 BIT SET
	 BNE.S	   DECODE2
*
	 MOVE.L    #110000000,D0
	 BTST.B    #I110NAV1,INAV1F1(A6)	 JUMP IF 110 BIT SET
	 BNE.S	   DECODE2
*
	 MOVE.L    #109000000,D0
	 BTST.B    #I109NAV1,INAV1F2(A6)	 JUMP IF 109 BIT SET
	 BNE.S	   DECODE2
*
	 MOVE.L    #108000000,D0
DECODE2  EQU	   *
	 MOVE.L    #900000,D1
	 BTST.B    #I9THNAV1,INAV1F2(A6)	 JUMP IF .9 BIT SET
	 BNE.S	   DECODE3
*
	 MOVE.L    #800000,D1
	 BTST.B    #I8THNAV1,INAV1F2(A6)	 JUMP IF .8 BIT SET
	 BNE.S	   DECODE3
*
	 MOVE.L    #700000,D1
	 BTST.B    #I7THNAV1,INAV1F2(A6)	 JUMP IF .7 BIT SET
	 BNE.S	   DECODE3
*
	 MOVE.L    #600000,D1
	 BTST.B    #I6THNAV1,INAV1F2(A6)	 JUMP IF .6 BIT SET
	 BNE.S	   DECODE3
*
	 MOVE.L    #500000,D1
	 BTST.B    #I5THNAV1,INAV1F2(A6)	 JUMP IF .5 BIT SET
	 BNE.S	   DECODE3
*
	 MOVE.L    #400000,D1
	 BTST.B    #I4THNAV1,INAV1F2(A6)	 JUMP IF .4 BIT SET
	 BNE.S	   DECODE3
*
	 MOVE.L    #300000,D1
	 BTST.B    #I3THNAV1,INAV1F3(A6)	 JUMP IF .3 BIT SET
	 BNE.S	   DECODE3
*
	 MOVE.L    #200000,D1
	 BTST.B    #I2THNAV1,INAV1F3(A6)	 JUMP IF .2 BIT SET
	 BNE.S	   DECODE3
*
	 MOVE.L    #100000,D1
	 BTST.B    #I1THNAV1,INAV1F3(A6)	 JUMP IF .1 BIT SET
	 BNE.S	   DECODE3
*
	 MOVE.L    #000000,D1			 DEFAULT TO .0
DECODE3  EQU	   *
	 ADD.L	   D1,D0			 CALULATE FREQ
****************************************************************
*
*	 DECODE FINISHED NOW SEARCH TABLE
*
****************************************************************
	 CMP.L	   NAV1FREQ,D0	       COMPARE NEW FREQ WITH OLD
	 BEQ.S	   SKIP 	       SKIP SEARCH IF SAME AS LAST
*
	 MOVE.L    D0,NAV1FREQ	       STORE NEW FREQ
	 MOVEA.L   #NDDPTAB,A0	       LOAD TABLE STARTING ADDRESS
	 MOVEA.L   #NVOREND,A1	       LOAD TABLE ENDING ADDRESS
	 BSR	   TABSERCH	       SEARCH TABLE
	 MOVE.B    D1,NAV1TUN	       STORE VALIDITY FLAG
	 MOVE.L    A0,NAV1STA	       STORE STATION ADDR
*****************************************************************
*
*		   SEARCH DME TABLE
*
*****************************************************************
	 MOVE.L    NAV1FREQ,D0	       GET NAV1 FREQ
	 MOVEA.L   #NDDPDME,A0	       LOAD TABLE STARTING ADDRESS
	 MOVEA.L   #NFTABEND,A1        LOAD TABLE ENDING ADDRESS
	 BSR	   TABSERCH	       SEARCH TABLE
*
	 MOVE.B    D1,NDME1TUN	       STORE VALIDIY FLAG
	 MOVE.L    A0,NDME1STA	       STORE STATION ADDRESS
SKIP	 EQU	   *
*******************************************************
*
*	 NAV2 FREQ DECODE AND RANGE SEARCH ROUTINE
*
*******************************************************
	 BTST.B    #INAV2ON,INAV2F3(A6) 	 SKIP DECODE AND SEARCH
	 BNE.S	   NAV2DEC			 CONTINUE IF NAV2 SW ON
	 SF.B	   NAV2TUN
	 CLR.L	   NAV2FREQ
	 BRA	   SKIP2			 SKIP IF NAV2 SW NOT ON
*
NAV2DEC  MOVE.L    #117000000,D0
	 BTST.B    #I117NAV2,INAV2F1(A6)	 JUMP IF 117 BIT SET
	 BNE.S	   DECODE5
*
	 MOVE.L    #116000000,D0
	 BTST.B    #I116NAV2,INAV2F1(A6)	 JUMP IF 116 BIT SET
	 BNE.S	   DECODE5
*
	 MOVE.L    #115000000,D0
	 BTST.B    #I115NAV2,INAV2F1(A6)	 JUMP IF 115 BIT SET
	 BNE.S	   DECODE5
*
	 MOVE.L    #114000000,D0
	 BTST.B    #I114NAV2,INAV2F1(A6)	 JUMP IF 114 BIT SET
	 BNE.S	   DECODE5
*
	 MOVE.L    #113000000,D0
	 BTST.B    #I113NAV2,INAV2F1(A6)	 JUMP IF 113 BIT SET
	 BNE.S	   DECODE5
*
	 MOVE.L    #112000000,D0
	 BTST.B    #I112NAV2,INAV2F1(A6)	 JUMP IF 112 BIT SET
	 BNE.S	   DECODE5
*
	 MOVE.L    #111000000,D0
	 BTST.B    #I111NAV2,INAV2F1(A6)	 JUMP IF 111 BIT SET
	 BNE.S	   DECODE5
*
	 MOVE.L    #110000000,D0
	 BTST.B    #I110NAV2,INAV2F1(A6)	 JUMP IF 110 BIT SET
	 BNE.S	   DECODE5
*
	 MOVE.L    #109000000,D0
	 BTST.B    #I109NAV2,INAV2F2(A6)	 JUMP IF 109 BIT SET
	 BNE.S	   DECODE5
*
	 MOVE.L    #108000000,D0
DECODE5  EQU	   *
	 MOVE.L    #900000,D1
	 BTST.B    #I9THNAV2,INAV2F2(A6)	 JUMP IF .9 BIT SET
	 BNE.S	   DECODE6
*
	 MOVE.L    #800000,D1
	 BTST.B    #I8THNAV2,INAV2F2(A6)	 JUMP IF .8 BIT SET
	 BNE.S	   DECODE6
*
	 MOVE.L    #700000,D1
	 BTST.B    #I7THNAV2,INAV2F2(A6)	 JUMP IF .7 BIT SET
	 BNE.S	   DECODE6
*
	 MOVE.L    #600000,D1
	 BTST.B    #I6THNAV2,INAV2F2(A6)	 JUMP IF .6 BIT SET
	 BNE.S	   DECODE6
*
	 MOVE.L    #500000,D1
	 BTST.B    #I5THNAV2,INAV2F2(A6)	 JUMP IF .5 BIT SET
	 BNE.S	   DECODE6
*
	 MOVE.L    #400000,D1
	 BTST.B    #I4THNAV2,INAV2F2(A6)	 JUMP IF .4 BIT SET
	 BNE.S	   DECODE6
*
	 MOVE.L    #300000,D1
	 BTST.B    #I3THNAV2,INAV2F3(A6)	 JUMP IF .3 BIT SET
	 BNE.S	   DECODE6
*
	 MOVE.L    #200000,D1
	 BTST.B    #I2THNAV2,INAV2F3(A6)	 JUMP IF .2 BIT SET
	 BNE.S	   DECODE6
*
	 MOVE.L    #100000,D1
	 BTST.B    #I1THNAV2,INAV2F3(A6)	 JUMP IF .1 BIT SET
	 BNE.S	   DECODE6
*
	 MOVE.L    #000000,D1			 DEFAULT TO .0
*
DECODE6  EQU	   *
	 ADD.L	   D1,D0			 CALULATE FREQ
****************************************************************
*
*	 DECODE FINISHED NOW SEARCH TABLE
*
****************************************************************
	 CMP.L	   NAV2FREQ,D0	       COMPARE NEW FREQ WITH OLD
	 BEQ.S	   SKIP2	       SKIP SEARCH IF SAME AS LAST
*
	 MOVE.L    D0,NAV2FREQ	       STORE NEW FREQ
	 MOVEA.L   #NDDPTAB,A0	       LOAD TABLE STARTING ADDRESS
	 MOVEA.L   #NVOREND,A1	       LOAD TABLE ENDING ADDRESS
	 BSR	   TABSERCH	       SEARCH TABLE
*
	 MOVE.B    D1,NAV2TUN	       STORE VALIDITY FLAG
	 MOVE.L    A0,NAV2STA	       FOUND: STORE STATION ADDRESS
*****************************************************************
*
*		   SEARCH DME TABLE
*
*****************************************************************
	 MOVE.L    NAV2FREQ,D0	       GET NAV2 FREQ
	 MOVEA.L   #NDDPDME,A0	       LOAD TABLE STARTING ADDR
	 MOVEA.L   #NFTABEND,A1        LOAD TABLE ENDING ADDR
	 BSR	   TABSERCH	       SEARCH TABLE
*
	 MOVE.B    D1,NDME2TUN	       STORE VALIDITY FLAG
	 MOVE.L    A0,NDME2STA	       FOUND: STORE STATION ADDRESS
SKIP2	 EQU	   *
***************************************************************
*
*		   ADF1 FREQENCY DECODE
*
***************************************************************
	 BTST.B    #ILF1FAIL,IFAIL2(A6) CHECK FOR ADF MALFUNCTION
	 BNE	   NOTTUNED	       JUMP IF FAILED
*
	 BTST.B    #IADF1REC,IADF1DI(A6)
	 BNE.S	   ADF1DCD	       JUMP IF ADF RECEIVER MODE
*
	 BTST.B    #IADF1ADF,IADF1DI(A6)
	 BEQ	   NOTTUNED	       EXIT IF NOT ADF OR REC MODE
ADF1DCD  EQU	   *
	 MOVEP.W   IADF1FRQ(A6),D2     READ RAW FREQ
	 MOVE.B    D2,IADF1FRQ(A6)     START NEXT A/D CONVERSION
	 LSL.W	   #1,D2	       SHIFT OUT MSB (ALWAYS 1)
	 LSR.W	   #5,D2	       SHIFT OUT 4 LSB'S (UNWANTED)  +00
*
	 BTST.B    #IADF1B1,IADF1DI(A6) CHECK FOR BAND1
	 BNE.S	   ADF1B1
*
	 BTST.B    #IADF1B2,IADF1DI(A6) CHECK FOR BAND2
	 BNE	   ADF1B2
*
****************************************
*		   BAND 3
****************************************
	 CMP.L	   #9000,N3KBAND       SEE IF BAND NUM CHANGED
	 BNE.S	   NEWFRQ3	       JUMP IF BAND JUST CHANGED
*
	 CMP.W	   NADF1RAW,D2	       COMPARE LAST RAW FRQ WITH NEW
	 BEQ	   ADF1END	       AND EXIT IF SAME
*
NEWFRQ3  MOVE.W    D2,NADF1RAW	       STORE NEW RAW FREQ
	 MOVE.L    #9000,N3KBAND       STORE NEW BAND NUMBER
	 CMPI.W    #192,D2	       CHECK FOR GRAPH BREAKPOINT
	 BLT.S	   ADF1B3A
*
	 CMPI.W    #704,D2	       CHECK FOR GRAPH BREAKPOINT
	 BLT.S	   ADF1B3B
*
	 MOVE.L    #709000,D0	       LOAD Y INTERCEPT 	     +00
	 MOVE.W    #16640,D1	       LOAD SLOPE		     +05
	 BRA	   ADF1CON	       CONTINUE WITH CALCULATION
ADF1B3A  EQU	   *
	 MOVE.L    #840000,D0	       LOAD Y INTERCEPT 	     +00
	 MOVE.W    #7488,D1	       LOAD SLOPE		     +05
	 BRA	   ADF1CON	       CONTINUE WITH CALCULATION
ADF1B3B  EQU	   *
	 MOVE.L    #814000,D0	       LOAD Y INTERCEPT 	     +00
	 MOVE.W    #11872,D1	       LOAD SLOPE		     +05
	 BRA	   ADF1CON	       CONTINUE WITH CALCULATION
****************************************
*		   BAND 1
****************************************
ADF1B1	 EQU	   *
	 CMP.L	   #3000,N3KBAND       SEE IF BAND CHANGED
	 BNE.S	   NEWFRQ1	       JUMP IF BAND JUST CHANGED
*
	 CMP.W	   NADF1RAW,D2	       COMPARE NEW RAW FRQ WITH LAST
	 BEQ	   ADF1END	       AND EXIT IF SAME
*
NEWFRQ1  MOVE.W    D2,NADF1RAW	       STORE NEW RAW FREQ
	 MOVE.L    #3000,N3KBAND       STORE NEW BAND NUMBER
*
	 CMPI.W    #250,D2	       GRAPH BREAKPOINT= 250 (NOT 192)
*
	 BLT.S	   ADF1B1A
*
	 CMPI.W    #704,D2	       CHECK FOR GRAPH BREAKPOINT
	 BLT.S	   ADF1B1B
*
	 MOVE.L    #149000,D0	       LOAD Y INTERCEPT 	     +00
	 MOVE.W    #4460,D1	       LOAD SLOPE=4460 (NOT 4512)    +05
	 BRA.S	   ADF1CON	       CONTINUE WITH CALCULATION
ADF1B1A  EQU	   *
	 MOVE.L    #190000,D0	       LOAD Y INTERCEPT 	     +00
*
	 MOVE.W    #1700,D1	       LOAD SLOPE=1700 (NOT 1834)    +05
*
	 BRA.S	   ADF1CON	       CONTINUE WITH CALCULATION
ADF1B1B  EQU	   *
	 MOVE.L    #186000,D0	       LOAD Y INTERCEPT 	     +00
	 MOVE.W    #2650,D1	       LOAD SLOPE=2650 (NOT 2560)    +05
	 BRA.S	   ADF1CON	       CONTINUE WITH CALCULATION
****************************************
*		   BAND 2
****************************************
ADF1B2	 EQU	   *
	 CMP.L	   #6000,N3KBAND       CHECK FOR BAND 2
	 BNE.S	   NEWFRQ2	       JUMP IF BAND JUST CHANGED
*
	 CMP.W	   NADF1RAW,D2	       COMPARE WITH LAST
	 BEQ	   ADF1END	       AND EXIT IF SAME
*
NEWFRQ2  MOVE.W    D2,NADF1RAW	       STORE NEW RAW FREQ
	 MOVE.L    #6000,N3KBAND       STORE NEW BAND NUMBER
	 CMPI.W    #192,D2	       CHECK FOR GRAPH BREAKPOINT
	 BLT.S	   ADF1B2A
*
	 CMPI.W    #704,D2	       CHECK FOR GRAPH BREAKPOINT
	 BLT.S	   ADF1B2B
*
	 CMPI.W    #1728,D2	       CHECK FOR GRAPH BREAKPOINT
	 BLT.S	   ADF1B2C
*
	 MOVE.L    #509000,D0	       LOAD Y INTERCEPT 	     +00
	 MOVE.W    #5376,D1	       LOAD SLOPE		     +05
	 BRA.S	   ADF1CON	       CONTINUE WITH CALCULATION
ADF1B2A  EQU	   *
	 MOVE.L    #420000,D0	       LOAD Y INTERCEPT 	     +00
	 MOVE.W    #4000,D1	       LOAD SLOPE		     +05
	 BRA.S	   ADF1CON	       CONTINUE WITH CALCULATION
ADF1B2B  EQU	   *
	 MOVE.L    #408000,D0	       LOAD Y INTERCEPT 	     +00
	 MOVE.W    #5500,D1	       LOAD SLOPE=5500 (NOT 6048)    +05
	 BRA.S	   ADF1CON	       CONTINUE WITH CALCULATION
ADF1B2C  EQU	   *
	 MOVE.L    #364000,D0	       LOAD Y INTERCEPT 	     +00
	 MOVE.W    #8064,D1	       LOAD SLOPE		     +05
*
ADF1CON  EQU	   *		       CONTINUE WITH CALCULATION
*					      Y = M*X + B
	 MULU.W    D2,D1	       SLOPE * X		     +05
	 ASR.L	   #05,D1	       RESCALE			     +00
	 ADD.L	   D0,D1	       ADD INTERCEPT		     +00
	 MOVE.L    D1,NADF1FRQ	       STORE FREQ		     +00
*********************************************************
*
*		   ADF1 SEARCH ROUTINE
*
*********************************************************
	 MOVE.L    #00,NADF1STA        NADF1FRQ IS IN D1
	 MOVE.L    #NVOREND,A0	       LOAD TABLE STARTING ADDRESS
	 MOVE.L    #NDDPDME,A1
	 MOVE.L    N3KBAND,D2	       D2 IS 3000XBAND#
	 MOVE.L    #10000,NADF1ERR     INITIALIZE ERROR RANGE
ADF1LOOP EQU	   *
	 MOVE.L    4(A0),D0	       GET TABLE VALUE
	 SUB.L	   D1,D0	       SUBTRACT FROM ADF1FREQ
	 BPL.S	   POSITIVE	       BRANCH IF ALREADY POSITIVE
*
	 NEG.L	   D0		       ELSE MAKE POSITIVE
POSITIVE EQU	   *
	 CMP.L	   D2,D0	       COMPARE DIFF WITH BANDX3000
	 BGT.S	   GT
*
	 CMP.L	   NADF1ERR,D0	       COMPARE DIFF WITH ERR
	 BGT.S	   GT
*
	 MOVE.L    D0,NADF1ERR
	 MOVE.L    A0,NADF1STA
GT	 EQU	   *
	 ADDA.L    #64,A0	       NEXT TABLE ADDRESS
	 CMP.L	   A1,A0	       CHECK FOR END OF TABLE
	 BLT.S	   ADF1LOOP	       LOOP UNTIL END
*
	 SF.B	   NADF1TUN	       CLEAR ADF TUNED FLAG
	 CMPI.L    #00,NADF1STA        SEE IF A STATION WAS FOUND
	 BEQ.S	   NOTTUNED	       JUMP IF NO STATION FOUND
*
	 CMP.L	   NADF1ERR,D2	       SEE IF WITHIN 3000*BAND#
	 BMI.S	   NOTTUNED	       BRANCH IF NOT WITHIN
*
	 ST.B	   NADF1TUN	       SET ADF TUNED FLAG
*
*	     IF NADF1ERR < 500 THEN NADF1ERR = 0
*	     ELSE NADF1ERR = NADF1ERR - 500
*
	 SUB.L	   #500,NADF1ERR       SUBTRACT 500 HZ
	 BPL.S	   NOERR1	       SEE IF TUNING ERROR > 0
*
	 MOVE.L    #0,NADF1ERR	       IF WITHIN 500 HZ THEN SET TO 0
NOERR1	 EQU	   *
**********************************************************************
*								     *
*		  ADF1 TUNING METER DRIVE			     *
*		  -----------------------			     *
*	 OADF1TM = 32768 + 32767 * NADF1ERR / ( 3000 * BAND# )	     *
*								     *
*		   0 < NADF1ERR < 3000*BAND#			     *
*      ($8000) 32768 < OADF1TM	< 65535 ($FFFF) 		      *
**********************************************************************
	 TST.B	   NLF1LOS	       SEE IF ADF1 IN RANGE AND LOS
	 BEQ.S	   NOTTUN1	       EXIT IF NOT
*
	 MOVE.L    NADF1ERR,D0	       GET TUNING ERROR 	     +00
	 MULU	   #32767,D0	       D0 = 32767 * NADF1ERR	     +00
	 DIVU	   D2,D0	       DIVIDE BY 3000 * BAND#	     +00
	 ADDI.W    #32768,D0	       FINISH OADF1TM CALCULATION    +00
	 BRA.S	   ADF1TM	       OUTPUT ADF1TM
*
NOTTUNED SF.B	   NADF1TUN	       CLEAR TUNED FLAG
NOTTUN1  CLR.W	   NADF1RAW	       CLEAR RAW FREQ
	 CLR.L	   NADF1FRQ	       CLEAR ADF1 FREQ
	 MOVE.W    #$FFFF,D0	       SET TUNING METER FOR MIN DEFLECTION
ADF1TM	 MOVEP.W   D0,OADF1TM(A6)      OUTPUT
ADF1END  EQU	   *
*********************************************************
*
*		   FIND CLOSEST AIRPORT
*
*********************************************************
	 MOVE.L    #NDDPPS0,A0	       PRESELECT ADDR OF LAT
	 MOVE.L    #$7FFFFFFF,NAPTRNG2 INIT SMALLEST RANGE SQUARED
	 MOVE.W    #NJNFAC,D3	       NUMBER OF AIRPORTS
APTLOOP  EQU	   *
	 MOVE.L    (A0),A3	       GET ADDRESS OF LAT
	 MOVE.L    (A3),D0	       GET LAT
	 MOVE.L    4(A3),D1	       GET LON
	 BSR	   FINDR2	       CALCULATE RANGE SQUARED
*				       RETURN (D0=RANGE2)
	 CMP.L	   NAPTRNG2,D0	       SEE IF IT IS CLOSER THAN
	 BGT.S	   APTFAR	       CURRENT CLOSEST
*
	 MOVE.L    A0,A1	       STORE CLOSER PRESELECT ADDR
	 MOVE.L    D0,NAPTRNG2	       STORE CLOSER RANGE SQUARED
APTFAR	 EQU	   *
	 ADD.L	   #4,A0	       INCREMENT PRESELECT
	 SUBI.W    #1,D3	       DECREMENT NJNFAC LOOP COUNTER
	 CMPI.W    #00,D3	       CHECK FOR END OF LOOP
	 BNE.S	   APTLOOP
*
	 MOVE.L    A1,NAPTPSA	       STORE CLOSEST APT PRESELECT ADDR
	 MOVE.L    (A1),A0	       GET LAT ADDR
	 SUB.L	   #26,A0	       CHANGE TO STATION ADDR
	 MOVE.L    A0,NSACF	       STORE CLOSEST APT STA ADDR
******************************************************
*
*		   SEE IF AIRPORT IS IN RANGE
*
******************************************************
	 CMPI.L    #2796202,NAPTRNG2   CHECK FOR OUT OF RANGE LIMIT  +24
	 BLE.S	   INRANGE	       AIRPORT WITHIN 10 MILES
*
	 SF.B	   NMKRVL	       CLEAR MARKER VALID FLAG
	 BRA.S	   MKREND
******************************************************
*
*		   FIND CLOSEST MARKER
*
******************************************************
INRANGE  EQU	   *
	 MOVE.L    #$7FFFFFFF,NMKRRNG2 INIT RANGE SQUARED
	 MOVE.L    (A1),A3	       GET LAT ADDR
	 SUBA.L    #26,A3	       CHANGE TO STA ADDR
	 ADDA.L    #4,A1	       INCREMENT PRESELECT ADDR
	 MOVE.L    (A1),A1	       GET LAT ADDR OF PRESELECT
MKRLOOP  EQU	   *
	 BTST.B    #7,3(A3)	       TEST BIT 7 OF STA TYPE WORD
	 BEQ.S	   MKRFAR	       TO SEE IF IT IS A MKR
*
	 MOVE.L    26(A3),D0	       GET LAT
	 MOVE.L    30(A3),D1	       GET LON
	 BSR.S	   FINDR2	       CALCULATE RANGE SQUARED
*				       RETURN (D0=RANGE2)
	 CMP.L	   NMKRRNG2,D0	  +24  CHECK TO SEE IF MKR IS
	 BGT.S	   MKRFAR	       CLOSER THAN CURRENT CLOSEST
*
	 ST.B	   NMKRVL	       SET VALIDITY FLAG TRUE
	 MOVE.L    D0,NMKRRNG2	  +24  STORE NEW CLOSEST RANGE2
	 MOVE.L    A3,NMKRSA	       STORE NEW CLOSEST STA ADDR
MKRFAR	 ADDA.L    #64,A3	       NEXT STATION
	 CMP.L	   A1,A3	       LOOP UNTIL NEXT AIRPORT
	 BLT.S	   MKRLOOP
*
MKREND	 BRA.S	   NAVEND
*				       MASTER SWITCH OFF:
MSTOFF	 SF.B	   NADF1TUN	       CLEAR ADF1 TUNING FLAG
	 SF.B	   NDME1TUN	       CLEAR DME1 TUNING FLAG
	 SF.B	   NDME2TUN	       CLEAR DME2 TUNING FLAG
	 SF.B	   NAV1TUN	       CLEAR NAV1 TUNING FLAG
	 SF.B	   NAV2TUN	       CLEAR NAV2 TUNING FLAG
	 SF.B	   NMKRVL	       CLEAR MKR  TUNING FLAG
	 MOVE.L    #00,D0
	 MOVE.L    D0,NAV1FREQ	       CLEAR NAV1 FREQ
	 MOVE.L    D0,NAV2FREQ	       CLEAR NAV2 FREQ
	 MOVE.W    D0,NADF1RAW	       CLEAR ADF1 RAW FREQ
	 MOVE.L    D0,NADF1FRQ	       CLEAR ADF1 FREQ
NAVEND	 TRAP	   #0
	 PAGE
*************************************************************
*
*		   CALCULATE RANGE SQUARED SUBROUTINE
*
* INPUTS:
*	 D0	   STATION LAT
*	 D1	   STATION LON
* OUTPUTS:
*	 D0	   RANGE SQUARED
*
*************************************************************
FINDR2	 EQU	   *
	 SUB.L	   NGLAT,D0	       +23     FIND DELTA LAT
	 BPL.S	   POS1
*
	 NEG.L	   D0			       FIND ABS(DELTA LAT)
POS1	 EQU	   *
	 ASR.L	   #8,D0	       23-8=15 MAKE LONG WORD INTO
	 ASR.L	   #3,D0	       15-3=12 WORD BEFORE MULTIPLY
	 MULU.W    D0,D0	       +24     FIND DELTA LAT SQUARED
	 SUB.L	   NGLON,D1	       +23     FIND DELTA LON
	 BPL.S	   POS2
*
	 NEG.L	   D1			       FIND ABS(DELTA LON)
POS2	 EQU	   *
	 ASR.L	   #8,D1	       23-8=15 MAKE LONG WORD INTO
	 ASR.L	   #3,D1	       15-3=12 WORD BEFORE MULTIPLY
	 MULU.W    D1,D1	       +24     FIND DELTA LON SQUARED
	 ADD.L	   D1,D0	       +24     FIND RANGE SQUARED
	 RTS
***************************************************************
*
*		   TABLE SEARCH SUB ROUTINE
*	 INPUTS:
*		   A0 -  TABLE STARTING ADDRESS
*		   A1 -  TABLE ENDING ADDRESS
*		   D0 -  FREQUENCY SEARCHING FOR
*	 OUTPUTS:
*		   A0 -  STATION ADDRESS IF FOUND, 0 IF NOT FOUND
*		   D1 -  VALIDITY FLAG, FF IF FOUND
*
***************************************************************
TABSERCH EQU	   *
	 ST.B	   D1		       INITIALIZE VALIDITY FLAG
LOOP	 EQU	   *
	 CMP.L	   4(A0),D0	       CHECK FREQ AGAINST TABLE VALUE
	 BEQ.S	   FOUND	       BRANCH IF FOUND
*
	 ADDA.L    #64,A0	       INCREMENT TABLE POINTER
	 CMPA.L    A1,A0
	 BLT.S	   LOOP 	       LOOP UNTIL END OF TABLE
*
	 SF.B	   D1		       NOT FOUND: CLEAR FLAG
	 MOVE.L    #00,A0			  CLEAR ADDRESS
FOUND	 EQU	   *
	 RTS
	 END
