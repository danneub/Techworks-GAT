	 OPT	   MEX,CEX,NOPCS
	 XDEF	   CLIMB
*
	 INCLUDE   FSYM.SA
	 INCLUDE   IO.SA
*
	 SECTION   12
FHFC1	 DC.L	   17039360	    130 AT 2-17
FHFC2	 DC.L	   420741120	    3210 AT 2-17
FHFC3	 DC.W	   3736 	    934 AT 2-2
FHFC4	 DC.L	   9793700	    74.72 AT 2-17
FHFC5	 DC.W	   30720	    30 AT 2-10
FHFC6	 DC.W	   -839 	   -1/20000 AT 2-24
FHFC7	 DC.L	   -6554	   -1000/20000 AT 2-17
FHFC8	 DC.L	   262144	    2 AT 2-17
FHFC9	 DC.W	   30638	    29.92 AT 2-10
FALC1	 DC.L	   33554432	    2  AT 2-24
FALC2	 DC.L	   -33554432	   -2  AT 2-24
FALC3	 DC.W	   26214	   .05 AT 2-19
FALC4	 DC.L	   805306360	    24 AT 2-25
FALC5	 DC.L	   -536870910	   -16 AT 2-25
FALC6	 DC.W	   1920 	    30 AT 2-6
FALC7	 DC.W	   -1920	   -30 AT 2-6
FALC8	 DC.W	   143		    1/57.3 AT 2-13
FALC9	 DC.W	   171		    1/6 AT 2-10
FALC10	 DC.L	   13107200	    200 AT 2-16
FALC11	 DC.L	   -13107200	   -200 AT 2-16
FALC12	 DC.L	   -125829120	   -30 AT 2-22
FALC13	 DC.L	   832307	   (127/20)AT 2-17
FALC14	 DC.L	   2143027200	    16350 AT 2-17
FALC15	 DC.L	   125829120	    30 AT 2-22
FALC16	 DC.W	   1966 	    60/2000 AT 2-16
FALC17	 DC.L	   4194300	    1 AT 2-22 (LESS 4)
FALC18	 DC.L	   -4194300	   -1 AT 2-22 (LESS 4)
FALC19	 DC.L	   409600	    100 AT 2-12
FALC20	 DC.L	   -409600	   -100 AT 2-12
FALC21	 DC.L	   13107	   .1 AT 2-17
FRC1	 DC.W	   205		    1/80 AT 2-14
**********************************************************************
*	 THIS MODULE COMPUTES:ALPHA DOT(DEG/SEC) AT 2-7 	     *
*			      ALPHA (DEGREES)	 AT 2-25,2-9	     *
*			      GAMMA (DEGREES)	 AT 2-10	     *
*			      SINE GAMMA	 AT 2-15	     *
*			      WT.ON.GROUND(WOG)  BOOLEAN	     *
*			      RATE OF CLIMB(FT/SEC)AT 2-6	     *
*		    PRESSURE  ALTITUDE (FEET)	 AT 2-17,2-1	     *
*		    GEOMETRIC ALTITUDE (FEET)	 AT 2-17,2-1	     *
*		    BAROMETRIC PRESS(IN-HG)	 AT 2-10	     *
*		    FIELD SELECTED BOOLEAN   1=B FIELD SELECTED      *
*		    ALTITUDE SLEW UP BOOLEAN  1=SLEW UP 	     *
*		    ALTITUDE SLEW DOWN BOOLEAN	1=SLEW DOWN	     *
*		    ALTITUDE FREEZE BOOLEAN	1=FREEZE	     *
*		    GEOMETRIC FIELD ELEVATION(FT)AT 2-17,2-1	     *
*		    PRESSURE FIELD ELEVATION(FT)AT 2-17,2-1	     *
*		    GEOMETRIC HT ABOVE GROUND(FT)AT 2-17,2-1	     *
*		    ALTIMETER DRIVE(A/O)-10V=19000 FT,0V=-1000 FT    *
*		    RATE OF CLIMB IND(A/0) 10V=2000 FT/MIN	     *
*			 (4 SECOND TIME CONSTANT ON INDICATER)	     *
*	    MODULE IS SKIPPED IF FLIGHT FREEZE IS ACTIVATED	     *
*					OCTOBER 28,1983 	     *
*	  REVISED APRIL 13,1984 TO ADD VISUAL PROVISIOPNS  A.S.W.    *
**********************************************************************

************************************************************************
*	 HF=130+3080(IF B FIELD IS SELECTED)			       *
*	 HFP=HF+(29.92-PBAR)(934)				       *
*	 PBAR=30+PBAR(INSTRUCTOR)				       *
*		-1<PBAR(INSTRUCTOR)<1				       *
*	 HFP=HF-934*P BAR(INSTRUCTER)-74.72			       *
*	 HFG=INTEGRAL(VP*SIN GAMMA)				       *
*	      HFG IS NEVER LESS THAN HF 			       *
*		   NOTE:					       *
* HF IS STORED AS A LONG WORD AT 2-17 BUT CAN BE USED AS A WORD AT 2-1 *
*HFP "    "    "  "  "    "   "   "    "   "  "   "   "  "  "   "   "  *
************************************************************************
CLIMB	 EQU *
	 MOVE.L    #$FE6000,A6		   (I/O BASE ADRESS IN A6)
	 TST.B	   FFREEZE		   1=FLIGHT FREEZE
	 BEQ.S	   UNFREEZC		   BRANCH IF NOT FLIGHT FREEZE
	 TRAP	   #0			   REAL TIME RETURN TO EXEC
UNFREEZC MOVE.L    FHFC1,D7		   130 AT 2-17
	 BTST.B    #IBFIELD,IINSTDI1(A6)   1=B FIELD SELECTED
	 SNE.B	   FBFIELD		   1=B FIELD SELECTED
	 BEQ.S	   NEXT 		   BRANCH IF B FIELD IS NOT SELECTED
	 MOVE.L    FHFC2,D7		   130+3080(IF B FIELD SEL)  AT 2-17
NEXT	 MOVE.L    D7,FHF		   GEOMETRIC FIELD ELEV AT 2-17,2-1
	 MOVE.W    FHFC3,D6		   934 AT 2-2
	 MOVE.W    FBAROPR,D0	      (12 BIT A/I)+10 VOLTS=+1 INCH HG AT 2-15
	 ASR.W	   #7,D0
	 ASL.W	   #7,D0		   DEADBAND 3 LEAST SIGNIFIGANT BITS
	 MULS	   D0,D6		   934*(INST A/I) AT 2-17
	 ADD.L	   FHFC4,D6		   934*PBAR+74.72 AT 2-17
	 SUB.L	   D6,D7		   HF-934*P BAR-74.72 AT 2-17
	 MOVE.L    D7,FHFP		   FIELD PRESSURE ALT AT 2-17,2-1
	 ASR.W	   #5,D0		   INST BARO PRESS AT 2-10
	 ADD.W	   FHFC5,D0		   INST BARO PRESS +30(AT 2-10)
	 MOVE.W    D0,FPBAR		   TOTAL BARO PRESS AT 2-10
**************************************************************************
*							  LIM(+- 2)	 *
*	 ALPHA DOT=1845/VP(AZA-WOW+COS PHI)+QA+W.O.G.(2)(THETA-ALPHA)	 *
*			   -200<ALPHA DOT<200				 *
**************************************************************************
	 MOVE.W    FCOSPHI,D6		   COS PHI AT 2-15
	 ASR.W	   #3,D6		   2-12
	 ADD.W	   FAZA,D6		   AZA+COS PHI AT 2-12
	 SUB.W	   FWOW,D6		   AZA+COS PHI -WOW AT 2-12
	 MULS	   FVPINV,D6		   1845/VP*(AZA+COS PHI)AT 2-16
	 MOVE.L    FQA,D5		   FQA AT 2-25
	 ASR.L	   #8,D5		   2-17
	 ASR.L	   #1,D5		   2-16
	 ADD.L	   D5,D6		   1845/VP*(AZA+COS PHI)+QA AT 2-16
	 CLR.L	   D2			   ZERO IN D2
	 TST.B	   FWOG 		   1=ON GROUND
	 BEQ.S	   NEXT2		   BRANCH IF IN AIR
	 MOVE.L    FTHETA,D2		   THETA AT 2-25
	 SUB.L	   FALPHA,D2		   THETA-ALPHA AT 2-25
	 ASR.L	   #1,D2		   2-24
	 CMP.L	   FALC1,D2		   (THETA-ALPHA) -2  AT 2-24
	 BMI.S	   NEXT1		   BRANCH IF (THETA-ALPHA)<2
	 MOVE.L    FALC1,D2		   LIMIT IN D2 AT 2-24
NEXT1	 CMP.L	   FALC2,D2		   (THETA-ALPHA)+2
	 BPL.S	   NEXT2		   BRANCH IF (THETA-ALPHA)>-2
	 MOVE.L    FALC2,D2		   LIMIT IN D2 AT 2-24
NEXT2	 ASR.L	   #7,D2		   2*(THETA-ALPHA)AT 2-16
	 ADD.L	   D2,D6		   ALPHA DOT (N)AT 2-16
	 CMP.L	   FALC10,D6		   (---)-200 AT  2-16
	 BMI.S	   NEXT3		   BRANCH IF (---)<200
	 MOVE.L    FALC10,D6		   LIMIT IN D6 AT 2-16
NEXT3	 CMP.L	   FALC11,D6		   (---)+200 AT 2-16
	 BPL.S	   NEXT4		   BRANCH IF (---)>-255
	 MOVE.L    FALC11,D6		   LIMIT IN D6 AT 2-16
NEXT4	 ASR.L	   #8,D6		   2-8
	 ASR.L	   #1,D6		   2-7
	 MOVE.W    FALPHDOT,D7		   ALPHA DOT(N-1)AT 2-7
	 MOVE.W    D6,FALPHDOT		   ALPHA DOT (N)AT 2-7
************************************************************
*	 ALPHA=INTEGRAL(ALPHA DOT*DT)			   *
*	       -16<ALPHA<24				   *
*	 NOTE:ALPHA IS STORED AS A LONG WORD AT 2-25 BUT   *
*	      CAN BE USED AS A WORD AT 2-9		   *
************************************************************
	 SUB.W	   D7,D6		   ALPHA DOT(N)-ALPHA DOT(N-1)AT 2-7
	 ASR.W	   #1,D6		   1/2(----) AT 2-7
	 ADD.W	   FALPHDOT,D6	     3/2*ALPHA DOT(N)-1/2*ALPHA DOT(N-1)@ 2-7
	 MULS	   FALC3,D6		  .05*(----)AT 2-26
	 ASR.L	   #1,D6		  2-25
	 ADD.L	   FALPHA,D6		  ALPHA(N)AT 2-25
	 BMI.S	   NEXT5		  BRANCH IF NEGATIVE
	 CMP.L	   FALC4,D6		  ALPHA-24 AT 2-25
	 BMI.S	   NEXT6		  BRANCH IF ALPHA<24
	 MOVE.L    FALC4,D6		  LIMIT IN D6 AT 2-25
	 BRA.S	   NEXT6
NEXT5	 CMP.L	   FALC5,D6		  ALPHA+16 AT 2-25
	 BPL.S	   NEXT6		  BRANCH IF ALPHA >-16
	 MOVE.L    FALC5,D6		  LIMIT IN D6 AT 2-25
NEXT6	 MOVE.L    D6,D5		  (IN ORDER TO FLAG SIGN)
	 BPL.S	   NEXT7
	 TST.B	   FWOG 		  1=ON GROUND
	 BEQ.S	   NEXT7		  BRANCH IF IN AIR
	 CLR.L	   D6			  ZERO SLICE ALPHA ON GROUND
NEXT7	 MOVE.L    D6,FALPHA		  ALPHA LIMITED AT 2-25,2-9
**********************************************************************
*	 FLT.PATH ELEV ANGLE(GAMMA)=THETA-ALPHA*COS PHI(DEG)	     *
*		       -30<GAMMA<30				     *
*		NOTE:GAMMA CANNOT GO NEGATIVE WHEN ON GROUND	     *
**********************************************************************
	 MOVE.L    FTHETA,D6		  THETA AT 2-25
	 ASR.L	   #1,D6		  2-24
	 MOVE.W    FCOSPHI,D4		  COSINE BANK ANGLE AT 2-15
	 MULS	   FALPHA,D4		  ALPHA*COS PHI AT 2-24
	 SUB.L	   D4,D6		  THETA-ALPHA*COS PHI (GAMMA)AT 2-24
	 ASR.L	   #2,D6		  2-22
	 CMP.L	   FALC15,D6		  (GAMMA)-30 AT 2-22
	 BMI.S	   NEXT8		  BRANCH IF GAMMA<30
	 MOVE.L    FALC15,D6		  LIMIT IN D6 AT 2-22
NEXT8	 CMP.L	   FALC12,D6		  (GAMMA+30) AT 2-22
	 BPL.S	   NEXT9		  BRANCH IF GAMMA>-30
	 MOVE.L    FALC12,D6		  LIMIT IN D6 AT 2-22
NEXT9	 ASR.L	   #8,D6		  2-14
	 ASR.L	   #4,D6		  2-10
	 BPL.S	   NEXT10		  BRANCH IF POSITIVE
	 TST.B	   FWOG 		  1=ON GROUND
	 BEQ.S	   NEXT10		  BRANCH IF IN AIR
	 CLR.W	   D6			  ZERO SLICE
NEXT10	 MOVE.W    D6,FGAMMA		  GAMMA AT 2-10
**********************************************************************
*	 SINE GAMMA=GAMMA(RADIANS)-1/6*GAMMA CUBED(RADIANS)	     *
**********************************************************************
	 MULS	   FALC8,D6		  1/57.3*GAMMA AT 2-23
	 ASR.L	   #8,D6		  2-15		      MAX=17156
	 MOVE.W    D6,D7		  GAMMA(RADIANS)IN D6,D7 AT 2-15
	 MULS	   D6,D7		  GAMMA SQ. AT 2-30
	 ASR.L	   #8,D7		  2-22
	 ASR.L	   #6,D7		  2-16		      MAX=17964
	 MULS	   D6,D7		  GAMMA CUBED AT 2-31
	 ASR.L	   #8,D7		  2-23
	 ASR.L	   #6,D7		  2-17		      MAX=18811
	 MULS	   FALC9,D7		  1/6*GAMMA CUBED AT 2-27
	 ASR.L	   #8,D7		  2-19
	 ASR.L	   #4,D7		  2-15		      MAX=98
	 SUB.W	   D7,D6		  GAMMA-1/6*GAMMA CUBED AT 2-15
	 MOVE.W    D6,FSINGAMA		  SIN GAMMA AT 2-15
**************************************************************************
*		  RATE OF CLIMB=VP*SIN GAMMA				 *
*		 -100<RATE OF CLIMB<100   (FT/SEC)			 *
**************************************************************************
	 MULS	   FVP,D6		  VP*SIN GAMMA AT 2-21
	 ASR.L	   #8,D6		  2-13
	 ASR.L	   #1,D6		  2-12
	 CMP.L	   FALC19,D6		  H DOT-100   AT 2-12
	 BMI.S	   NEXT11		  BRANCH IF H DOT<100
	 MOVE.L    FALC19,D6		  LIMIT IN D6 AT 2-12
NEXT11	 CMP.L	   FALC20,D6		  H DOT+100   AT 2-12
	 BPL.S	   NEXT12		  BRANCH IF H DOT>-100
	 MOVE.L    FALC20,D6		  LIMIT IN D6 AT 2-12
NEXT12	 ASR.L	   #6,D6		  2-6
	 MOVE.W    FHDOT,D7		  H DOT(N-1)AT 2-6
	 MOVE.W    D6,FHDOT		  H DOT(N)AT 2-6
	 MOVE.W    D6,D1		  H DOT(N)AT 2-6
	 MULS	   #60,D1		  H DOT(N)(FT/MINUTE)AT 2-6
	 ASR.L	   #3,D1		  H DOT(N)(FT/MINUTE)AT 2-3
	 MOVE.L    D1,FRCLIMB1		  H DOT(N)(FT/MINUTE)AT 2-3
************************************************************************
* RATE OF CLIMB INDICATER    (D/A)   +10 VOLTS=+2000 FT/MINUTE CLIMB   *
*		       NOTE:  FOUR SEC TIME CONSTANT ON INDICATER      *
************************************************************************
	 MULS	   FALC16,D6		  H DOT*60/2000 AT 2-22
	 CMP.L	   FALC17,D6		  (---)-1   AT 2-22
	 BMI.S	   NEXT13		  BRANCH IF LESS THAN 2000 FT/MIN
	 MOVE.L    FALC17,D6		  LIMIT IN D6 AT 2-22
NEXT13	 CMP.L	   FALC18,D6		  (---)+1 AT 2-22
	 BPL.S	   NEXT14		  BRANCH IF > -2000 FT/MIN
	 MOVE.L    FALC18,D6		  LIMIT IN D6 AT 2-22
NEXT14	 SUB.L	   FRCLIMB,D6		  RC(N)-RC(N-1) AT 2-22
	 ASR.L	   #8,D6		  2-14
	 ASR.L	   #2,D6		  2-12
	 MULS	   FRC1,D6		  1/80*[RC(N)-RC(N-1)] AT 2-26
	 ASR.L	   #4,D6		  2-22
	 ADD.L	   FRCLIMB,D6		  RATE OF CLIMB (N)/2000 AT 2-22
	 CMP.L	   FALC17,D6
	 BMI.S	   NEXT15
	 MOVE.L    FALC17,D6		  LIMIT IN D6 AT 2-22
NEXT15	 CMP.L	   FALC18,D6
	 BPL.S	   NEXT16
	 MOVE.L    FALC18,D6		  LIMIT IN D6 AT 2-22
NEXT16	 MOVE.L    D6,FRCLIMB		  RATE OF CLIMB (N)/2000 AT 2-22
	 ASR.L	   #7,D6		  2-15
	 BCHG	   #15,D6		  COMPLEMENT SIGN BIT
	 BTST.B    #IRCFAIL,IFAIL1(A6)	  1=FAIL R/C INDICATER
	 BNE.S	   NEXT17		  BRANCH IF FAILED
	 MOVEP.W   D6,OIRCP(A6) 	  (A/O) 10V=2000 FT/MIN RATE OF CLIMB
****************************************************************************
*   GEOMETRIC ALTITUDE(HG)=INTEGRAL(H DOT*DT*K1)+INTEGRAL(SLEW*DT)	   *
*	 GEOM ALT NEVER LESS THAN FIELD ALTITUDE			   *
*	 GEOMETRIC HT. ABOVE GROUND=HG-HF(GEOMETRIC FIELD ELEVATION)	   *
*	      WHERE:K1=0 IF ALTITUDE FREEZE IS ACTIVATED		   *
*		    K1=1 OTHERWISE					   *
*		    HFP<HP<16350					   *
*		     HF<HG<16350					   *
*   PRESSURE ALTITUDE=GEOMETRIC ALT+934*(29.92-PBARO)			   *
* NOTE:HP,HG,AND (HG-HF) ARE STORED AS LONG WORDS AT 2-17 BUT CAN BE USED  *
*	   AS WORDS AT 2-1						   *
****************************************************************************
NEXT17	 MOVE.W    FHDOT,D6		  HDOT(N) AT 2-6
	 SUB.W	   D7,D6		  HDOT(N)-HDOT(N-1) AT 2-6
	 ASR.W	   #1,D6		  1/2(HDOT(N)-HDOT(N-1)AT 2-6
	 ADD.W	   FHDOT,D6		  3/2HDOT(N)-1/2HDOT(N-1)AT 2-6
	 MULS	   FALC3,D6		 .05*(----)AT 2-25
	 ASR.L	   #8,D6		  2-17
	 BTST.B    #IALTFRZ,IINSTMDI(A6)  1=ALTITUDE FREEZE
	 SNE.B	   FALTFRZ		  1=ALTITUDE FREEZE
	 BEQ.S	   NEXT18		  BRANCH IF NOT FREEZE
	 CLR.L	   D6			  ZERO IN D6
NEXT18	 MOVE.L    FALC13,D5		  (127/20)AT 2-17
	 ST.B	   D4			  D4=TRUE
	 BTST.B    #IALTSUP,IINSTMDI(A6)  1=SLEW ALT UP
	 SNE.B	   FALTSUP		  1=SLEW UP
	 BEQ.S	   NEXT19		  BRANCH IF NOT SLEW UP
	 SF.B	   D4			  D4=FALSE
	 ADD.L	   D5,D6		  H DOT +127/20 AT 2-17
NEXT19	 BTST.B    #IALTSDN,IINSTMDI(A6)  1=SLEW ALT DOWN
	 SNE.B	   FALTSDN		  1=SLEW DOWN
	 BEQ.S	   NEXT20		  BRANCH IF NOT SLEW DOWN
	 SUB.L	   D5,D6		  H DOT-127/20 AT 2-17
NEXT20	 MOVE.L    D6,D1		  DELTA H AT 2-17
	 ADD.L	   FHG,D6		  GEOMETRIC ALTITUDE(N)AT 2-17
	 BMI.S	   NEXT21		  BRANCH IF ALTITUDE IS NEGATIVE
	 CMP.L	   FALC14,D6		  ALT(N)-16350 AT 2-17
	 BMI.S	   NEXT21		  BRANCH IF ALTITUDE<16350
	 MOVE.L    FALC14,D6		  LIMIT IN D6 AT 2-17
NEXT21	 MOVE.L    D6,D7		  ALTITUDE AT 2-17 IN D7,D6
	 SF.B	   D5			  0=IN AIR
	 SUB.L	   FHF,D7		  HG-HF AT 2-17
	 BMI.S	   NEXT22		  BRANCH IF HG<HF
	 CMP.L	   FALC21,D7		  ABSOLUTE VALUE-.1 AT 2-17
	 BPL.S	   NEXT23		  BRANCH IF HG >(FHF+.1)
NEXT22	 MOVE.L    FHF,D6		  HF IN D6 AT 2-17
	 ST.B	   D5			  1=ON GROUND
	 CLR.L	   D7			  ZERO IN D7
NEXT23	 TST.B	   FINIT		  1=INITIALIZED
	 BNE.S	   NEXT24		  BRANCH IF INITIALIZED
	 MOVE.L    FHF,D6		  FIELD  ELEVATION AT 2-17
NEXT24	 MOVE.L    D6,FHG	GEOMETRIC ALTITUDE AT 2-17(LONG WORD) 2-1(WORD)
	 SUB.L	   FHF,D6	  GEOMETRIC ALTITUDE-FIELD ALTITUDE AT 2-17
	 MOVE.L    D6,FHGGND		  GEOMETRIC HT ABOVE GND AT 2-17
	 MOVE.W    FHFC9,D1		  29.92 AT 2-10
	 SUB.W	   FPBAR,D1		  (29.92-BARO PRESS) AT 2-10
	 MULS	   FHFC3,D1		  934*(29.92-BARO PRESS) AT 2-12
	 ASL.L	   #5,D1		  934*(29.92-BARO PRESS) AT 2-17
	 ADD.L	   FHG,D1		  PRESSURE ALTITUDE AT 2-17
	 BMI.S	   NEXX1		  BRANCH IF NEGATIVE
	 CMP.L	   FALC14,D1		  FHP-16350 AT 2-17
	 BMI.S	   NEXX1		  BRANCH IF LESS THAN 16350
	 MOVE.L    FALC14,D1		  LIMIT IN D1 AT 2-17
NEXX1	 MOVE.L    D1,FHP		  PRESSURE ALTITUDE AT 2-17
	 MOVE.W    FHP,D6		  ALTITUDE AT 2-1
	 MULS	   FHFC6,D6		  -(1/20000)*ALT AT 2-25
	 ASR.L	   #8,D6		   2-17
	 ADD.L	   FHFC7,D6		  -(ALT+1000)/20000 AT 2-17
	 ASR.L	   #2,D6		   2-15
	 BCHG	   #15,D6		   COMPLEMENT SIGN BIT
	 BTST.B    #IALTFAIL,IFAIL1(A6)    1=FAIL ALTITUDE
	 BNE.S	   NEXT25		   BRANCH IF FAILED
	 MOVEP.W   D6,OALTMP(A6)	ALTIMETER(A/0)-10V=19000 FT,0V=-1000 FT
****************************************************************
*	 WT ON GROUND(FWOG)=(A+B.C).(D BAR)=C		       *
*	 WHERE:A=1 IF HP=HFP,ZERO OTHERWISE			*
*	       B=1 IF VP<12 FT/SEC,ZERO OTHERWISE	       *
*	       D=1 IF ALT SLEW UP IS ACTIVATED,ZERO OTHERWISE  *
****************************************************************
NEXT25	 MOVEQ	   #96,D7		   12 AT 2-3
	 ASL.W	   #3,D7		   2-6
	 SUB.W	   FVP,D7		   12-VP AT 2-6
	 SPL.B	   D3			   1=VP<12
	 AND.B	   FWOG,D3		   1=VP<12 AND ON GROUND
	 OR.B	   D3,D5		   1=(--)OR H=HF
	 AND.B	   D4,D5		   1=(---)AND NOT SLEW UP
	 TST.B	   D5			   1=ON GROUND
	 BNE.S	   NEXT26		   BRANCH IF ON GROUND
	 CLR.W	   D1
	 MOVE.W    D1,FWOW		   SET FWOW=ZERO IF NOT ON GROUND
NEXT26	 TST.B	   D5			   1= ON GROUND
	 BEQ.S	   NEXT27		   BRANCH IF IN AIR
	 MOVE.L    FHF,D0
	 MOVE.L    D0,FHG	SET ALTITUDE=FIELD  ALTITUDE IF ON GROUND
	 MOVE.W    FGAMMA,D2
	 BPL.S	   NEXT27
	 CLR.W	   D1			   ZERO IN D1
	 MOVE.W    D1,FGAMMA		   ZERO SLICE GAMMA ON GROUND
NEXT27	 MOVE.B    D5,FWOG		   1=ON GROUND
	 TRAP	   #0			   REAL TIME RETURN TO EXEC
	 END
