	 OPT	   MEX,CEX,NOPCS
*
	 XDEF	   NAV1
	 XDEF	   A1IPTAB
	 XDEF	   A2IPTAB
	 XREF	   MSC1
	 XREF	   MSIN1
	 XREF	   MCOS1
	 XREF	   NAVRCV
	 XREF	   AEPC
	 XREF	   NCDI
	 XREF	   NATAN2
	 XREF	   SQRT
*
	 SECTION   12
****************************************************************************
*
*		       NAV MODULE NUMBER 1
*
*		   NAV ENVIRONMENT AND RADIOS
*
***************************************************************************
	 INCLUDE   FSYM.SA
	 INCLUDE   NSYM.SA
	 INCLUDE   IO.SA
*
ZFPDEG	 DC.W	   22780	       -04	 FEET PER DEG
ZDGPFT	 DC.W	   9		       +26	 1.37085*10-7 DEG/FT PER ITER.
ZLATMAX  DC.L	   12582912	       +23	 MAX LAT 1.5 DEG
ZLONMAX  DC.L	   12582912	       +23	 MAX LON 1.5 DEG
*
CCTAB	 DC.L	   00		       +23	 CROSS COUNTRY CENTER LAT
	 DC.L	   00		       +23	 CROSS COUNTRY CENTER LON
	 DC.L	   27962	       +23	 CROSS COUNTRY SLEW/ITERATION
	 DC.W	   64		       +06	 CC SCALE (10 VOLT/DEG)
*
AP1TAB	 DC.L	   -3143538	       +23	 APPR 1 CEN LAT SEAPORT BEACH
	 DC.L	   4200824	       +23	 APPR 1 CEN LON
	 DC.L	   8144 	       +23	 APPR 1 SLEW/ITERATION
	 DC.W	   220		       +06	 APPR 1 SCALE (10 VOLT/DEG)
*
AP2TAB	 DC.L	   3728694	       +23	 APPR 2 CEN LAT MOUNTAIN DALE
	 DC.L	   -4901338	       +23	 APPR 2 CEN LON
	 DC.L	   8144 	       +23	 APPR 2 SLEW/ITERATION
	 DC.W	   220		       +06	 APPR 2 SCALE (10 VOLT/DEG)
*
A1IPTAB  DC.L	   -3485586	       +23	 INITIAL POS. LAT SEAPORT BEACH
	 DC.L	   3671320	       +23	 INITIAL POS. LON
	 DC.W	   -4608	       +06	 INITIAL POS. HDG. -72 DEG.
*
A2IPTAB  DC.L	   4126117	       +23	 INITIAL POS. LAT MOUNTAIN DALE
	 DC.L	   -4819899	       +23	 INITIAL POS. LON
	 DC.W	   3392 	       +06	 INITIAL POS. HDG. 53 DEG.
*
NAV1	 EQU	   *
	 MOVE.L    #$FE6000,A6			 LOAD I/O BASE ADDR
*
	 TST.B	   NINIT			 TEST INIT FLAG
	 BNE.S	   ENDINIT			 BRANCH IF TRUE
*
	 MOVE.W    #10,NITCOUNT 		 TO STOP THUMPING
	 ST.B	   NIPFLAG			 SET INITIAL POS. FLAG
	 ST.B	   NINIT			 SET INITIALIZED FLAG
ENDINIT  EQU	   *
*
*	 CHECK FOR INITIAL POSITION
*
	 TST.B	   NIPFLAG			 TEST IP FLAG
	 BEQ.S	   ENDIP			 BRANCH IF FALSE
*
	 SF.B	   NIPFLAG			 CLEAR IP FLAG
	 ST.B	   UHDSET			 SET NEW HEADING FLAG
	 MOVE.L    #A1IPTAB,A0			 LOAD APPROACH 1 ADDR
	 BTST.B    #IENGFIR,IINSTMDI(A6)	 TEST VISUAL RESET
	 BNE.S	   APPR1IP			 BRANCH IF TRUE
*
	 BTST.B    #IAPPR1,IRCDRDI(A6)		 TEST APPROACH 1 SWITCH
	 BNE.S	   APPR1IP			 BRANCH IF TRUE
*
	 MOVE.L    #A2IPTAB,A0			 LOAD APPROACH 2 ADDR
APPR1IP  MOVE.L    (A0)+,NGLAT			 SET IP LAT
	 MOVE.L    (A0)+,NGLON			 SET IP LON
	 MOVE.W    (A0)+,USETHD 		 SET IP HEADING
ENDIP	 EQU	   *
*
*			     NAV INTERFACE
*
	 TST.B	   NAVFRZ	       LB	 NAV FREEZE ?
	 BNE	   ENDVEL	       .	 YES GO TO END FREEZE
*
*     NPSIGA   =FPSI + NGBIAS			 HEADING
*     NGS      =FVPGND				 GROUND SPEED
*
*     NGVNS = NUEW * NCPSIB - NVEW * NSPSIB	 N-S GROUND VEL
*     NGVEW = NVEW * NCPSIB + NUEW * NSPSIB	 E-W GROUND VEL      FT/SEC
*
	 MOVE.W    NGBIAS,D0	       +06
	 BSR	   MSC1
	 MOVE.W    D0,NSPSIB	       +15
	 MOVE.W    D1,NCPSIB	       +15
	 MULS	   NUEW,D1	       4+15=+19  NUEW * NCPSIB
	 MULS	   NVEW,D0	       4+15=+19  NVEW * NSPSIB
	 SUB.L	   D0,D1	       +19
	 ASL.L	   #1,D1	       +20
	 SWAP	   D1		       +04	 RESCALE
	 MOVE.W    D1,NGVNS	       +04	 NORTH-SOUTH VELOCITY FT/SEC
*
	 MOVE.W    NSPSIB,D0	       +15
	 MOVE.W    NCPSIB,D1	       +15
	 MULS	   NVEW,D1	       4+15=+19  NVEW * NCPSIB
	 MULS	   NUEW,D0	       4+15=+19  NUEW * NSPSIB
	 ADD.L	   D0,D1	       +19
	 ASL.L	   #1,D1	       +20
	 SWAP	   D1		       +04	 RESCALE
	 MOVE.W    D1,NGVEW	       +04	 EAST-WEST VELOCITY FT/SEC
*
*		   CHECK FOR HEADING INITIAL POS.
*
*    IF UHDSET THEN
*	      NGBIAS = AEPC(NGBIAS + USETHD - NPSIGA)
*	      UHDSET = .FALSE.
*    END IF
*
	 TST.B	   UHDSET	       .	 IP HEADING?
	 BEQ.S	   ENDHDSET	       .	 NO: BRANCH
*
	 MOVE.W    NGBIAS,D0	       +06	 YES: RECALCULATE NGBIAS
	 ADD.W	   USETHD,D0	       +06
	 BSR	   AEPC 	       +06	 LIMIT TO +/- 180 DEG
*
	 SUB.W	   NPSIGA,D0	       +06
	 BSR	   AEPC 	       +06	 LIMIT TO +/- 180 DEG
	 MOVE.W    D0,NGBIAS	       +06
	 SF.B	   UHDSET	       .	 CLEAR FLAG
ENDHDSET EQU	   *
*
*	 NPSIGA  =  AEPC(FPSI  +  NGBIAS)      LIMIT TO +/-  180 DEG
*
	 MOVE.W    FPSI,D0	       +06	 FLT TRUE HDG  WORD	  DEG
	 ADD.W	   NGBIAS,D0	       +06	 ADD NGBIAS
	 BSR	   AEPC 	       +06	 LIMIT TO +/- 180 DEG
	 MOVE.W    D0,NPSIGA	       +06	 NAV TRUE HDG		   DEG
*
	 MOVE.W    FVPGND,D1	       +05	 FLT GND SPD		   FPS
	 ASR.W	   #1,D1	       +05-01+04 FLT GND SPD		   FPS
	 MOVE.W    D1,NGS	       +04	 NAV GND SPD		   FPS
*
*						 MAG HEADING
*      NPSIMH  =AEPC(NPSIGA  -NMAGVAR ,0,360.)
*
	 MOVE.W    NMAGVAR,D1	       +06	 MAG VAR		   DEG
	 SUB.W	   D1,D0	       +06	 SUB MAG VAR FROM TRUE HDG DEG
	 BSR	   AEPC 	       .	 LIMIT TO 0- 360 DEG
	 MOVE.W    D0,NPSIMH	       +06	 STORE IT		   DEG
*
*		   DRIVE RMI HEADING  OR DIRECTIONAL GYRO (DG) IN TEST BED
*
	 BSR	   MSC1 	      . 	 GET SINE/COSINE OF NPSIMH
	 BCHG	   #15,D0	       .	 COMPLEMENT SIGN BIT
	 BCHG	   #15,D1	       .	 COMPLEMENT SIGN BIT
	 MOVEP.W   D1,ORMIHC(A6)       +15	 COSINE OF MAG HEADING
	 MOVEP.W   D0,ORMIHS(A6)       +15	 SINE OF MAG HEADING
*
*		   WIND DIRECTION WITH RESPECT TO NAV HEADING
*
*
	 MOVE.W    FWINDS,D1	       +15	 SIN(WIND DIRECTION)
	 MOVE.W    FWINDC,D0	       +15	 COS(WIND DIRECTION)
	 EXT.L	   D1		       +15	 PREPARE FOR NATAN2
	 EXT.L	   D0		       +15	 PREPARE FOR NATAN2
	 BSR	   NATAN2			 RESOLVE SIN/COS TO DEG
*
	 SUB.W	   NGBIAS,D0	       +06	 SUB INITIAL HEADING
	 BSR	   AEPC 	       +06	 LIMIT TO +/- 180 DEG
	 SUB.W	   NMAGVAR,D0	       +06	 SUBTRACT MAG VARIATION
	 BSR	   AEPC 	       +06	 LIMIT TO +/- 180 DEG
*
	 BSR	   MSC1
	 MOVE.W    D0,FSINPSIW	       +15	 SINE WIND DIRECTION W.R.T. NAV
	 MOVE.W    D1,FCOSPSIW	       +15	 COSE WIND DIRECTION W.R.T. NAV
*
*     NGGA    =FHGGND  +NGFE	       .	 NAV GEOM ALT ABOVE SEA LEVEL
*
	 MOVE.W    NGFE,D0	       +00	 FIELD ELEV		   FT
	 MOVE.W    FHGGND,D1	       +01	 FLT HT ABOVE GND	   FT
	 ASR.W	   #1,D1	       +01-01+00 FLT HT ABOVE GND	   FT
	 ADD.W	   D1,D0	       +00	 NAV GEOM ALT		   FT
	 MOVE.W    D0,NGGA	       +00	 STORE IT		   FT
*
*						 NAV HT ABOVE GND
*     NHRA    =FHGGND  -CONST(ASSUME 0. FOR PFT )
*
	 SUB.W	   #0,D1	       +00	 NAV HT ABOVE GND	   FT
	 MOVE.W    D1,NHRA	       +00	 STORE IT		   FT
*
*     NBWOW    =FWOG			LB	  NAV WT ON WHEELS
*
	 MOVE.B    FWOG,D0	       LB	 FLT WOW
	 MOVE.B    D0,NBWOW	       LB	 NAV WOW
*
*			     VELOCITIES INCLUDING WIND
*     IF ( FVPGND  .LT..25FPS.AND.NBWOW ) THEN
*	 NUEW	 =0.
*	 NVEW	 =0.
*      ELSE
*	 NUEW	 =FUEW
*	 NVEW	 =FVEW
*    END IF
*
	 BEQ.S	   MOVING	       LB	 WOW  N0
	 MOVE.W    NGS,D1	       +04	 YES			   FPS
	 CMP.W	   #$0004,D1	       +04	 IS IT MOVING ?      .25   FPS
	 BPL.S	   MOVING	       .	 YES
	 CLR.W	   NUEW 	       +04	 NAV N/S VEL WITH WIND	   FPS
	 CLR.W	   NVEW 	       +04	 NAV E/W VEL WITH WIND	   FPS
	 BRA.S	   ENDVEL	       .	 JMP TO END
MOVING	 MOVE.W    FUEW,D2	       +05	 FLT N/S VEL WITH WIND	   FPS
	 MOVE.W    FVEW,D3	       +05	 FLT E/W VEL WITH WIND	   FPS
	 ASR.W	   #1,D2	       +05-01+04 FLT N/S VEL WITH WIND	   FPS
	 ASR.W	   #1,D3	       +05-01+04 FLT E/W VEL WITH WIND	   FPS
	 MOVE.W    D2,NUEW	       +04	 STORE IT		   FPS
	 MOVE.W    D3,NVEW	       +04	 STORE IT		   FPS
ENDVEL	 EQU	   *
*
*		   LOAD CHART RECORDER VARIABLES DEPENDING ON MODE SWITCHES
*
	 MOVE.L    #CCTAB,A0	       .	 LOAD CROSS COUNTRY ADDR
	 BTST.B    #ICCMODE,IRCDRDI(A6) .	 CROSS COUNTRY MODE?
	 BNE.S	   LOADEND	       .	 YES: BRANCH
*
	 MOVE.L    #AP1TAB,A0	       .	 NO: LOAD APPROACH 1 ADDR
	 BTST.B    #IAPPR1,IRCDRDI(A6) .	 APPROACH 1 MODE?
	 BNE.S	   LOADEND	       .	 YES: BRANCH
*
	 MOVE.L    #AP2TAB,A0	       .	 NO: LOAD APPROACH 2 ADDR
LOADEND  MOVE.L    (A0)+,NCENLAT       +23	 STORE MAP CENTER LAT
	 MOVE.L    (A0)+,NCENLON       +23	 STORE MAP CENTER LON
	 MOVE.L    (A0)+,NRSLEW        +23	 STORE RECORDER SLEW RATE
	 MOVE.W    (A0)+,NRSCALE       +00	 STORE RECORDER MAP SCALE
*
*		   CHECK FOR CHART RECORDER SLEW
*
*	 IF SLEW SWITCH ON, SLEW .5 "/SEC ON RECORDER
*	    (.5"/SEC)*(1/20HZ)=.025"
*	    .025" = .00333333 DEG LAT/LON CROSS COUNTRY MODE
*	    .025" = .00097083 DEG LAT/LON APPROACH MODE
*
	 MOVE.L    NRSLEW,D2	       +23	 LOAD SLEW RATE
	 BTST.B    #ISLEWN,IRCDRDI(A6) .	 CHECK SLEW NORTH SWITCH
	 BEQ.S	   SLEWS	       .	 NO: BRANCH
*
	 BSR	   ADDLAT	       +23	 YES: SLEW (ADD D2 TO LAT)
SLEWS	 EQU	   *
	 BTST.B    #ISLEWS,IRCDRDI(A6) .	 CHECK SLEW SOUTH SWITCH
	 BEQ.S	   SLEWE	       .	 NO: BRANCH
*
	 NEG.L	   D2		       +23	 YES: SLEW
	 BSR	   ADDLAT	       +23	 SUB D2 FROM LAT
SLEWE	 EQU	   *
	 MOVE.L    NRSLEW,D2	       +23	 LOAD SLEW RATE
	 BTST.B    #ISLEWE,IRCDRDI(A6) .	 CHECK SLEW EAST SWITCH
	 BEQ.S	   SLEWW	       .	 NO: BRANCH
*
	 BSR	   ADDLON	       +23	 YES: SLEW (ADD D2 TO LON)
SLEWW	 EQU	   *
	 BTST.B    #ISLEWW,IRCDRDI(A6) .	 CHECK SLEW WEST SWITCH
	 BEQ.S	   SLEWEND	       .	 NO: BRANCH
*
	 NEG.L	   D2		       +23	 YES: SLEW
	 BSR	   ADDLON	       +23	 SUB D2 FROM LON
SLEWEND  EQU	   *
*
*
*			     UPDATE LAT
*
	 BTST.B    #IPOSFRZ,IRCDRDI(A6) .	 POSITION FREEZE?
	 BNE.S	   RCDR 	       .	 YES: DON'T CHANGE LAT/LON
*
	 MOVE.W    ZDGPFT,D1	       +26	 DEG TO FEET CONST.B
	 MOVE.W    NGVNS,D2	       +04	 N-S VEL WITH WIND   FT/SEC
	 MULS	   D1,D2	       +26+04+30 MULTIPLY  D2 = DEG/SEC/ITER
	 ASR.L	   #7,D2	       +30-07+23 RESCALE
*
*
	 BSR.S	   ADDLAT	       +23	 ADD D2 TO LAT AND LIMIT
*
*
*
*			     UPDATE LONG
*
	 MOVE.W    ZDGPFT,D1	       +26	 DEG TO FT CONST.B
	 MOVE.W    NGVEW,D2	       +04	 E-W VEL WITH WIND   FT/SEC
	 MULS	   D1,D2	       +26+04+30 MULTIPLY  D2 = DEG/SEC/ITER
	 ASR.L	   #7,D2	       +23	 RESCALE
*
	 MOVE.W    NGCLATIN,D3	       +07	 COS LAT INV =ABS(1/COS(NGLAT))
	 MULS	   D3,D2	       +23+07+30 MULTIPLY
	 ASR.L	   #7,D2	       +30   +23 RESCALE
*
	 BSR.S	   ADDLON	       +23	 ADD D2 TO LON AND LIMIT
*
*
*
*			     UPDATE CHART RECORDER
*
RCDR	 EQU	   *
	 MOVE.L    NGLAT,D0	       +23	 GET LAT
	 SUB.L	   NCENLAT,D0	       +23	 LAT DEVIATION FROM CENTER
*
*
	 BSR.S	   RCDRCALC	       +23-08+15 CALC RCDR DEV VOLTAGE
*
	 MOVEP.W   D0,ORCDNSD(A6)      +15	 OUTPUT N-S DEV
*
	 MOVE.L    NGLON,D0	       +23	 GET LON
	 SUB.L	   NCENLON,D0	       +23	 LON DEVIATION FROM CENTER
	 BSR.S	   RCDRCALC	       +23-08+15 CALC RCDR DEV VOLTAGE
	 MOVEP.W   D0,ORCDEWD(A6)      +15	 OUTPUT E-W DEV
	 BRA.S	   RCDREND	       .
*
*		   CALCULATE CHART RECORDER VOLTAGE
*	       INPUTS:
*		   D0  -  LAT OR LON DEVIATION FROM CENTER	     +23
*	       OUTPUTS:
*		   D0  -  RECORDER DEVIATION VOLTAGE		     +15
*
RCDRCALC EQU	   *
	 ASR.L	   #8,D0	       +23-08+15 RESCALE
	 ASR.L	   #1,D0	       +15-01+14 RESCALE
	 MULS	   NRSCALE,D0	       +06+14=20 MULT BY SCALE
	 ASR.L	   #5,D0	       +20-05=15 RESCALE
	 CMP.L	   #32767,D0	       +15	 COMPARE WITH +1
	 BLT.S	   INLIM1	       .
	 MOVE.L    #32767,D0	       +15	 LIMIT TO +1
INLIM1	 EQU	   *
	 CMP.L	   #-32767,D0	       +15	 COMPARE WITH -1
	 BGT.S	   INLIM2	       .
	 MOVE.L    #-32767,D0	       +15	 LIMIT TO -1
INLIM2	 EQU	   *
	 BCHG.L    #15,D0	       +15	 SET UP FOR AO
	 RTS			       .	 RETURN WITH WORD IN D0
*		       ADD TO LAT
*		       ----------
*	 INPUT:
*	       D2.L - NUMBER TO BE ADDED TO LAT 	   +23
*
ADDLAT	 MOVE.L    NGLAT,D1	       +23	 GET OLD LAT
	 BSR.S	   ADDLTLN	       .	 CALC NEW LAT
	 MOVE.L    D1,NGLAT	       +23	 STORE NEW LAT
	 RTS
*		       ADD TO LON
*		       ----------
*	 INPUT:
*	       D2.L - NUMBER TO BE ADDED TO LON 	   +23
*
ADDLON	 MOVE.L    NGLON,D1	       +23	 GET OLD LONGITUDE
	 BSR.S	   ADDLTLN	       .	 CALC NEW LON
	 MOVE.L    D1,NGLON	       +23	 STORE NEW LONGITUDE
	 RTS
*
*		       ADD TO LAT/LON
*		       --------------
*	 INPUTS:
*	       D2.L - NUMBER TO BE ADDED TO LAT/LON	   +23
*	       D1.L - LAT OR LON			   +23
*	 OUTPUT:
*	       D1.L - UPDATED LAT OR LON		   +23
*
ADDLTLN  ADD.L	   D2,D1	       +23   +23 CALC NEW LAT/LON
	 MOVE.L    ZLONMAX,D0	       +23	 CHECK FOR MAX LAT/LON
	 CMP.L	   D0,D1	       +23
	 BGT.S	   LIMPOS1	       .	 BRA IF BEYOND MAX
	 NEG.L	   D0		       +23	 MAKE MAX LAT/LON NEG
	 CMP.L	   D0,D1	       +23	 CHECK FOR MIN LAT/LON
	 BLT.S	   LIMNEG1	       .	 BRA IF BEYOND MIN
	 RTS
LIMPOS1  MOVE.L    ZLONMAX,D1	       +23	 LIMIT TO MAX
	 RTS
LIMNEG1  MOVE.L    ZLONMAX,D1	       +23
	 NEG.L	   D1		       +23	 LIMIT TO MIN
	 RTS
*
RCDREND  EQU	   *
*
*
*
*			     FIELD ELEVATION AND MAGVAR
*
	 MOVE.L    NSACF,A0			  ADDR OF CLOSEST FIELD
*
*			     FIELD ELEVATION
*
*     NGFE    =NGFE    -1/10*QESIK*(NGFE    -NVTNN18 )
*
	 MOVE.W    NGFE,D0	       +00	 OLD FIELD ELEV 	   FT
	 MOVE.W    D0,D1	       +00	 SAVE IT HERE TOO	   FT
	 SUB.W	   18(A0),D0	       +00	 SUB DEM FROM OLD ACTUAL   FT
	 MULS	   #$80,D0	       +00+08+08 INTEGRATE IT (0.5)	   FT
	 ASR.L	   #8,D0	       +08-08+00 RESCALE		   FT
	 SUB.W	   D0,D1	       +00	 OLD - DELTA =NEW	   FT
	 MOVE.W    D1,NGFE	       +00	 STORE IT		   FT
*
*			     MAG VAR
*      NMAGVAR =NMAGVAR + ( NVTNN34 - NMAGVAR ) ! 0.15
*
	 MOVE.W    34(A0),D0	       +06	 STATION MAGVAR (APPROX)   DEG
	 SUB.W	   NMAGVAR,D0	       +06	 DELTA MAGVAR
	 CMPI.W    #$000A,D0	       +06	 > 0.15?
	 BLE.S	   SKIPMV	       .	 WITHIN LIMIT
	 MOVE.W    #$000A,D0	       +06	 0.15 DEG/ITERATION
SKIPMV	 EQU	   *
	 ADD.W	   D0,NMAGVAR	       +06	 STORE FILTERED MAGVAR
*
*     NGCLAT  =ALIML(ABS(COS(NGLAT   )),.25)
*
	 MOVE.L    NGLAT,D0	       +23	 LAT COARSE		   DEG
	 ASR.L	   #8,D0	       +23-08+15 RESCALE
	 ASR.L	   #8,D0	       +15-08+07 RESCALE
	 ASR.L	   #1,D0	       +07-01+06 RESCALE TO WORD
	 TST.W	   D0		       +06	 IS D0 NEGATIVE ?
	 BPL.S	   ABS1
	 NEG.W	   D0				 MAKE IT POSITIVE
ABS1	 EQU	   *
	 BSR	   MCOS1	       +06	 D0 = COS(LAT D0) AT +15
	 CMPI.W    #$2000,D0	       +15	 GT .25 ?		   NORM
	 BGT.S	   LIM1 	       .	 YES
	 MOVE.W    #$2000,D0	       +15	 NO LIMIT TO .25	   NORM
LIM1	 MOVE.W    D0,NGCLAT	       +15	 STORE IT		   NORM
*
*     NGCLATIN=1/NGCLAT
*
	 MOVE.L    #$7FFFFFFF,D1       +31	 .99999 APPROX		   1
	 ASR.L	   #8,D1	       +31-08+23 .99999 APPROX		   1
	 DIVU	   D0,D1	       +23-15+08 COS LAT INV		 RATIO
	 ASR.W	   #1,D1	       +08-01+07 COS LAT INV		 RATIO
	 MOVE.W    D1,NGCLATIN	       +07	 COS LAT INV		 RATIO
*
*
	 PAGE
*************************************************************
*
*			     NAV1 (VOR/ILS)
*
*************************************************************
	 BTST.B    #IMSTSW,IMPSDI1(A6) LB	 MASTER ON?		   LB
	 BEQ	   CLRALL1	       .	 NO CLEAR INDICATORS
*
	 BTST.B    #INAV1ON,INAV1F3(A6) LB	  NAVCOM 1 ON?		    LB
	 BEQ	   CLRALL1	       .	 NO
*
	 BTST.B    #INAV1ID,INAV1F3(A6) .	 SEE IF IDENT ON
	 BNE.S	   NV1IDON
	 MOVE.B    #$FF,ONV1VOL(A6)    .	 SET VOLUME OFF
	 BRA.S	   NV1IDOFF	       .	 CONTINUE
*
*		   NAV1 VOLUME CONTROL
*
NV1IDON  MOVEP.W   INAV1VOL(A6),D0     +15	 XFER AUDIO VOLUME  OFFSET BIN
	 MOVE.B    D0,INAV1VOL(A6)     .	 START NEXT A/D CONVERSION
	 BSR	   CONATT	       .	 MAKE VOLUME CONTROL LINEAR
	 MOVE.B    D0,NNV1VOL	       .	 STORE IN TEMP CORE FOR DEBUG
	 MOVE.B    D0,ONV1VOL(A6)      .	 OUTPUT NAV1 VOLUME
*
*
*
*			 STATION RANGE
*
NV1IDOFF TST.B	   NAV1TUN	       LB	 STATION TUNED?
	 BEQ	   CLRILS1	       .	 NO CLR THE NEEDLES
	 MOVE.L    NAV1STA,A1	       .	 YES FIND STA NO
	 BSR	   NAVRCV	       .	 CALL NAV RCVR SUBROUTINE
*
*	 TEST NEW NATAN2 ROUTINE & DOUBLE PRECEION MUL	DSMUL
*
	 MOVE.L    NRXGASL,NSPARE1     +23	 SAVE XGASL
	 MOVE.L    NRYGASL,NSPARE2     +23	 SAVE YGASL
	 MOVE.L    NRAGAS,NSPARE3      +08	 NEW AGAS STORED FOR DEBUG
	 MOVE.L    NRBGAS,NSPARE4      +08	 NEW BGAS STORED FOR DEBUG
*
	 MOVE.L    NSPARE5,NSPARE7     +10	 OLD AGAS STORED FOR DEBUG
	 MOVE.L    NSPARE6,NSPARE8     +10	 OLD BGAS STORED FOR DEBUG
*
	 MOVE.W    NSPAREW2,NSPAREW3   +06	 BRG TO STATION FROM NATAN2
	 MOVE.W    NBASEREG,NSPAREW4   +06	 BASE ANGLE FROM NATAN2
*
	 MOVE.W    NRXGAS,NSPAREW7     +12	 XGAS TEMP STORE
	 MOVE.W    NRYGAS,NSPAREW8     +12	 YGAS TEMP STORE
*
	 MOVE.W    NSPAREW5,NSPAREW6   +12	 YGASL/XGASL
*
	 TST.B	   NRLOS	       .	 LINE OF SIGHT?
	 BEQ	   CLRILS1	       .	 NO? THEN CLEAR SYSTEM
*
	 TST.B	   NRWR1	       .	 STATION IN RANGE ?
	 BEQ	   CLRILS1
*
*
*			     SIGNAL LEVEL NAV 1 ILS/VTC
*     INAV1VOL =ALIMU((NRRGAS/COR ALT RNG)**2 LIM.99)
*
	 MOVE.W    NRRGAS,D5	       +06	 RANGE TO STATION
	 MOVE.W    20(A1),D7	       +00	 RANGE 1
	 EXT.L	   D5		       +06	 MAKE TO LONG WORD
	 TST.W	   D7		       .	 CHECK FOR ZERO
	 BNE.S	   NAV1Z	       .	 NOT ZERO, GO TO DIVIDE
	 MOVE.W    #1,D7	       +00	 MAKE RANGE 1 NON-ZERO
NAV1Z	 DIVS	   D7,D5	       +06	 % OF MAX RANGE
	 MULS	   D5,D5	       +12	 % RANGE SQUARED      (RATIO)
	 MULS	   #07,D5	       +12+04+16 MULT BY .44 (FUDGE FACTOR)
	 ASR.L	   #8,D5	       +16-08+08 RESCALE
	 CMP.L	   #$80,D5	       +08	 CHECK FOR LIMIT
	 BMI.S	   NAV1Z1	       .	 BRANCH IF OK
	 MOVE.B    #$80,D5	       +08	 LIMIT TO $80 (MUTE)
NAV1Z1	 MOVE.B    D5,NAV1VOL	       +08	 SAVE NAV 1 VOLUME
*
	 SUB.B	   #$80,D5	       +08	 D5-$80    ($80=MUTE)
	 NEG.B	   D5		       +08	 $80-D5 = WHITE NOISE LEVEL
	 MOVE.B    D5,ONV1WNL(A6)      +08	 OUTPUT WHITE NOISE LEVEL
*
*
*			     FIND STA TYPE (VTC/ILS)
*
	 MOVE.W    2(A1),D2	       HEX	 STA TYP
	 CMP.W	   #$0068,D2	       HEX	 VTC ?
	 BEQ	   VOR1 	       .	 YES
	 PAGE
***********************************************************
*
*			     NAV1 ILS
*
***********************************************************
*
*			     MAG BEARING TO STATION (+/- 180 DEG)
*     NAV1BRG =NRBRG=(ATAN(NRYGAS/NRXGAS) - 36(A0) = -DEV
*     36(A0)= RNWY MAG HEADING FROM ADDP TABLE
*     DEV =(RNWY MAG HEADING - AIRCRAFT TO ILS STATION BEARING) BY DEFINTION
*     A/C TO ILS STA BEARING=(ATAN(NRYGAS/NRXGAS))=ATAN(DELTA LON/DELTA LAT)
*
	 MOVE.W    NRBRG,NAV1BRG       +06	 -DEV FOR ILS STA
*
*			     ANGULAR DIFFERENCE(DEVIATION)
*    -DEV = NCDI1DEG = NAV1BRG	  FOR TUNED ILS STATIONS
*
	 MOVE.L    NAV1STA,A0	       .	 FIND STA SA		   ADDR
*
*	 MOVE.W    36(A0),D0	       +06	 RNWY MAG HDG		   DEG
*	 SUB.W	   NAV1BRG,D0	       +06	 DIFFERENCE= +DEV	   DEG
*	 BSR	   AEPC 	       +06	 LIMIT DEV TO +/-180 DEG
*	 NEG.W	   D0		       +06	 -DIFFERENCE=-DEV	   DEG
*
	 MOVE.W    NAV1BRG,D0	       +06	 -DEV
	 BSR	   NCDI 	       +06	 COURSE DEVIATION	   DEG
	 MOVE.W    D0,NCDI1DEG	       +06	 +DEV
*
*			     LOCALIZER INDICATOR DRIVE
*     NCDI1DEG =ALIM(NCDI1DEG ,5)
*
	 CMP.W	   #319,D0	       +06	 GT.4.99		   DEG
	 BPL.S	   PLUS31	       +06	 YES
	 CMP.W	   #-319,D0	       +06	 GT -4.99		   DEG
	 BPL.S	   LMTILS1	       .	 YES
	 MOVE.W    #-319,D0	       +06	 LIMIT TO -4.99
	 BRA.S	   LMTILS1	       .
PLUS31	 MOVE.W    #319,D0	       +06	 LIMIT TO 4.99		   DEG
*
*     ONAV1LR =DEFLECTION/5.	       NEEDLE DRIVE
LMTILS1  EXT.L	   D0
	 DIVS	   #$0005,D0	       +06-00+06 DIV BY 5		   NORM
	 ASL.W	   #7,D0	       +06+07+13			   NORM
	 ASL.W	   #2,D0	       +13+02+15			   NORM
	 NEG.W	   D0		       +15	 FOR DRIVING NEEDLE
*	 ASR.W	   #1,D0	       +15	 DIVIDE BY 2(NOT NEEDED)
*						 i.e. 2.5 DEG = EDGE OF YELLOW
*							 = 5 V = 3 DOTS VOA9
	 BCHG.L    #15,D0	       +15	 CHANGE BIT 15 FOR OUTPUT
	 MOVE.W    NAV1LR,D1	       +15	 GET OLD AO
	 BSR	   INTEG1	       .	 INTEGRATE TO NEW AO
	 MOVE.W    D0,NAV1LR	       +15	 STORE NEW AO
	 MOVEP.W   D0,ONAV1LR(A6)      +15	 OUTPUT NEW CDI AO	   NORM
*
*			     "ON"("TO") FLAG
*
	 MOVE.W    #$FFFF,D0	       +15	 ONLY "TO" FLAG FOR ILS
	 MOVEP.W   D0,ONAV1TF(A6)
*
*			     GLIDE SLOPE
*			      (ASSUME IN RNG SINCE ILS IN RNG) AN APPROX
*
	 BTST.B    #IGSFAIL,IFAIL2(A6) LB	 GS MALF?
	 BNE	   GSOFF1	       .	 YES
*
	 MOVE.L    NRAGAS,D2	       +08	 DISTANCE ALONG RUNWAY	  FEET
	 MOVE.W    42(A0),D3	       +00	 RUNWAY LENGTH		  FEET
	 EXT.L	   D3		       +00	 RUNWAY LENGTH		  FEET
	 ASL.L	   #8,D3	       +08	 RESCALE
	 SUB.L	   D3,D2	       +08	 D2=DIST TO TOUCH DOWN	  FEET
*						 ON BACKCOURSE IF NEGATIVE
	 BMI	   GSOFF1
*
*	 MOVE.W    NRRGAS,D0	       +06	 RANGE TO STATION	   NM
*	 MULS	   #6076,D0	       +06-00+06 CHANGE TO FEET 	   FEET
*	 ASR.L	   #4,D0	       +06-04+02 RESCALE
*
*	 MOVE.L    NAV1STA,A0	       .	 SA OF STA
*	 MOVE.W    42(A0),D1	       +00	 RUNWAY LENGTH		   FEET
*	 EXT.L	   D1		       +00	 RUNWAY LENGTH
*	 ASL.L	   #2,D1	       +02	 RESCALE
*	 SUB.L	   D1,D0	       +02	 DIST TO TOUCHDOWN 4 RES   FEET
*
	 MOVE.L    D2,D0	       +08	 DIST TO TOUCH DOWN	   FEET
	 ASR.L	   #6,D0	 +08-06+02	 RESCALE TO WORD	   FEET
	 ASR.L	   #4,D0	 +02-04-02	 RESCALE TO WORD       FEET
*
	 TST.L	   D0		       -02	 CHECK FOR ZERO
	 BNE.S	   DIVOK	       .	 BRANCH IF OK TO DIVIDE
	 MOVE.L    #1,D0	       .	 MAKE NON ZERO FOR DIVIDE
*
DIVOK	 MOVE.W    NRZGAS,D1	       +00	 HT. ABOVE STATION	   FEET
	 EXT.L	   D1		       +00	 HT. ABOVE STATION	   FEET
	 ASL.L	   #8,D1	       +00+08+08 RESCALE
	 ASL.L	   #2,D1	       +08+02+10 RESCALE
*
	 DIVS	   D0,D1	       +10+02+12 CALC TAN		  RATIO
*
	 BVS.S	   GSPLUS14			 LIMIT TO TAN(1.39) IF OVFLOW
	 MOVE.W    D1,D0	       +12
	 MOVE.W    D0,NGSTAN	       +12	 TEMP STORE FOR DEBUG
	 SUB.W	   #215,D0	       +12	 SUBTRACT TAN(3)
*
*	 FLAIR = .999 IF(NRAGAS - RNWY) > 3040 FEET   , 3040 FT = 0.5 NM
*	 FLAIR = 1/6  +  (NRAGAS - RNWY)/3648	      , 3648 FT = 0.6 NM
*
*   NOTE: D4.W = FLAIR IS ALWAYS LESS TNAN OR EQAUL TO .999  AT SCALE OF +10
*
*	INPUTS:     D0.W = (NGSTAN - TAN(3))	+12    = DELTA GS
*		    D2.L = (NRAGAS - RNWY)	+08  IN FEET
*	OUTPUTS:    D0.L = (NGSTAN - TAN(3)) * FLAIR   +22  =(DELTA GS)*FLAIR
*
	 CMP.L	   #778240,D2	       +08	 IS D2 > 3040 FT ?
	 BPL.S	   FLAIR1
	 DIVS	   #912,D2	 +08+02+10	 D2.W = D2.L/3648     RATIO
	 ADD.W	   #170,D2	       +10	 D2.W = D2 + 0.166
	 MOVE.W    D2,D4	       +10
	 BRA.S	   FLAIR2
FLAIR1	 MOVE.W    #1023,D4	       +10	 FLAIR = D4.W = 0.999
*
FLAIR2	 EQU	   *
	 MULS	   D4,D0	 +10+12+22	 D0.L = D0.W * D4.W
*
	 CMP.L	   #102390,D0	       +22	 GT TAN(1.398) ?
	 BPL.S	   GSPLUS14	       .	 BRANCH IF GT
	 CMP.L	   #-102390,D0	       +22	 LT TAN(-1.398) ?
	 BPL.S	   CONT3	       +22
	 MOVE.L    #-102390,D0	       +22	 LIMIT TO TAN(-1.398)
	 BRA.S	   CONT3
GSPLUS14 MOVE.L    #102390,D0	       +22	 LIMIT TO TAN(1.398)
*
*LT14D1   CMP.W     #07,D0		+12	  CHECK FOR > TAN(.1)
*	 BPL.S	   CONT1	       .	 IF GREATER CONTINUE
*	 CMP.W	   #-07,D0	       +12	 CHECK FOR < TAN(-.1)
*	 BMI.S	   CONT2	       .	 IF LESS CONTINUE
*	 MOVE.W    #0,D0	       +12	 ELSE LIMIT TO 0 DEVIATION
*	 BRA.S	   CONT3
*CONT1	  SUB.W     #07,D0		+12	  SUB TAN(.1) FOR POS FULL SCAL
*	 BRA.S	   CONT3
*CONT2	  ADD.W     #07,D0		+12	  ADD TAN(.1) FOR NEG FULL SCAL
*
CONT3	 EQU	   *
	 ASL.L	   #2,D0	  +22+02+24	 RESCALE
	 DIVS	   #25,D0	  +24-10+14	 DIV BY TAN(1.4)	  RATIO
	 ASL.W	   #1,D0	  +14+01+15	 RESCALE FOR OUTPUT
*
	 NEG.W	   D0			+15	  FOR NEEDLE DRIVE POLARITY
	 BCHG.L    #15,D0		+15	  CHANGE TO OFFSET BINARY
	 MOVE.W    NAV1GS,D1	       +15	 GET OLD AO
	 BSR	   INTEG1	       .	 INTEGRATE TO NEW AO
	 MOVE.W    D0,NAV1GS	       +15	 STORE NEW AO
	 MOVEP.W   D0,ONAV1GS(A6)      +15	 OUTPUT NEW AO (GS NEEDLE)
	 BCLR.B    #ONAV1GSF,NCDI1DO   .	 SET GS FLAG "ON"
	 BRA	   ENDVRIL1
*
*
*			     GS FLAG "OFF"
*
GSOFF1	 BSET.B    #ONAV1GSF,NCDI1DO   .	 GS FLAG "OFF"
	 MOVE.W    #$8000,D0
	 MOVEP.W   D0,ONAV1GS(A6)      +15	 CENTRE THE GS NDLE "AO" 0 VOLT
	 BRA	   ENDVRIL1
*
*
	 PAGE
**************************************************************
*
*			     NAV1 VOR
*
**************************************************************
VOR1	 EQU	   *
	 MOVEP.W   ICRS1SC(A6),D0      +15	 SIN/COS OF SET COURSE
	 BCHG.L    #15,D0	       +15	 CONVERT TO 2'S COMPLEMENT
*
*      SCALE SIN/COS INPUT
*
	 BTST.B    #OCRS1SEL,NCDI1DO   .	 READING SINE OR COS?
	 BEQ.S	   SINE1	       .
*
	 MOVE.W    D0,NCRS1COS	       +15	 SAVE COSINE
	 BCLR.B    #OCRS1SEL,NCDI1DO   .	 CLR TO READ SIN NEXT TIME
	 BRA.S	   CON1 	       .
*
SINE1	 MOVE.W    D0,NCRS1SIN	       +15	 SAVE THE SINE
	 BSET.B    #OCRS1SEL,NCDI1DO   .	 SET TO READ COS NEXT TIME
*
CON1	 MOVE.B    NCDI1DO,OCDI1DO(A6) .	 OUTPUT TEMP TO OUTPUT
*
	 MOVE.W    NCRS1COS,D0	       +15	 PREPARE FOR NATAN2
	 MOVE.W    NCRS1SIN,D1	       +15	 PREPARE FOR NATAN2
	 EXT.L	   D1		       +15
	 EXT.L	   D0		       +15
	 BSR	   NATAN2			 RESOLVE SIN/COS TO DEG
	 MOVE.W    D0,NAV1OBS	       +06	 SAVE NAV1 SELECTED BEARING
*
*			     TEST IF NAV1 PUSH-TO-TEST ON
*			     IF ON THEN NRBRG=180 DEGREES
*
	 BTST.B    #INAV1TST,ICRS1DI(A6)	 TEST IF PUSH TO TEST PRESSED
	 BEQ.S	   NAV1CON1
	 MOVE.W    #11520,NRBRG        +06	 NRBRG=180 DEG
*
*
*
*			     MAG BEARING FROM STATION(0-360)
*     NAV1BRG =TRUE BRG +NMAGVAR
*
NAV1CON1 MOVE.W    NRBRG,NAV1BRG       +06	 MAG BRG FROM STA	   DEG
*
*		   DRIVE RMI VOR POINTER #1
*
*	 BTST.B    #IRMI1ADF,IMPSDI1(A6)
*	 BEQ.S	   NOPTR	       .	 PTR 1 NOT VOR
*	 MOVE.W    NAV1BRG,D0	       +06	 DRIVE RMI PTR 1 W/ VOR 1 BRG
*	 SUB.W	   NPSIMH,D0	       +06	 SUBTRACT MAG HDG
*	 BSR	   AEPC 	       +06	 LIMIT 0-360
*	 BSR	   MSC1 	      . 	GET SIN/COS OF BEARING
*	 BCHG.L    #15,D0	       .	 CHANGE BIT 15 FOR OUTPUT
*	 BCHG.L    #15,D1	       .	 CHANGE BIT 15 FOR OUTPUT
*	 MOVEP.W   D0,ORMI1S(A6)       +15	 SINE OF PTR 1
*	 MOVEP.W   D1,ORMI1C(A6)       +15	 COSINE OF PTR 1
*NOPTR	  EQU	    *
*
*			     ANGULAR DIFFERENCE(DEVIATION)
*     DEVIATION  =NAV1OBS -NAV1BRG
*
	 MOVE.W    NRBRG,D0	       +06
	 SUB.W	   NAV1OBS,D0	       +06	 -DEVIATION		   DEG
	 BSR	   AEPC 	       +06	 LIMIT -DEV TO +/- 180 DEG
*	 BSR	   NCDI 	       +06	  DEVIATION		   DEG
	 MOVE.W    D0,NCDI1DEG	       +06	 DEVIATION		   DEG
*
*			     VTC       INDICATOR DRIVE
*     NCDI1DEG =ALIM(NCDI1DEG ,20.)
*
*	 CMP.W	   #1279,D0	       +06	 GT 19.99		   DEG
*	 BPL.S	   PLUS121	       .	 YES
*	 CMP.W	   #-1279,D0	       +06	 GT -19.99		   DEG
*	 BPL.S	   LMTVOR1	       .	 YES
*	 MOVE.W    #-1279,D0	       +06	 LIMIT -19.99		   DEG
*	 BRA.S	   LMTVOR1	       .
*PLUS121  MOVE.W    #1279,D0		+06	  LIMIT TO 19.99	    DEG
*
*     ONAV1LR =DEFLECTION/20,	       NEEDLE DRIVE
*LMTVOR1  EXT.L     D0
*	 DIVS	   #20,D0	       +06-00+06 DIV BY 20		   NORM
*	 ASL.W	   #7,D0	       +06+07+13			   NORM
*	 ASL.W	   #2,D0	       +13+02+15			   NORM
*	 NEG.W	   D0		       +15	 FOR NEEDLE DRIVE
**	 ASR.W	   #1,D0	       +15	 DIVIDE BY 2(NOT NEEDED)
*						 10 DEG = EDGE OF YELLOW
*						     = 5 V = 3 DOTS VOA9
*
*	 CDI NEEDLE = SIN(-DEV) LIMIT TO +/- 4 DOTS, +/- 2 DOTS=YELLOW EDGE
*	  INPUT = -DEV IN D0.W AT +06  = NCDI1DEG
*
	 BSR	   MSC1 			 SIN IN D0, COS IN D1 AT +15
	 MULS	   #737,D0	 +15+06+21	 D0 = 11.517472(SIN(-DEV)) DOTS
*
*     LIMIT # OF DOTS TO +/- 4 BEFORE OUTPUTTING = +/- 10 VOLTS
*			 +/- 2 DOTS = 5 V =(EDGE OF YELLOW = 3 DOTS ON VOA9)
*
	 CMPI.L    #8388607,D0	       +21	 IS D0 < 3.999 DOTS ?
	 BLT.S	   VOR1LM1
	 MOVE.L    #8388607,D0	       +21	 LIMIT TO +3.999 DOTS
	 BRA.S	   VOR1LM2
VOR1LM1  CMPI.L    #-8388607,D0        +21	 IS D0 < -3.999 DOTS ?
	 BGT.S	   VOR1LM2
	 MOVE.L    #-8388607,D0        +21	 LIMIT TO -3.999 DOTS
VOR1LM2  EQU	   *
*
	 DIVS	   #256,D0	 +21-06+15	 DIV BY 4 TO NORMALISE
*
	 BCHG.L    #15,D0	       +15	 CONVERT TO OFFSET BINARY
	 MOVEP.W   D0,ONAV1LR(A6)      +15	 CDI "AO"  IN VOLTS        NORM
*
*	 TO-OFF-FROM FLAG CALCULATIONS
*	 -DEV IN NCDI1DEG = (NAV1BRG - NAV1OBS)  LIMITED TO +/- 180 DEG
*	 D1.W = COS(-DEV) AT +15
*	     -75 < -DEV < 75 DEG       FLAG = "TO"
*	    -105 > -DEV >105 DEG       FLAG = "FROM"
*  75 < -DEV <105 DEG	OR -105 < -DEV <-75 DEG FLAG BETWEEN TO-OFF-FROM
*
	 MOVE.W    D1,D2	       +15	 SAVE COS FOR SIGN
*
	 BCHG.L    #15,D1	       +15	 CONVERT FROM BTC TO BOB
	 MOVEP.W   D1,ONAV1TF(A6)      +15	 SET "TO/FROM" FLAG        VOLT
*
*			     CONE OF CONFUSION
*
*
*
*			     CONE RADIUS
*
*	 CONE RADIUS = (NARZGAS X TAN 30)/6080	IN NAUTICAL MILES
*	 TAN 30 = 0.5773503 = 591 AT SCALE +10
*	 1 N.M. = 6080 FT.  = 6080  AT SCALE +00
*
*
	 MOVE.W    NRZGAS,D7	       +00	 HEIGHT ABOVE STATION	   FEET
	 MULS	   #591,D7	       +10	 CONE RADIUS IN D7.L IN    FEET
	 DIVS	   #6080,D7	       +10	 CONE RADIUS IN D7.W IN    N.M.
	 ASR.W	   #4,D7	       +10-04+06 RESCALE
	 MOVE.W    D7,NCONRAD	       +06	 SAVE CONE RADIUS IN NCONRAD
	 CMP.W	   NRRGAS,D7	       +06	 CONE RADIUS <= A/C DIST ? N.M.
	 BLE.S	   OUTCONE1	       .	 YES
	 MOVE.W    #$8000,D0	       .
	 MOVEP.W   D0,ONAV1TF(A6)      +15	 NO SET FLAG "OFF"   0     VOLT
OUTCONE1 EQU	   *
*
*			     MALF LOC/VTC (CDI)
*
ENDVRIL1 BTST.B    #IVL1FAIL,IFAIL1(A6) LB	  MALF VTC/ILS ?
	 BEQ.S	   ENDVL1	       .	 NO
	 MOVE.W    #$8000,D0	       .
	 MOVEP.W   D0,ONAV1LR(A6)      +15	 YES CENTER CDI      0	   VOLT
	 MOVEP.W   D0,ONAV1TF(A6)      +15	 YES SET "OFF" FLAG  0     VOLT
	 BRA.S	   ENDVL1
*
*			     ILS/VTC NOT IN RANGE
*
CLRILS1  BSET.B    #ONAV1GSF,NCDI1DO   LB	 GLIDE SLOPE FLAG "OFF"    LB
	 SF.B	   NAV1TUN	       LB	 CLEAR NAV1 TUNED	   LB
	 CLR.L	   NAV1FREQ	       +00	 CLEAR FREQ
	 MOVE.W    #$8000,D0	       .
	 MOVEP.W   D0,ONAV1TF(A6)      +15	 SET LOC/ILS FLAG "OFF" 0  VOLT
	 MOVEP.W   D0,ONAV1LR(A6)      +15	 CENTER CDI 1 NEEDLE	0  VOLT
	 MOVEP.W   D0,ONAV1GS(A6)      +15	 CENTER GSDI  NEEDLE	0  VOLT
	 MOVE.B    #$FF,ONV1WNL(A6)    +08	 MUTE WHITE NOISE LEVEL NAV1
	 ST.B	   NAV1VOL	       +08	 SET NAV1 IDENT TO MAX ATTEN
	 PAGE
**********************************************************************
*
*	 MARKER BEACONS (NAV1 ONLY)
*
*	 NE = (64*NRZGAS*.2)/[8*(NRAGAS**2)+(NRBGAS**2)+(NRZGAS**2)]
*
*	 NMKRLVL = 1 - [((NE*NK1 LIM.12)+(NE-NK2 LIM0)*NK3) LIM.999]
*
*		   HI SENSE	       LO SENSE
*	 NK1	    600 	       187
*	 NK2	   .0002	       .002
*	 NK3	    352 	       63
*
ENDVL1	 TST.B	   NMKRVL	       .	 MARKER VALID?
	 BEQ	   CLRMKRS	       .	 NO, IT IS NOT VALID
*				       .	 OF EVERY MKR STA
	 MOVE.L    NMKRSA,A1	       .	 LOAD ADDRESS OF MKR STA
	 BSR	   NAVRCV	       .	 CALL NAV RCVR SUBROUTINE
*
	 TST.B	   NRWR1	       .	 MARKER IN RANGE?
	 BEQ	   CLRMKRS	       .	 YES IT IS OUT OF RANGE
*
	 BTST.B    #IADF1MKR,IADF1DI(A6)	 MKRS ON?
	 BEQ	   CLRMKRS	       .
*
	 MOVE.W    #63,NK3	       +00	 CONSTANT K3=63
	 MOVE.W    #8389,NK2	       +22	 CONSTANT K2=.002
	 MOVE.W    #187,NK1	       +00	 CONSTANT K1=187
	 TST.B	   NMKRSNS	       .	 LO SENSE ?
	 BEQ.S	   LOSNS	       .	 YES, SKIP HI SENSE K'S
*
	 MOVE.W    #839,NK2	       +22	 CONSTANT K2 .0002
	 MOVE.W    #600,NK1	       +00	 CONSTANT K1
LOSNS	 MOVE.L    NRAGAS,D0	       +08	 AGAS		     FEET
	 ASR.L	   #8,D0	       +08-08+00 RESCALE TO WORD
	 MULS	   D0,D0	       +00+00+00 AGAS SQUARED	     FEET SQ
	 ASL.L	   #3,D0	       +00	 MULT BY 8
	 MOVE.L    NRBGAS,D1	       +08	 BGAS		     FEET
	 ASR.L	   #8,D1	       +08-00+00 RESCALE TO WORD
	 MULS	   D1,D1	       +00+00+00 BGAS SQUARED	     FEET SQ
	 ADD.L	   D1,D0	       +00	 ADD TO TOTAL
	 MOVE.W    NRZGAS,D1	       +00	 ZGAS		     FEET
	 MULS	   D1,D1	       +00	 ZGAS SQUARED	     FEET SQ
	 ADD.L	   D1,D0	       +00	 ADD TO TOTAL
	 ASR.L	   #8,D0	       +00-08-08 RESCALE TO WORD
	 ASR.L	   #6,D0	       -08-06-14
	 TST.W	   D0		       .	 IS IT ZERO
	 BNE.S	   ROK		       .	 NO, OK TO DIVIDE
*
	 ADDQ.W    #1,D0	       -14	 MAKE IT NON-ZERO
ROK	 MOVE.W    NRZGAS,D1	       +00	 ZGAS		     FEET
	 MULS	   #3277,D1	       +08	 64*ZGAS*.2
	 DIVS	   D0,D1	       +08+14+22
	 BVC.S	   NOTOVER	       .	 CHECK FOR OVERFLOW
*
	 MOVE.W    #32767,D1	       +22	 LIMIT IF OVERFLOW
NOTOVER  CMP.W	   #16777,D1	       +22	 GT .004  ?
	 BMI.S	   NOLIM4	       .	 NO: BRANCH
*
	 MOVE.B    #0,D1	       +08	 YES: FULL VOLUME
	 BRA.S	   MKRLVL1	       .	 OUTPUT VOLUME LEVEL
*
NOLIM4	 MOVE.W    D1,NE	       +22	 SAVE NE
	 MULS	   NK1,D1	       +22+00+22 MULT BY CONST 1
	 CMP.L	   #1568670,D1	       +22	 IS D1 <= .374
	 BLE.S	   MLMT1
*
	 MOVE.L    #1568670,D1	       +22	 LIMIT TO .374
MLMT1	 MOVE.W    NE,D0	       +22
	 SUB.W	   NK2,D0	       +22	 NK2=.002 BREAK POINT
	 BPL.S	   MENDC1	       .	 BRANCH IF NEG
*
	 MOVE.W    #0,D0	       +22	 LIMIT TO 0
MENDC1	 MULS	   NK3,D0	       +22+00+22
	 ADD.L	   D1,D0	       +22
	 CMP.L	   #261724,D0	       +22	 GT .0624?
	 BPL.S	   MENDC	       .	 BRANCH IF GT
*
	 MOVE.B    #$FF,D1	       +08	 ELSE MUTE
	 BRA.S	   MKRLVL1	       .	 OUTPUT
*
MENDC	 MOVE.L    #2097151,D1	       +22	 .4999
	 SUB.L	   D0,D1	       +22	 .4999 - D0
	 ASR.L	   #6,D1	       +22-06+16 CHANGE TO BYTE
	 ASR.L	   #8,D1	       +16-08+08
MKRLVL1  MOVE.B    D1,NMKRLVL	       +08	 STORE MKR LEVEL
	 MOVE.L    NMKRSA,A0	       .	 ADDRESS OF MKR
	 MOVE.W    2(A0),D0	       .	 STA TYPE
	 CMP.W	   #$008C,D0	       .	 OM ?
	 BEQ.S	   OTRMKR	       .
*
	 CMP.W	   #$008B,D0	       .	 MM ?
	 BEQ.S	   MIDMKR	       .
*
	 MOVE.B    NMKRLVL,NAMRA       .	 MUST BE INNER MARKER
	 ST.B	   NIMTONE
	 SF.B	   NMMTONE
	 SF.B	   NOMTONE
	 ST.B	   NMMRA
	 ST.B	   NOMRA
	 BRA.S	   ENDMKRS
*
MIDMKR	 EQU	   *
	 MOVE.B    NMKRLVL,NMMRA       .	 MIDDLE MARKER
	 ST.B	   NMMTONE
	 SF.B	   NIMTONE
	 SF.B	   NOMTONE
	 ST.B	   NAMRA
	 ST.B	   NOMRA
	 BRA.S	   ENDMKRS
*
OTRMKR	 EQU	   *
	 MOVE.B    NMKRLVL,NOMRA       .	 OUTER MARKER
	 ST.B	   NOMTONE
	 SF.B	   NIMTONE
	 SF.B	   NMMTONE
	 ST.B	   NAMRA
	 ST.B	   NMMRA
	 BRA.S	   ENDMKRS
*
CLRMKRS  EQU	   *
	 ST.B	   NAMRA	       .	 TURN OFF ALL MARKERS
	 ST.B	   NMMRA
	 ST.B	   NOMRA
	 BRA.S	   ENDMKRS
*
*
*			     POWER NAV 1 CLEAR EVERYTHING
*
CLRALL1  EQU	   *
	 SF.B	   NAV1TUN	       LB	 CLR NAV1 IN TUNE
	 CLR.L	   NAV1FREQ	       +00	 CLR NAV1 FREQ
	 BSET.B    #ONAV1GSF,NCDI1DO   LB	 CLR NAV1 GS FLAG "OFF"
	 ST.B	   NAV1VOL	       +15	 CLR NAV1 IDENT LEVEL  0   VOL
	 MOVE.B    #$FF,ONV1WNL(A6)    +08	 MUTE WHITE NOISE LEVEL NAV1
	 MOVE.B    #$FF,ONV1VOL(A6)    +08	 TURN NAV1 VOL OFF
	 MOVE.W    #$8000,D0
	 MOVEP.W   D0,ONAV1TF(A6)      +15	 CLR NAV1 ILS/LOC "OFF"0   VOLT
	 MOVEP.W   D0,ONAV1LR(A6)      +15	 CTR NAV1 ILS/LOC NDLE 0   VOLT
	 MOVEP.W   D0,ONAV1GS(A6)      +15	 CTR NAV1 GS NDLE      0   VOLT
ENDMKRS  EQU	   *
	 MOVE.B    #$FF,OMKRTST(A6)
	 MOVE.B    #$20,OMKRVOL(A6)		 SET MARKER VOLUME LEVEL
	 BCLR.B    #OOMFAIL,NMKRDOB
	 BCLR.B    #OOMRAATT,NMKRDOB
	 BCLR.B    #OMMFAIL,NMKRDOB
	 BCLR.B    #OMMRAATT,NMKRDOB
	 BCLR.B    #OAMFAIL,NMKRDOB
	 BCLR.B    #OAMRAATT,NMKRDOB
	 BTST.B    #IMSTSW,IMPSDI1(A6)
	 BNE.S	   MSTON			 JUMP IF MASTER SW ON
*
	 BCLR.B    #OMKRLTST,NMKRDOB		 CLR MARKER LAMP TEST
	 BRA.S	   CONTIN1
*
MSTON	 BSET.B    #OMKRLTST,NMKRDOB		 SET MARKER LAMP TEST
*
CONTIN1  MOVE.B    NMKRDOB,OMKRDO(A6)		  OUTPUT MARKER DO'S
	 PAGE
*****************************************************************
*
*			NAV2	  (VOR/ILS)
*
*****************************************************************
*
	 BTST.B    #IMSTSW,IMPSDI1(A6) LB	 MASTER ON?		   LB
	 BEQ	   CLRILS2	       .	 NO CLEAR INDICATORS
*
	 BTST.B    #INAV2ON,INAV2F3(A6) LB	  NAVCOM 2 ON?		    LB
	 BEQ	   CLRILS2	       .	 NO
*
	 BTST.B    #INAV2ID,INAV2F3(A6) .	 SEE IF IDENT ON
	 BNE.S	   NV2IDON
	 MOVE.B    #$FF,ONV2VOL(A6)    .	 SET VOLUME OFF
	 BRA.S	   NV2IDOFF	       .	 CONTINUE
*
*		   NAV2 VOLUME CONTROL
*
NV2IDON  MOVEP.W   INAV2VOL(A6),D0     .	 XFER AUDIO VOLUME OFFSET BIN
	 MOVE.B    D0,INAV2VOL(A6)     +15	 START A/D CONVERSION
	 BSR	   CONATT	       .	 ATTEN CONTROL
	 MOVE.B    D0,NNV2VOL	       .	 STORE IN TEMP CORE FOR DEBUG
	 MOVE.B    D0,ONV2VOL(A6)
*
*
*				       STATION RANGE
*
NV2IDOFF TST.B	   NAV2TUN	       LB	 STATION TUNED?
	 BEQ	   CLRILS2	       .	 NO CLR THE NEEDLES
	 MOVE.L    NAV2STA,A1	       .	 YES FIND STA NO
	 BSR	   NAVRCV	       .	 CALL NAV RCVR SUBROUTINE
*
*
	 TST.B	   NRLOS	       .	 IN RANGE & LINE OF SIGHT?
	 BEQ	   CLRILS2	       .	 NO? THEN CLEAR SYSTEM
*
*
*
*			     SIGNAL LEVEL NAV 2 ILS/VTC
*     INAV2VOL =ALIMU((NRRGAS/COR ALT RNG)**2,.99)
*
	 MOVE.W    20(A1),D7	       .	 RANGE 1
	 MOVE.W    NRRGAS,D5	       .	 RANGE TO STATION
	 EXT.L	   D5		       .	 PREPARE TO DIVIDE
	 TST.W	   D7		       .	 CHECK FOR ZERO
	 BNE.S	   NAV2Z	       .	 NOT ZERO, GO TO DIVIDE
	 MOVE.W    #1,D7	       +00	 MAKE RANGE 1 NON-ZERO
NAV2Z	 DIVS	   D7,D5	       +06	 % OF MAX RANGE
	 MULS	   D5,D5	       +12	 % RANGE SQUARED
	 MULS	   #07,D5	       +12+04+16 MULT BY .44 (FUDGE FACTOR)
	 ASR.L	   #8,D5	       +16-08+08 RESCALE
	 CMP.L	   #$80,D5	       +08	 CHECK FOR LIMIT
	 BMI.S	   NAV2Z1	       .	 BRANCH IF OK
	 MOVE.B    #$80,D5	       +08	 LIMIT TO $80 (MUTE)
NAV2Z1	 MOVE.B    D5,NAV2VOL	       +08	 SAVE NAV 2 VOLUME
*
	 SUB.B	   #$80,D5	       +08	 D5-$80    ($80=MUTE)
	 NEG.B	   D5		       +08	 $80-D5 = WHITE NOISE LEVEL
	 MOVE.B    D5,ONV2WNL(A6)      +08	 OUTPUT WHITE NOISE LEVEL
*
*
*			     FIND STA TYPE (VTC/ILS)
*
	 MOVE.W    2(A1),D2	       HEX	 STA TYP
	 CMP.W	   #$0068,D2	       HEX	 VTC ?
	 BEQ	   VOR2 	       .	 YES
	 PAGE
************************************************************
*
*			     NAV2 ILS
*
************************************************************
*
*			     MAG BEARING FROM STATION (+/- 180 DEG)
*     NAV2BRG =NRBRG=(ATAN(NRYGAS/NRXGAS) - 36(A0) = -DEV
*     36(A0)= RNWY MAG HEADING FROM ADDP TABLE
*     DEV =(RNWY MAG HEADING - AIRCRAFT TO ILS STATION BEARING)
*     A/C TO ILS STA BEARING=(ATAN(NRYGAS/NRXGAS))=ATAN(DELTA LON/DELTA LAT)
*
*
*
	 MOVE.W    NRBRG,NAV2BRG       +06	 MAG BRG FROM STATION
*
*			     ANGULAR DIFFERENCE(DEVIATION)
*    -DEV = NCDI2DEG = NAV2BRG	  FOR TUNED ILS STATIONS
*
	 MOVE.L    NAV2STA,A0	       .	 FIND STA SA		   ADDR
*
*	 MOVE.W    36(A0),D0	       +06	 RNWY MAG HDG		   DEG
*	 SUB.W	   NAV2BRG,D0	       +06	 -HDG DIFF		   DEG
*	 NEG.W	   D0		       +06	 CORRECT SIGN		   DEG
*
	 MOVE.W    NAV2BRG,D0	       +06	 -DEV
	 BSR	   NCDI 	       +06	 COURSE DEVIATION	   DEG
	 MOVE.W    D0,NCDI2DEG	       +06	 +DEV			   DEG
*
*			     LOCALIZER INDICATOR DRIVE
*     NCDI2DEG =ALIM(NCDI2DEG ,5)
*
	 CMP.W	   #319,D0	       +06	 GT.4.99		   DEG
	 BPL.S	   PLUS32	       +06	 YES
	 CMP.W	   #-319,D0	       +06	 GT -4.99		   DEG
	 BPL.S	   LMTILS2	       .	 YES
	 MOVE.W    #-319,D0	       +06	 LIMIT TO -4.99 	   DEG
	 BRA.S	   LMTILS2
PLUS32	 MOVE.W    #319,D0	       +06	 LIMIT TO 4.99		   DEG
*
*			     NEEDLE DRIVE
*	      ONAV2LR =DEFLECTION/5.
LMTILS2  EXT.L	   D0
	 DIVS	   #5,D0	       +06-00+06 DIV BY 5		   NORM
	 ASL.W	   #7,D0	       +06+07+13			   NORM
	 ASL.W	   #2,D0	       +13+02+15			   NORM
	 NEG.W	   D0		       .	 FOR NEEDLE DRIVE
*	 ASR.W	   #1,D0	       .	 DIVIDE BY 2(NOT NEEDED)
	 BCHG.L    #15,D0	       .	 SET UP FOR OUTPUT
	 MOVE.W    NAV2LR,D1	       +15	 GET OLD AO
	 BSR	   INTEG1	       .	 INTEGRATE TO NEW AO
	 MOVE.W    D0,NAV2LR	       +15	 STORE NEW AO
	 MOVEP.W   D0,ONAV2LR(A6)      +15	 CDI "AO"                  NORM
*
*			     "ON"("TO") FLAG
*
	 MOVE.W    #$FFFF,D0	       +15	 "TO" FLAG ON FOR ILS
	 MOVEP.W   D0,ONAV2TF(A6)
*
*			     GLIDE SLOPE
*			      (ASSUME IN RNG SINCE ILS IN RNG) AN APPROX
*
	 BTST.B    #IGSFAIL,IFAIL2(A6) LB	 GS MALF?
	 BNE.S	   GSOFF2	       .	 YES
	 MOVE.W    NRRGAS,D0	       +16	 RANGE TO STATION	   DEG
*
	 MULS	   ZFPDEG,D0	       +16-04+12 364566 FT PER DEG	   FEET
	 ASR.L	   #8,D0	       +12-08+04 DIST TO STATION	   FEET
	 MOVE.L    NAV2STA,A0	       .	 SA OF STA
	 ASR.L	   #6,D0	       +04-06-02 DIST.B 		   FEET
	 MOVE.W    42(A0),D1	       +00	 RUNWAY LENGTH		   FEET
	 ASR.W	   #2,D1	       +00-02-02 RUNWAY LENGTH		   FEET
*
*			     GS-DIST TO TOUCHDOWN POINT
*
	 SUB.W	   D1,D0	       -02	 DIST TO TOUCHDOWN 4 RES   FEET
	 MOVE.W    NRZGAS,D1	       +00	 HT. ABOVE STATION	   FEET
	 EXT.L	   D1		       +00	 HT. ABOVE STATION	   FEET
	 ASL.L	   #8,D1	       +00+08+08 HT. ABOVE STATION	   FEET
	 ASL.L	   #8,D1	       +00+08+16 HT. ABOVE STATION	   FEET
	 DIVS	   D0,D1	       +16+02+18 GS PATH ATAN		  RATIO
	 ASR.W	   #8,D1	       +18-08+10 GS PATH ATAN		  RATIO
	 MOVE.W    D1,D0	       +10	 GS PATH ATAN		  RATIO
**	 BSR	   ATAN2	       +10   +06 GS PATH		   DEG
**	 MOVE.W    D0,NGSD2DEG	       +06	 A/C GS 		   DEG
	 CMP.W	   #$0059,D0	       +06	 1.4			   DEG
	 BMI.S	   LT14D2	       +06	 LT 1.4 		   DEG
	 MOVE.W    #$0059,D0	       +06	 NO LIMIT TO 1.4	   DEG
LT14D2	 SUB.W	   #$002C,D0	       +06	 SUB .7 FOR NDL CTR	   DEG
*
*			     GS NEEDLE DRIVE AND FLAG
*     ONAV2GS =DEFLECTION/.7
*
	 MULS	   #$026C,D0	       +06+09+15 DIV BY .7		  RATIO
	 MOVEP.W   D0,ONAV2GS(A6)      +15	 GS NEEDLE DRIVE "AO"      NORM
	 BSET.B    #ONAV2GSF,NCDI2DO   .	 SET GS FLAG "ON"
	 BRA	   ENDVRIL2
*
**			     GS FLAG "OFF"
*
GSOFF2	 BCLR.B    #ONAV2GSF,NCDI2DO   .	 GS FLAG "OFF"
	 MOVE.W    #$8000,D0
	 MOVEP.W   D0,ONAV2GS(A6)      +15	 CENTRE THE GS NDLE "AO" 0 VOLT
	 BRA	   ENDVRIL2
	 PAGE
*************************************************************
*
*			     NAV2 VOR
*
*************************************************************
VOR2	 EQU	   *
	 MOVEP.W   ICRS2SC(A6),D0      +15	 SIN/COS OF SET COURSE
	 BCHG.L    #15,D0	       .	 CONVERT TO 2'S COMPLEMENT
*
*      SCALE SIN/COS INPUT
*
	 BTST.B    #OCRS2SEL,NCDI2DO   .	 READING SINE OR COS?
	 BEQ.S	   SINE2	       .
*
	 MOVE.W    D0,NCRS2COS	       +15	 SAVE COSINE
	 BCLR.B    #OCRS2SEL,NCDI2DO   .	 TOGGLE THE SIN/COS BIT
	 BRA.S	   CON2
*
SINE2	 MOVE.W    D0,NCRS2SIN	       +15	 SAVE THE SINE
	 BSET.B    #OCRS2SEL,NCDI2DO   .	 TOGGLE THE SIN/COS BIT
*
CON2	 MOVE.B    NCDI2DO,OCDI2DO(A6) .	 OUTPUT TEMP TO OUTPUT
*
	 MOVE.W    NCRS2COS,D0	       +15	 PREPARE FOR NATAN2
	 MOVE.W    NCRS2SIN,D1	       +15	 PREPARE FOR NATAN2
	 EXT.L	   D1		       +15
	 EXT.L	   D0		       +15
	 BSR	   NATAN2			 RESOLVE SIN/COS TO DEG
	 MOVE.W    D0,NAV2OBS	       +06	 SAVE NAV2 SELECTED BEARING
*
*
*			     TEST IF NAV2 PUSH-TO-TEST ON
*			     IF ON THEN NRBRG=180 DEGREES
*
	 BTST.B    #INAV2TST,ICRS2DI(A6)	 TEST IF PUSH TO TEST PRESSED
	 BEQ.S	   NAV2CON1
	 MOVE.W    #11520,NRBRG        +06	 NRBRG=180 DEG
*
*			     MAG BEARING FROM STATION(0-360)
*     NAV2BRG =TRUE BRG +NMAGVAR
*
NAV2CON1 MOVE.W    NRBRG,NAV2BRG       +06	 MAG BRG FROM STA	   DEG
*
*   DRIVE RMI VOR POINTER #2.(THIS OUTPUT NOT USED PRESENTLY IN TEST BED)
*			     (WHERE RMI IS REPLACED BY DG.)
*
	 MOVE.W    NAV2BRG,D0	       +06	 DRIVE RMI PTR 2 W/ VOR 2 BRG
	 SUB.W	   NPSIMH,D0	       +06	 SUBTRACT MAG HDG
	 BSR	   AEPC 	       +06	 LIMIT 0-360
	 BSR	   MSC1 	      . 	GET SIN/COS OF BEARING
	 BCHG.L    #15,D0	       +15	 CHANGE MSB FOR OUTPUT
	 BCHG.L    #15,D1	       +15	 CHANGE MSB FOR OUTPUT
	 MOVEP.W   D0,ORMI2S(A6)       +15	 SINE OF PTR 2
	 MOVEP.W   D1,ORMI2C(A6)       +15	 COSINE OF PTR 2
*
*			     ANGULAR DIFFERENCE(DEVIATION)
*     DEVIATION  =NAV2OBS -NAV2BRG
*
	 MOVE.W    NRBRG,D0	       +06
	 SUB.W	   NAV2OBS,D0	       +06	 -DEVIATION		   DEG
	 BSR	   AEPC
*	 BSR	   NCDI 	       +06	  DEVIATION		   DEG
	 MOVE.W    D0,NCDI2DEG	       +06	 DEVIATION		   DEG
*
*			     VTC       INDICATOR DRIVE
*     NCDI2DEG =ALIM(NCDI2DEG ,12.)
*
*	 CMP.W	   #1279,D0	       +06	 GT.19.99		   DEG
*	 BPL.S	   PLUS122	       .	 YES
*	 CMP.W	   #-1279,D0	       +06	 GT -19.99		   DEG
*	 BPL.S	   LMTVOR2	       .	 YES
*	 MOVE.W    #-1279,D0	       +06	 LIMIT -19.99		   DEG
*	 BRA.S	   LMTVOR2	       .
*PLUS122  MOVE.W    #1279,D0		+06	  LIMIT TO 19.99	    DEG
*
*			     NEEDLE DRIVE
*		ONAV2LR =DEFLECTION/12,
*LMTVOR2  EXT.L     D0
*	 DIVS	   #20,D0	       +06-00+06 DIV BY 20		   NORM
*	 ASL.W	   #7,D0	       +06+07+13			   NORM
*	 ASL.W	   #2,D0	       +13+02+15			   NORM
*	 NEG.W	   D0		       +15	 FOR DRIVING NEEDLE
**	 ASR.W	   #1,D0	       +15	 DIVIDE BY 2(NOT NEEDED)
*
*
*	 CDI NEEDLE = SIN(-DEV) LIMIT TO +/- 4 DOTS, +/- 2 DOTS=YELLOW EDGE
*	  INPUT = -DEV IN D0.W AT +06  = NCDI2DEG
*
	 BSR	   MSC1 			 SIN IN D0, COS IN D1 AT +15
	 MULS	   #737,D0	 +15+06+21	 D0 = 11.517472(SIN(-DEV)) DOTS
*
*     LIMIT # OF DOTS TO +/- 4 BEFORE OUTPUTTING = +/- 10 VOLTS
*			 +/- 2 DOTS = 5 V =(EDGE OF YELLOW = 3 DOTS ON VOA9)
*
	 CMPI.L    #8388607,D0	       +21	 IS D0 < 3.999 DOTS ?
	 BLT.S	   VOR2LM1
	 MOVE.L    #8388607,D0	       +21	 LIMIT TO +3.999 DOTS
	 BRA.S	   VOR2LM2
VOR2LM1  CMPI.L    #-8388607,D0        +21	 IS D0 < -3.999 DOTS ?
	 BGT.S	   VOR2LM2
	 MOVE.L    #-8388607,D0        +21	 LIMIT TO -3.999 DOTS
VOR2LM2  EQU	   *
*
	 DIVS	   #256,D0	 +21-06+15	 DIV BY 4 TO NORMALISE
*
	 BCHG.L    #15,D0	       +15	 CHANGE BIT 15 FOR OUTPUT
	 MOVEP.W   D0,ONAV2LR(A6)      +15	 CDI "AO"                  NORM
*
*
*	 TO-OFF-FROM FLAG CALCULATIONS
*	 -DEV IN NCDI2DEG = NAV2BRG - NAV2OBS
*	 D1.W = COS(-DEV) AT +15
*	     -75 < -DEV < 75 DEG       FLAG = "TO"
*	    -105 > -DEV >105 DEG       FLAG = "FROM"
*  75 < -DEV <105 DEG	OR -105 < -DEV <-75 DEG FLAG BETWEEN TO-OFF-FROM
*
*
	 MOVE.W    D1,D2	       +15	 SAVE COS FOR SIGN
*
*
*
	 BCHG.L    #15,D1	       +15	 FROM BTC TO BOB
	 MOVEP.W   D1,ONAV2TF(A6)      +15	 SET "TO/FROM" FLAG        VOLT
*
*			     CONE OF CONFUSION
*
*
*
*			     CONE RADIUS
*
	 MOVE.W    NRZGAS,D7	       +00	 HEIGHT ABOVE STATION	   FEET
	 MULS	   #591,D7	       +10	 CONE RADIUS		   FEET
	 DIVS	   #6080,D7	       +10-00+10 CONE RADIUS		   NM
	 ASR.W	   #4,D7	       +10-04+06 RESCALE
	 CMP.W	   NRRGAS,D7	       +04	 CONE RADIUS <= A/C DIST ? FEET
	 BLE.S	   OUTCONE2	       .	 YES
	 MOVE.W    #$8000,D0
	 MOVEP.W   D0,ONAV2TF(A6)      +15	 NO SET FLAG "OFF"   0     VOLT
OUTCONE2 EQU	   *
*
*			     MALF LOC/VTC (CDI)
*
ENDVRIL2 BTST.B    #IVL2FAIL,IFAIL1(A6) LB	  MALF VTC/ILS ?
	 BEQ.S	   ENDVL2	       .	 NO
	 MOVE.W    #$8000,D0
	 MOVEP.W   D0,ONAV2LR(A6)      +15	 YES CENTER CDI      0	   VOLT
	 MOVEP.W   D0,ONAV2LR(A6)      +15	 YES SET "OFF" FLAG  0     VOLT
	 BRA.S	   ENDVL2
*
*			     ILS/VTC NOT IN RANGE
*
CLRILS2  BCLR.B    #ONAV2GSF,NCDI2DO   .	 GLIDE SLOPE FLAG "OFF"    LB
	 SF.B	   NAV2TUN	       LB	 CLEAR NAV1 TUNED	   LB
	 CLR.L	   NAV1FREQ	       +00	 CLEAR NAV2 FREQ
	 MOVE.W    #$8000,D0
	 MOVEP.W   D0,ONAV2TF(A6)      +15	 SET LOC/ILS FLAG "OFF" 0  VOLT
	 MOVEP.W   D0,ONAV2LR(A6)      +15	 CENTER CDI 1 NEEDLE	0  VOLT
	 MOVEP.W   D0,ONAV2GS(A6)      +15	 CENTER GSDI  NEEDLE	0  VOLT
	 MOVE.B    #$FF,ONV2WNL(A6)    +08	 MUTE WHITE NOISE LEVEL
	 MOVE.B    #$FF,ONV2VOL(A6)    +08	 TURN OFF NAV2 VOL ATTN
	 ST.B	   NAV2VOL	       +06	 SET IDENT LEVEL TO MAX ATTEN
ENDVL2	 EQU	   *
	 PAGE
**********************************************************
*
*			     ADF
*
**********************************************************
	 BTST.B    #IMSTSW,IMPSDI1(A6) LB	 MASTER ON?		   DI
	 BEQ	   CLRADF	       .	 NO CLEAR INDICATORS
	 BTST.B    #IADF1ADF,IADF1DI(A6) LB	   ADF	    ON? 	     DI
	 BNE.S	   ADFON
	 BTST.B    #IADF1REC,IADF1DI(A6) LB	   NO TRY "REC" SW POS       DI
	 BEQ	   CLRADF	       .	 NO
*
*				       STATION RANGE
*
ADFON	 MOVE.B    #$FF,OLF2VOL(A6)    +08	 ADF2 IS ALWAYS OFF
	 MOVEP.W   IADF1VOL(A6),D0     ADDR	 READ IADF1VOL OFFSET BINARY
	 MOVE.B    D0,IADF1VOL(A6)     .	 START A/D CONVERSION
	 BSR	   CONATT	       .	 ATTEN CONTROL
	 MOVE.B    D0,OLF1VOL(A6)      +00	 OUTPUT OLF1VOL
*
	 TST.B	   NADF1TUN	       .	 ADF TUNED ?
	 BEQ	   NOTTUN1	       .	 YES: BRANCH
	 MOVE.L    NADF1STA,A1	       .	 NO:  FIND STA NO
	 BSR	   NAVRCV
*
	 SF.B	   NLF1LOS	       .	 CLEAR ADF 1 LOS FLAG
	 TST.B	   NRLOS	       .	 IN RANGE?
	 BEQ	   CLRADF	       .	 OUT OF LON RANGE
	 ST.B	   NLF1LOS	       .	 SET ADF1 LOS FLAG
*
*
*
*
*			     SIGNAL LEVEL ADF
*
*	 NLF1VOL = (( NRRGAS / 120.0 ) **2 + NADF1ERR / 2000.0 ) LIM 0.9999
*
	 MOVE.W    NRRGAS,D0	       +06	 RANGE TO STATION
	 EXT.L	   D0		       +06	 EXTEND FOR DIVIDE
	 DIVS	   #120,D0	       +06-00+06 DIVIDE BY MAX RANGE
	 MULS	   D0,D0	       +06+06+12 SQUARE IT
	 MOVE.L    NADF1ERR,D1	       +00	 GET FREQ DIFF
	 ASL.L	   #8,D1	       +00+08+08 RESCALE
	 ASL.L	   #4,D1	       +08+04+12 RESCALE
	 DIVS	   #2000,D1	       +12-00+12
	 CMP.W	   #4095,D1	       +12	 > 1.0
	 BLT.S	   NOLIM1
	 MOVE.W    #4095,D1	       +12	 LIMIT TO .999
*
NOLIM1	 ADD.W	   D1,D0	       +12
	 CMP.W	   #4095,D0	       +12	 > 1.0
	 BLT.S	   NOLIM2
	 MOVE.W    #4095,D0	       +12	 LIMIT TO .999
*
NOLIM2	 ASR.W	   #4,D0	       +08	 RESCALE TO BYTE
	 MOVE.B    D0,NLF1VOL
	 MOVE.W    #$00FF,D1	       +08	 .999
	 SUB.W	   D0,D1	       +08	 .999 - NLF1RNKY
	 MOVE.B    D1,OLF1WNL(A6)      +08	 ADF1 WHITE NOISE LEVEL
*
*
*
*
*			     "REC" SW POSITION ?
*
	 BTST.B    #IADF1REC,IADF1DI(A6) LB	   "REC"  SW POS ?           DI
	 BNE.S	   ENDADF	       .	 YES SKIP NEEDLE DRIVE
*			     TEST ?
*
	 BTST.B    #IADF1TST,IADF1DI(A6) LB	   TEST SW   ?		     DI
	 BEQ.S	   RELBRG	       .	 NO
	 MOVE.W    NADF1BRG,D0	       +06	 BRG TO STA		   DEG
	 ADD.W	   #$0080,D0	       +06	 INCRS POINTER AT 2D/ITER  DEG
	 BRA.S	   LIM360R
*
*
RELBRG	 EQU	   *
*
*			     ADF RELATIVE BEARING TO STATION (POINTER
*     NADF1BRG = NRBRG - NPSIMH  (0-360)
*
	 MOVE.W    NRBRG,D0	       +06	 GET STATION BEARING	   DEG
	 SUB.W	   NPSIMH,D0	       +06	 BRG FROM - HDG 	   DEG
LIM360R  BSR	   AEPC 	       +06	 LIMIT TO +/- 180 DEG	   DEG
	 MOVE.W    D0,NADF1BRG	       +06	 ADF REL BRG TO STA (PTR)  DEG
*
*				       NEEDLE DRIVE
*			     MALF ADF
*
	 BTST.B    #ILF1FAIL,IFAIL2(A6) LB	  MALF ADF ?
	 BNE.S	   ENDADF	       .	 YES
*
*   DRIVE RMI VOR POINTER #1 OR ADF POINTER ON ADF CONTROL HEAD FOR TEST BED
*
*	 BTST.B    #IRMI1ADF,IMPSDI1(A6)
*	 BNE.S	   NADFPTR	       .	 PTR 1 NOT ADF
	 MOVE.W    NADF1BRG,D0	       +06	 DRIVE RMI PTR 1 W/ ADF 1 BRG
	 BSR	   MSC1 	      . 	GET SIN/COS OF BEARING
	 BCHG.L    #15,D0
*	 BCHG.L    #15,D1
	 MOVEP.W   D0,ORMI1S(A6)       +15	 SIN OF PTR 1
*
	 MOVE.W    NADF1BRG,D0	       +06
	 ADD.W	   #3840,D0	       +06	 ADD 60 DEG
	 BSR	   AEPC 	       +06	 LIMIT TO +/- 180 DEG
	 BSR	   MSC1
	 BCHG.L    #15,D0	       +15
	 MOVEP.W   D0,ORMI1C(A6)       +15	 OUTPUT COSINE
*
*	 MOVEP.W   D1,ORMI1C(A6)       +15	 COS OF PTR 1
NADFPTR  EQU	   *
*
	 BRA.S	   ENDADF
*
NOTTUN1  MOVE.B    #0,OLF1WNL(A6)      .	 SET MAX WHITE NOISE IF NOT TUN
	 BRA.S	   ENDADF
*
*			     ADF     NOT IN RANGE
*			 NO  POWER ADF	 CLEAR/FRZ EVERYTHING
*
CLRADF	 EQU	   *
	 SF.B	   NADF1TUN	       LB	 CLR ADF  IN TUNE
	 MOVE.B    #$FF,NLF1VOL       +15	CLR ADF  IDENT LEVEL  0   NOR
	 MOVE.B    #$FF,OLF1WNL(A6)
ENDADF	 EQU	   *
	 MOVE.B    NCDI1DO,OCDI1DO(A6) .	 SEND TEMP CORE TO OUTPUT
	 MOVE.B    NCDI2DO,OCDI2DO(A6) .	 SEND TEMP CORE TO OUTPUT
	 MOVE.B    D0,ICRS1SC(A6)      .	 START NEXT A/D CONVERSION
	 MOVE.B    D0,ICRS2SC(A6)      .	 START NEXT A/D CONVERSION
*
*
	 TRAP	   #0
	 PAGE
*
	 XDEF	   CONATT
*
*		   MAKE VOLUME CONTROL LINEAR
*	 INPUT:    D0.W      OFFSET BINARY INPUT FROM AI
*	 OUTPUT:   D0.B      BYTE OUTPUT FOR MAO (7111)
*
CONATT	 EQU	   *
	 BCHG.L    #15,D0	       +15	 CONVERT TO 2'S COMPLEMENT
	 BTST.L    #15,D0	       +15	 CHECK FOR VALID AI
	 BEQ.S	   VOLOK1	       .	 BRANCH IF VALID
	 MOVE.W    #0,D0	       +15	 ELSE LIMIT TO 0
VOLOK1	 EXT.L	   D0		       +15	 SET UP FOR DIVIDE
	 CMP.L	   #32700,D0	       +15	 CHECK FOR UPPER LIMIT
	 BMI.S	   BREAK1	       .
*
	 MOVE.B    #$FF,D0	       +08	 SET AT MAX ATTEN
	 RTS
*
BREAK1	 CMP.L	   #16000,D0	       +15	 CHECK FOR BREAKPOINT
	 BMI.S	   BREAK2	       .	 BRANCH IF <16000
*						 16001 < X < 32700
	 SUB.L	   #16000,D0	       +15	 SUBTRACT 16000
	 DIVS	   #87,D0	       +15-05+10 DIVIDE BY 2.7
	 ASR.W	   #2,D0	       +08	 CHANGE TO BYTE
	 ADD.W	   #$20,D0	       +08
	 RTS
*						 0 < X < 16000
BREAK2	 DIVS	   #125,D0	       +15-05+10 DIVIDE BY 3.9
	 ASR.W	   #2,D0	       +08	 CHANGE TO BYTE
	 RTS
	 PAGE
***************************************************
*
*		   INTEGRATION ROUTINE
*
***************************************************
*
*	 INPUTS:   D1.W - OLD VALUE (CURRENT AO)   +15
*		   D0.W - NEW VALUE (DESIRED AO)   +15
*
*	 OUTPUT:   D0.W - NEW AO		   +15
*
INTEG1	 SUB.W	   D1,D0	       +15	 NEW-OLD
	 MULS	   #102,D0	       +15+10+25 MULT BY INTEG CONSTANT
	 ASR.L	   #8,D0	       +25-08+17 RESCALE
	 ASR.L	   #2,D0	       +17-02+15 RESCALE
	 ADD.W	   D1,D0	       +15	 UPDATE OLD
	 RTS
	 END
