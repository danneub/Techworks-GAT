****************************************************************************
*
*	 NAV RECEIVER SUBROUTINE
*
*	 PROGRAMMER : L.T. HARRISON
*	 DATE	    : 16 JUNE 1983
*
*
*	 REG A1 CONTAINS ADDRESS OF RADIO BUFFER FOR STATION IN QUESTION
*
****************************************************************************
	 XDEF	   NAVRCV
	 XREF	   MSC1
	 XREF	   DSMUL
	 XREF	   AEPC
	 XREF	   SQRT
	 XREF	   NATAN2
*
	 INCLUDE   NSYM.SA
*
	 SECTION   12
TFTPDG	 DC.W	   22785	       -04	 FEET PER DEGREE
*
NAVRCV	 EQU	   *
*
*		   COMPUTE SIN/COS OF AVERAGE LATITUDE
*		   A1 = NAV1STA
*
	 MOVE.L    26(A1),D0	       +23	 STATION LATITUDE
	 ADD.L	   NGLAT,D0	       +23	 ADD A/C LATITUDE
	 ASR.L	   #1,D0	       +23	 DIVIDE BY 2
	 ASR.L	   #5,D0	       +23-5=+18 RESCALE
	 ASR.L	   #5,D0	       +18-5=+13 RESCALE
	 ASR.L	   #7,D0	       +13-7=+06 RESCALE
	 JSR	   AEPC 	       +06	 CONVERT TO +/-180 DEG
	 JSR	   MSC1 	      +06	TAKE SIN AND COS
	 MOVE.W    D0,NRASIN	       +15	 SINE OF AVG LATITUDE
	 MOVE.W    D1,NRACOS	       +15	 COSINE OF AVG LATITUDE
*
*		   DELTA LAT/LON COMPUTATIONS
*
	 MOVE.L    26(A1),D0	       +23	 STATION LATITUDE
	 SUB.L	   NGLAT,D0	       +23	 SUBTRACT A/C LATITUDE
*    NOTE:  NRXGASL   AT +23
	 MOVE.L    D0,NRXGASL	       +23	 NRXGASL    LONG WORD
*
	 ASR.L	   #5,D0	       +23-05+18 RESCALE
	 ASR.L	   #6,D0	       +18-06+12 RESCALE
	 MOVE.W    D0,NRXGAS	       +12	 SAVE DELTA LATITUDE IN  DEG
*
	 MOVE.L    30(A1),D0	       +23	 STATION LONGITUDE
	 SUB.L	   NGLON,D0	       +23	 SUBTRACT A/C LONGITUDE
	 MOVE.L    D0,D3	       +23
*
*	 TEST DSMUL & NATAN1
*
	 MOVE.L    D3,D0	       +23	 D0.L=(STA LON - A/C LON)
	 ASL.L	   #1,D0	 +23+01+24
	 MOVE.W    NRACOS,D1	       +15	 D1.W = MULTIPLIER
	 JSR	   DSMUL      +24+15-16+23	 YGASL AT +23
	 MOVE.L    D0,NRYGASL	       +23	 YGASL AT +23
*   NOTE:  NRYGASL   AT +23
	 ASR.L	   #8,D0	 +23-08+15	 RESCALE
	 ASR.L	   #3,D0	 +15-03+12	 RESCALE
	 MOVE.W    D0,NRYGAS	       +12	 YGAS IN WORD		 DEG
*
*     NRBRG = (ATAN(NRYGASL/NRXGASL) _ 36(A1))	=  -DEV FOR ILS STATIONS
*					      = A/C TO STA BEARING FOR VOR STA
*
	 MOVE.L    NRXGASL,D0	       +23	 D0.L=DELTA LAT= COS(ANGLE)
	 MOVE.L    NRYGASL,D1	       +23	 D1.L=DELTA LON= SIN(ANGLE)
	 JSR	   NATAN2	       +06	 D0.W = ANGLE (FROM ATAN.SA)
	 MOVE.W    D0,NSPAREW2	       +06	 TEMP STORE FOR DEBUG
*
	 SUB.W	   NMAGVAR,D0	       +06	 SUBTRACT MAGNETIC VARIATION
	 JSR	   AEPC 	       +06	 LIMIT TO +/- 180 DEG
*						 D0 = A/C TO STA BEARING
*
	 SUB.W	   36(A1),D0	       +06	 D0 =(D0 - RNWY HEADING)FOR ILS
*						    =(D0 - MAG VAR) FOR VOR STA
	 JSR	   AEPC
	 MOVE.W    D0,NRBRG	       +06	 MAG BEARING TO STATION
*
*	     AGAS, BGAS, AND BEARING COMPUTATIONS
*
	 MOVE.W    40(A1),D0	       +15	 COSINE OF APPROACH BRG
	 MULS	   NRXGAS,D0	       +15+12=27 DELTA LAT ALONG RNWY BRG
	 ASR.L	   #8,D0	       +27-08=19 RESCALE
	 ASR.L	   #6,D0	       +19-06+13 RESCALE
	 MOVE.W    38(A1),D1	       +15	 SINE OF APPROACH BRG
	 MULS	   NRYGAS,D1	       +15+12=27 DELTA LON ALONG RNWY BRG
	 ASR.L	   #8,D1	       +27-08=19
	 ASR.L	   #6,D1	       +19-06+13 RESCALE
	 ADD.L	   D1,D0	       +13	 SUM OF THE DELTAS
	 ASR.L	   #1,D0	       +12	 RESCALE
	 MULS	   TFTPDG,D0	       +12-04+08 CONVERT DEGREES TO FEET
	 MOVE.L    D0,NSPARE5	       +08	 TEMP STORE OLD AGAS
*	 MOVE.L    D0,NRAGAS	       +08	 DIST. ALONG RNWY HEADING
*
*
	 MOVE.W    40(A1),D0	       +15	 COSINE OF APPROACH BEARING
	 MULS	   NRYGAS,D0	       +15+12+27 DELTA LON ACROSS RNWY BRG
	 ASR.L	   #8,D0	       +27-08=19 RESCALE
	 ASR.L	   #6,D0	       +19-06+13 RESCALE
*
	 MOVE.W    38(A1),D1	       +15	 SINE OF APPROACH BEARING
	 MULS	   NRXGAS,D1	       +15+12=27 DELTA LAT ACCROSS RNWY BRG
	 ASR.L	   #8,D1	       +27-08=19 RESCALE
	 ASR.L	   #6,D1	       +19-06+13 RESCALE
	 SUB.L	   D1,D0	       +13	 DIST IN DEGREES
	 ASR.L	   #1,D0	       +13-01+12 RESCALE FOR MULTIPLY
	 MULS	   TFTPDG,D0	       +12-04+08 CONVERT DEGREES TO FEET
	 MOVE.L    D0,NSPARE6	       +08	 TEMP STORE OLD BGAS
*	 MOVE.L    D0,NRBGAS	       +08	 DIST. ACROSS RNWY HEADING
*
*	 USE XGASL & YGASL & DSMUL TO GET NEW AGAS & BGAS
*
*	 NRAGAS =(XGASL * COS(RNWY APPR) + YGASL * SIN(RNWY APPR))*TFTPDG  FT
*	 NRBGAS =(YGASL * COS(RNWY APPR) - XGASL * SIN(RNWY APPR))*TFTPDG  FT
*	       FIRST   LIMIT XGASL & YGASL TO +/- 1 DEG
*
	 MOVE.L    NRXGASL,D0	       +23
	 CMP.L	   #8388608,D0	       .	 XGASL > 1 DEG ?
	 BGT.S	   LMDEG1
	 CMP.L	   #-8388608,D0        +23	 XGASL < -1 DEG ?
	 BGT.S	   LMDEG2
	 MOVE.L    #-8388608,D0        +23	 LIMIT TO -1 DEG
	 BRA.S	   LMDEG2
LMDEG1	 MOVE.L    #8388608,D0	       +23	 LIMIT TP +1 DEG
LMDEG2	 MOVE.L    D0,D3	       +23	 SAVE IT
*
	 MOVE.L    NRYGASL,D0	       +23
	 CMP.L	   #8388608,D0	       .	 YGASL > 1 DEG ?
	 BGT.S	   LMDEG3
	 CMP.L	   #-8388608,D0        +23	 YGASL < -1 DEG ?
	 BGT.S	   LMDEG4
	 MOVE.L    #-8388608,D0        +23	 LIMIT TO -1 DEG
	 BRA.S	   LMDEG4
LMDEG3	 MOVE.L    #8388608,D0	       +23	 LIMIT TP +1 DEG
LMDEG4	 MOVE.L    D0,D4	       +23	 SAVE IT
*
	 MOVE.L    D3,D0	       +23	 -1<= XGASL <= +1    DEG
	 MOVE.W    40(A1),D1	       +15	 COS(RNWY APP BEARING)
	 JSR	   DSMUL      +23+15-16+22
	 MOVE.L    D0,D2	       +22	 SAVE IT
*
	 MOVE.L    D4,D0	       +23	 -1<= YGASL <= +1    DEG
	 MOVE.W    38(A1),D1	       +15	 SIN(RNWY APP BEARING)
	 JSR	   DSMUL      +23+15-16+22	 D0.L = PRODUCT
*
	 ADD.L	   D2,D0	       +22	 SUM OF DELTAS	     DEG
	 ASL.L	   #8,D0	 +22+08+30	 SUM <= 1.4	     DEG
	 MOVE.W    TFTPDG,D1	       -04	 FEET PER DEG CONSTANT
	 JSR	   DSMUL      +30-04-16+10	 NEW AGAS
	 ASR.L	   #2,D0	 +10-02+08	 SCALE CHANGE
	 MOVE.L    D0,NRAGAS	       +08	 NEW AGAS
*	 MOVE.L    D0,NSPARE5	       +10	 SAVE NEW AGAS
*
*    CALCULATE	 NEW BGAS
	 MOVE.L    D3,D0	       +23	 -1<= XGASL <= +1    DEG
	 MOVE.W    38(A1),D1	       +15	 SIN(RNWY APP BEARING)
	 JSR	   DSMUL      +23+15-16+22
	 MOVE.L    D0,D2			 SAVE TEMP
*
	 MOVE.L    D4,D0	       +23	 -1<= YGASL <= +1
	 MOVE.W    40(A1),D1	       +15	 COS(RNWY APP BEARING)
	 JSR	   DSMUL      +23+15-16+22
	 SUB.L	   D2,D0	       +22	 NEW BGAS IN DEG
*
	 ASL.L	   #8,D0	 +22+08+30	 SCALE CHANGE
	 MOVE.W    TFTPDG,D1	       -04	 FT PER DEG CONSTANT
	 JSR	   DSMUL      +30-04-16+10	 NEW NRBGAS IN FT
	 ASR.L	   #2,D0	 +10-02+08	 SCALE CHANGE
	 MOVE.L    D0,NRBGAS	       +08	 NEW BGAS
*	 MOVE.L    D0,NSPARE6	       +10	 SAVE IT
*
**    NRBRG = (ATAN(NRYGAS/NRXGAS) _ 36(A1))  =  -DEV FOR ILS STATIONS
*					      = A/C TO STA BEARING FOR VOR STA
*
**	 MOVE.W    NRXGAS,D0	       +12	 D0.W= GET DELTA LATITUDE
*	 MOVE.W    NRYGAS,D1	       +12	 D1.W= GET DELTA LONGITUDE
*	 BSR	   NATAN	       .	 CALC ATAN(YGAS/XGAS)
*	 SUB.W	   NMAGVAR,D0	       +06	 SUBTRACT MAGNETIC VARIATION
*	 BSR	   AEPC 	       +06	 LIMIT TO +/- 180 DEG
*						 D0 = A/C TO STA BEARING
*
*	 SUB.W	   36(A1),D0	       +06	 D0 =(D0 - RNWY HEADING)FOR ILS
*						    =(D0 - MAG VAR) FOR VOR STA
*	 BSR	   AEPC
*	 MOVE.W    D0,NRBRG	       +06	 MAG BEARING TO STATION
**
*		   GROUND RANGE TO STATION SQUARED
*
	 MOVE.W    NRXGAS,D0	       +12	 DELTA LATITUDE
	 MULS	   #15,D0	       +12-02+10 MULT BY 60 NM/DEG
	 ASR.L	   #3,D0	       +10-03+07 RESCALE TO WORD
	 MULS	   D0,D0	       +07+07+14 DELTA LATITUDE SQUARED
*
	 MOVE.W    NRYGAS,D1	       +12	 DELTA LONGITUDE
	 MULS	   #15,D1	       +12-02+10 MULT BY 60 NM/DEG
	 ASR.L	   #3,D1	       +10-03+07 RESCALE TO WORD
	 MULS	   D1,D1	       +07+07+14 DELTA LONGITUDE SQUARED
*
	 ADD.L	   D1,D0	       +14	 SUM OF THE SQUARES
	 ASR.L	   #3,D0	       +14-03+11 RESCALE
	 MOVE.L    D0,NRGAS2	       +11	 SAVE RGAS**2
*
*		   GROUND RANGE TO STATION
*
	 JSR	   SQRT 	       .	 CALL SQUARE ROOT FUNCTION
	 MOVE.W    D0,NRRGAS	       +06	 SAVE RANGE TO STATION
*
*		   HEIGHT ABOVE STATION
*
	 MOVE.W    NGGA,D0	       +00	 HEIGHT ABOVE SEA LEVEL
	 SUB.W	   18(A1),D0	       +00	 SUBTRACT STATION ELEVATION
	 MOVE.W    D0,NRZGAS	       +00	 SAVE HEIGHT ABOVE STATION
*
*		   SLANT RANGE TO STATION SQUARED
*
	 EXT.L	   D0		       +00	 SIGN EXTEND INTO LONG WORD
	 ASL.L	   #7,D0	       +00+07+07 RESCALE
	 DIVS	   #6076,D0	       +07-00+07 CONVERT FROM FEET TO N.M.
	 MULS	   D0,D0	       +07+07+14 HEIGHT ABOVE STATION SQUARED
	 ASR.L	   #3,D0	       +14-03+11 RESCALE
	 ADD.L	   NRGAS2,D0	       +11	 ADD GROUND RANGE SQUARED
	 MOVE.L    D0,NRRAS2	       +11	 SAVE SLANT RANGE SQUARED
*
*		   SLANT RANGE
*
	 JSR	   SQRT 	       .	 CALL SQUARE ROOT FUNCTION
	 MOVE.W    D0,NRRRAS	       +06	 SLANT RANGE TO STATION
*
*		   STATION WITHIN RANGE COMPUTATIONS
*
	 ASR.W	   #6,D0	       +00	 NORMALIZE SCALE
	 SF.B	   NRWR1	       .	 CLEAR FLAG
	 SF.B	   NRWR2	       .	 CLEAR FLAG
	 SF.B	   NRWR3	       .	 CLEAR FLAG
	 CMP.W	   20(A1),D0	       .	 IN RANGE 1?
	 BGE.S	   OUT1 	       .	 NO. THEN CHECK RANGE 2
	 ST.B	   NRWR1	       .	 YES. IN RANGE 1
OUT1	 EQU	   *
	 CMP.W	   22(A1),D0	       .	 IN RANGE 2?
	 BGE.S	   OUT2 	       .	 NO. THEN CHECK RANGE 3
	 ST.B	   NRWR2	       .	 YES. IN RANGE 2
OUT2	 EQU	   *
	 CMP.W	   24(A1),D0	       .	 IN RANGE 3?
	 BGE.S	   OUT3 	       .	 NO. THEN GO TO NEXT SECTION
	 ST.B	   NRWR3	       .	 YES. IN RANGE 3
OUT3	 EQU	   *
*
*
*		   WITHIN LINE OF SIGHT COMPUTATION
*
*	IF NRZGAS IN FEET > (NRRSA2 X .75  -  672)  THEN NRLOS=1
*	NRZGAS IN FEET,  NRRSA2 IN (NM)*2
*
*
XX1	 MOVE.L    NRRAS2,D0	       +11	 GET SLANT RANGE SQUARED IN NM
	 ASR.L	   #2,D0	       +11	 D0=D0/4
	 MOVE.L    D0,D2	       +11	 TEMP STORE
	 ADD.L	   D2,D0	       +11	 D0=2D0/4
	 ADD.L	   D2,D0	       +11	 D0=3D0/4  = NRRAS2 X3/4 IN NM
	 MOVE.L    D0,D3	       +11	 D3=NRRAS2 X3/4  IN NM
*
	 SUB.L	   #1376256,D0	       +11	 D0=(D0 - 672) IN NM
*
	 MOVE.W    NRZGAS,D1	       +00	 HEIGHT ABOVE STATION IN FEET
	 EXT.L	   D1		       +00	 CONVERT TO LONG WORD
	 ASL.L	   #8,D1	       +08	 CHANGE SCALE
	 ASL.L	   #3,D1	       +11	 CHANGE SCALE
*
*	 DIVS	   #6076,D1	 +11-00+11	 CONVERT NRZGAS FROM FEET TO NM
*	 EXT.L	   D1		       +11	 CONVERT TO LONG WORD (IN NM)
*
	 SF.B	   NRLOS	       .	 CLEAR IN L.O.S. FLAG
	 CMP.L	   D1,D0	       +11	 HIGHER THAN HORIZON LINE
	 BGT.S	   LOS		       .	 IF NOT, OUT OF SIGHT
	 ST.B	   NRLOS	       .	 YES, IN LINE OF SIGHT
LOS	 EQU	   *
	 RTS
	 END
