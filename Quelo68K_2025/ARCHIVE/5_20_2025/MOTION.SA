	 OPT	   MEX,CEX,NOPCS
	 XDEF	   MOTION
*
	 INCLUDE   FSYM.SA
	 INCLUDE   MSYM.SA
	 INCLUDE   IO.SA
*
	 SECTION   12
MRC1	 DC.W	   36		       SINE 2 DEGREES AT 2-10
MRC2	 DC.W	   -36		       -SINE 2 DEGREES AT 2-10
MRC3	 DC.W	   1024 	       1 AT 2-10
MRC4	 DC.W	   15820	       SINE 75 DEG AT 2-14(LESS 5)
MRC5	 DC.W	   -15820	      -SINE 75 DEG AT 2-14(LESS 5)
MRC6	 DC.W	   8187 	       SINE 30 AT 2-14 (LESS 5)
MRC7	 DC.W	   -4570	       SINE -16 AT 2-14 (LESS 5)
MRC8	 DC.L	   -1048570	       -1 AT 2-20(LESS 6)
MRC9	 DC.W	   17476	    1/30 AT 2-19 OR 1/60 AT 2-20
MRC10	 DC.L	   1048570	       1 AT 2-20 (LESS 6)
MRC11	 DC.W	   72		       SINE 4 DEGREES AT 2-10
MRC12	 DC.W	   -72		       -SINE 4 DEGREES AT 2-10
**********************************************************************
*	 THIS MODULE COMPUTES:ROLL MOTION COMMAND(DEG/SEC)   AT 2-8  *
*		     PITCH MOTION COMMAND(DEG/SEC)	     AT 2-8  *
*		     HEADING MOTION COMMAND(DEG/SEC)	     AT 2-6  *
*		     SIN COCKPIT BANK ANGLE		     AT 2-15 *
*		     SIN COCKPIT PITCH ANGLE		     AT 2-15 *
*		     SIN COCKPIT HEADING ANGLE		     AT 2-15 *
*		     COS COCKPIT HEADING ANGLE		     AT 2-15 *
*		     COCKPIT ROLL RATE COMMAND (D/A)  10V=60 DEG/SEC *
*		     COCKPIT PITCH RATE COMMAND)D/A)  10V=30 DEG/SEC *
*		     COCKPIT HEADING RATE COMMAND(D/A)10V=30 DEG/SEC *
*					       OCTOBER 28 1983	     *
**********************************************************************
*
**********************************************************************
*		   (LIMIT 1) (---------LIMIT 2---------)	     *
*    ROLL COMMAND=PHI DOT*K1+4*(SIN PHI-SIN PHI COCKPIT)*16(DEG/SEC) *
*	  WHERE K1=O IF PHI DOT IS POSITIVE AND PHI COCKPIT=75DEG.   *
*		K1=0 IF PHI DOT IS NEGATIVE AND PHI COCKPIT=-75DEG.  *
*		K1=0 IF FLT FREEZE IS ACTIVATED 		     *
*		K1=1 OTHERWISE					     *
*		LIMIT 1=60 DEG/SEC				     *
*		LIMIT 2=SINE 2 DEG				     *
*    NOTE:ROLL COMMAND IS FADED IN WHEN ROLL MOTION IS TURNED ON     *
**********************************************************************
MOTION	 EQU *
	 MOVE.L    #$FE6000,A6		   I/O BASE ADDRESS IN A6
	 MOVE.W    MTIMROLL,D0		   ROLL TIMER(N-1)
	 ADDQ.W    #5,D0		  (ROLL TIMER+.0048828) AT 2-10
	 BTST.B    #IROLLON,IINSTDI1(A6)   1=ROLL MOTION ON
	 BNE.S	   NEXT 		   BRANCH IF ROLL MOTION IS ON
	 CLR.W	   D0			   SET ROLL TIMER TO ZERO
NEXT	 CMP.W	   MRC3,D0		  (ROLL TIMER-1) AT 2-10
	 BMI.S	   NEXT1		   BRANCH IF ROLL TIMER IS LESS THAN 1
	 MOVE.W    MRC3,D0		   LIMIT IN D0	(1 AT 2-10)
NEXT1	 MOVE.W    D0,MTIMROLL		   STORE ROLL TIMER(N) AT 2-10
	 MOVEP.W   ICPROLS(A6),D7	  (12 BIT A/D)+10V=SINE 90 DEG
	 MOVE.B    #0,ICPROLS(A6)	   START A/D CONVERSION
	 BCHG	   #15,D7		    COMPLEMENT SIGN BIT  AT 2-15
	 ASR.W	   #5,D7
	 ASL.W	   #5,D7		   DEADBAND LEAST SIGNIFIGANT BIT
	 MOVE.W    D7,MSINPHI		   SIN COCKPIT ROLL ANGLE AT 2-15
	 ASR.W	   #3,D7		   2-12
	 MOVE.W    FSINPHI,D6		   SIN PHI AT 2-15
	 ASR.W	   #3,D6		   2-12
	 SUB.W	   D7,D6	     4*(SIN PHI-SIN COCKPIT BANK ANGLE) AT 2-10
	 CMP.W	   MRC1,D6		   (---)-SIN 2 DEGREES
	 BMI.S	   NEXT2		   BRANCH IF LESS THAN SIN 2 DEGREES
	 MOVE.W    MRC1,D6		   LIMIT IN D6 AT 2-10
NEXT2	 CMP.W	   MRC2,D6		   (---)+SIN 2 DEGREES
	 BPL.S	   NEXT3		   BRANCH IF >- SIN 2 DEGREES
	 MOVE.W    MRC2,D6		   LIMIT IN D6 AT 2-10
NEXT3	 ASL.W	   #2,D6		   16*STEADY STATE ERROR AT 2-8
	 MOVE.W    MSINPHI,D4		   SIN COCKPIT BANK ANGLE AT 2-15
	 ASR.W	   #1,D4		   2-14
	 CLR.L	   D5			   ZERO IN D5
	 TST.B	   FFREEZE		   1=FLIGHT FREEZE
	 BNE.S	   NEXT5		   BRANCH IF FREEZE IS ACTIVATED
	 MOVE.W    FPHIDOT,D5	       PHI DOT AT 2-8
	 BMI.S	   NEXT4	       BRANCH IF PHI DOT IS NEGATIVE
	 CMP.W	   MRC4,D4	       SIN COCKPIT BANK ANGLE-SIN 75 DEGREES
	 BMI.S	   NEXT5	    BRANCH IF COCKPIT ANGLE IS LESS THAN 75 DEG
	 CLR.W	   D5		       ZERO IN D5
	 BRA.S	   NEXT5
NEXT4	 CMP.W	   MRC5,D4	       SIN COCKPIT BANK ANGLE+SIN 75 DEGREES
	 BPL.S	   NEXT5	       BRANCH IF COCKPIT ANGLE IS >-75 DEGREES
	 CLR.W	   D5		       ZERO IN D5
NEXT5	 ADD.W	   D5,D6	       ROLL RATE COMMAND AT 2-8
	 MOVE.W    D6,MROLLCOM	       ROLL COMMAND AT 2-8
	 MULS	   D0,D6	       FADE IN MULT(0-1)*ROLL COMM AT 2-18
	 ASR.L	   #8,D6	       2-10
	 ASR.L	   #2,D6	       2-8
	 MULS	   MRC9,D6	       1/60*ROLL COMMAND AT 2-28
	 ASR.L	   #8,D6	       2-20
	 CMP.L	   MRC10,D6	       (ROLL COMM -1) AT 2-20
	 BMI.S	   NEXT6	       BRANCH IF LESS THAN 1
	 MOVE.L    MRC10,D6	       1 AT 2-20 IN D7
NEXT6	 CMP.L	   MRC8,D6	       (ROLL COMM+1) AT 2-20
	 BPL.S	   NEXT7	       BRANCH IF >-1
	 MOVE.L    MRC8,D6	       LIMIT IN D7 AT 2-20
NEXT7	 ASR.L	   #5,D6	       2-15
	 BCHG	   #15,D6	       COMPLEMENT SIGN BIT
	 MOVEP.W   D6,OMOTROLR(A6)     (12 BIT D/A)*10V=60 DEG/SEC RT ROLL
******************************************************************************
*		       (-LIMIT 1--) (-----------LIMIT 2-----------)	     *
*	 PITCH COMMAND=THETA DOT*K1+4*(SIN THETA-SIN COCKPIT THETA)*16	     *
*	       WHERE:K1=0 IF THETA DOT IS POSITIVE AND THETA COCKPIT =30 DEG *
*		     K1=0 IF THETA DOT IS NEGATIVE AND THETA COCKPIT= -16DEG *
*		     K1=0 IF FLIGHT FREEZE IS ACTIVATED 		     *
*		     KI=0 IF THETA DOT IS NEGATIVE AND AIRCRAFT IS ON GROUND *
*		     K1=1 OTHERWISE					     *
*		     LIMIT 1=30 DEG/SEC 				     *
*		     LIMIT 2=SIN 4 DEG					     *
*	 NOTE:PITCH COMMAND IS FADED IN WHEN PITCH MOTION IS TURNED ON	     *
******************************************************************************
	 MOVE.W    MTIMPIT,D0	       PITCH MOTION TIMER AT 2-10
	 ADDQ.W    #5,D0	      (TIMER+,0048828) AT 2-10
	 BTST.B    #IPITON,IINSTDI1(A6)     1=PITCH MOTION ON
	 BNE.S	   NEXT8	       BRANCH IF PITCH MOTION IS ON
	 CLR.W	   D0		       SET TIMER TO ZERO
NEXT8	 CMP.W	   MRC3,D0	       (TIMER-1) AT 2-10
	 BMI.S	   NEXT9	       BRANCH IF LESS THAN 1
	 MOVE.W    MRC3,D0	       LIMIT IN D6 AT 2-10
NEXT9	 MOVE.W    D0,MTIMPIT	       PITCH TIMER(N) AT 2-10
	 MOVEP.W   ICPPITS(A6),D6     (12 BIT A/D)10V=SINE 90 DEG
	 MOVE.B    #0,ICPPITS(A6)      START A/D CONVERSION
	 BCHG	   #15,D6	       COMPLEMENT SIGN BIT AT 2-15
	 ASR.W	   #5,D6
	 ASL.W	   #5,D6	       DEADBAND LEAST SIGNIFIGANT BIT
	 MOVE.W    D6,MSINTHET	       SIN COCKPIT PITCH ANGLE AT 2-15
	 ASR.W	   #3,D6	       2-12
	 MOVE.W    FSINTHET,D7	       SIN THETA AT 2-15
	 ASR.W	   #3,D7	       2-12
	 SUB.W	   D6,D7	4*(SIN THETA-SIN COCKPIT PITCH ANGLE) AT 2-10
	 CMP.W	   MRC11,D7	       (---)-SIN 4 DEGREES AT 2-10
	 BMI.S	   NEXT10	       BRANCH IF LESS THAN SIN 4 DEGREES
	 MOVE.W    MRC11,D7		LIMIT IN D7 AT 2-10
NEXT10	 CMP.W	   MRC12,D7	       (---)+SIN 4 DEGREES
	 BPL.S	   NEXT11	       BRANCH IF>- SIN 4 DEGREES
	 MOVE.W    MRC12,D7		LIMIT IN D7 AT 2-10
NEXT11	 ASL.W	   #2,D7	      16*STEADY STATE ERROR COMMAND AT 2-8
	 MOVE.W    MSINTHET,D4	       SINE COCKPIT PITCH ANGLE AT 2-15
	 ASR.W	   #1,D4	       2-14
	 CLR.W	   D5		       ZERO IN D5
	 TST.B	   FFREEZE	       1=FLIGHT FREEZE
	 BNE.S	   NEXT14	       BRANCH IF FREEZE IS ACTIVATED
	 MOVE.W    FTHETDOT,D5	       THETA DOT AT 2-8
	 TST.B	   FWOG 	       1=ON GROUND
	 BEQ.S	   NEXT12	       BRANCH IF IN AIR
	 MOVE.W    D5,D2	       IN ORDER TO FLAG POSITIVE
	 BPL.S	   NEXT12	       BRANCH IF THETA DOT IS POSITIVE
	 CLR.W	   D5		       ZERO SLICE IF ON GROUND
NEXT12	 MOVE.W    D5,D2	       IN ORDER TO FLAG NEGATIVE
	 BMI.S	   NEXT13	       BRANCH IF THETA DOT IS NEGATIVE
	 CMP.W	   MRC6,D4    SINE COCKPIT PITCH ANGLE -SIN 30 DEGREES AT 2-14
	 BMI.S	   NEXT14	       BRANCH IF COCKPIT PITCH ANGLE<30 DEGREES
	 CLR.W	   D5		       ZERO IN D5
	 BRA.S	   NEXT14
NEXT13	 CMP.W	   MRC7,D4   SINE COCKPIT PITCH ANGLE +SINE 16 DEGREES AT 2-14
	 BPL.S	   NEXT14	       BRANCH IF COCKPIT ANGLE>-16 DEGREES
	 CLR.W	   D5		       ZERO IN D5
NEXT14	 ADD.W	   D5,D7	       2-8
	 MOVE.W    D7,MPITCOMM	       COCKPIT PITCH RATE COMMAND AT 2-8
	 MULS	   D0,D7	       FADE IN MULT*PITCH COMMAND AT 2-18
	 ASR.L	   #8,D7	       2-10
	 ASR.L	   #2,D7	       2-8
	 MULS	   MRC9,D7	       1/30*PITCH COMM AT 2-27
	 ASR.L	   #7,D7	       2-20
	 CMP.L	   MRC10,D7	       (PITCH COMM-1) AT 2-20
	 BMI.S	   NEXT15	       BRANCH IF LESS THAN 1
	 MOVE.L    MRC10,D7	       LIMIT IN D6 AT 2-20
NEXT15	 CMP.L	   MRC8,D7	       (PITCH COMM+1)AT 2-20
	 BPL.S	   NEXT16	       BRANCH IF >-1
	 MOVE.L    MRC8,D7	       LIMIT IN D6 AT 2-20
NEXT16	 ASR.L	   #5,D7	       2-15
	 BCHG	   #15,D7	       COMPLEMENT SIGN BIT
	 MOVEP.W   D7,OMOTPITR(A6)     12 BIT D/A)+10V=30 DEG/SEC NOSE UP
****************************************************************************
*			  (LIMIT 1) (-----------LIMIT 2-----------)	   *
* HEADING COMMAND=PSI DOT*K1+SINE(PSI-COCKPIT HEADING ANGLE)*0(WAS 4)	   *
*		 WHERE:K1=0 IF FLIGHT FREEZE IS ACTIVATED		   *
*		       K1=0 IF MODE 1 IS ACTIVATED			   *
*		       K1=1 OTHERWISE					   *
*		       LIMIT 1=30 DEG/SEC				   *
*		       LIMIT 2=SIN 2 DEGREES				   *
*	 NOTE:HEADING COMMAND IS FADED IN WHEN YAW MOTION IS TURNED ON	   *
*	      NEGATIVE A/I'S HAVE 1 ADDED TO ALLOW NEGATION                *
****************************************************************************
	 MOVE.W    MTIMHEAD,D0	       HEADING TIMER AT 2-10
	 ADDQ.W    #5,D0	       (TIMER+.0048828) AT 2-10
	 BTST.B    #IYAWON,IINSTDI1(A6)     1=YAW MOTION ON
	 BNE.S	   NEXT17	       BRANCH IF MOTION IS ON
	 CLR.W	   D0		       SET TIMER TO ZERO
NEXT17	 CMP.W	   MRC3,D0	       (TIMER-1)AT 2-10
	 BMI.S	   NEXT18	       BRANCH IF LESS THAN 1
	 MOVE.W    MRC3,D0	       LIMIT IN D0
NEXT18	 MOVE.W    D0,MTIMHEAD	       HEADING TIMER AT 2-10
	 MOVEP.W   ICPHDGS(A6),D7      (12 BIT A/D)+10V=SINE 90 DEG
	 MOVE.B    #0,ICPHDGS(A6)      START A/D CONVERSION
	 BCHG	   #15,D7	       COMPLEMENT SIGN BIT  AT 2-15
	 ASR.W	   #7,D7
	 ASL.W	   #7,D7	       DEADBAND LEAST 3 SIGNIFIGANT BITS
	 BPL.S	   NEXT19	       BRANCH IF POSITIVE
	 ADDQ.W    #1,D7	       ADD 1 IF NEGATIVE
NEXT19	 MOVE.W    D7,MSINPSI	       SIN COCKPIT HEADING ANGLE AT 2-15
	 MOVEP.W   ICPHDGC(A6),D6      (12 BIT A/D)-10V=COS 0 DEG
	 MOVE.B    #0,ICPHDGC(A6)      START A/D CONVERSION
	 BCHG	   #15,D6	       COMPLEMENT SIGN BIT  AT 2-15
	 ASR.W	   #7,D6
	 ASL.W	   #7,D6	       DEADBAND LEAST 3 SIGNIFIGANT BITS
	 BPL.S	   NEXT20	       BRANCH IF POSITIVE
	 ADDQ.W    #1,D6	       ADD 1 IF NEGATIVE
NEXT20	 MOVE.W    D6,MCOSPSI	       COS COCKPIT HEADING ANGLE AT 2-15
	 MULS	   FSINPSI,D6	       SINE PSI*COS MOTION PSI AT 2-30
	 MULS	   FCOSPSI,D7	       COS PSI*SIN MOTION PSI AT 2-30
	 SUB.L	   D7,D6	       SIN(PSI-MOTION PSI)AT 2-30
	 ASR.L	   #8,D6	       2-22
	 ASR.L	   #8,D6	       2-14
	 ASR.L	   #4,D6	       2-10
	 CMP.W	   MRC1,D6	       (--)-SIN 2 DEGREES AT 2-10
	 BMI.S	   NEXT21	       BRANCH IF (---)LESS SIN 2 DEGREES
	 MOVE.W    MRC1,D6	       LIMIT IN D6 AT 2-10
NEXT21	 CMP.W	   MRC2,D6	       (--)+2	AT 2-10
	 BPL.S	   NEXT22	       BRANCH IF (---)>-SIN 2 DEGREES
	 MOVE.W    MRC2,D6	       LIMIT IN D6 AT 2-10
NEXT22	 ASR.W	   #2,D6	       4*STEADY STATE ERROR COMMAND AT 2-6
	 CLR.W	   D6		       *******(SET SYNC COMMAND TO ZERO)*******
	 MOVE.W    FPSIDOT,D7	       HEADING RATE AT 2-8
	 ASR.W	   #2,D7	       2-6
	 TST.B	   FMODE1	       1=MODE 1 ACTIVATED
	 BEQ.S	   NEXT23	       BRANCH IF NOT MODE 1
	 CLR.W	   D7		       ZERO IN D7
NEXT23	 TST.B	   FFREEZE	       1=FREEZE
	 BEQ.S	   NEXT24	       BRANCH IF NOT FREEZE
	 CLR.W	   D7		       ZERO IN D7
NEXT24	 ADD.W	   D7,D6	       HEADING COMMAND AT 2-6
	 MOVE.W    D6,MHEADCOM	       HEADING COMMAND AT 2-6
	 MULS	   D0,D6	       FADE IN MULT*HEADING COMMAND AT 2-16
	 ASR.L	   #8,D6	       2-8
	 ASR.L	   #2,D6	       2-6
	 MULS	   MRC9,D6	       1/30*TURN COMMAND AT 2-25
	 ASR.L	   #5,D6	       2-20
	 CMP.L	   MRC10,D6	       (HEAD COMM-1)AT 2-20
	 BMI.S	   NEXT25	       BRANCH IF LESS THAN 1
	 MOVE.L    MRC10,D6	       LIMIT IN D6 AT 2-20
NEXT25	 CMP.L	   MRC8,D6	       (HEAD COMM+1) AT 2-20
	 BPL.S	   NEXT26	       BRANCH IF >-1
	 MOVE.L    MRC8,D6	       LIMIT IN D6 AT 2-20
NEXT26	 ASR.L	   #5,D6	       2-15
	 BCHG	   #15,D6	       COMPLEMENT SIGN BIT
	 MOVEP.W   D6,OMOTTRNR(A6)     +10V=30 DEG/SEC RT TURN
	 TRAP	   #0		       RETURN TO EXEC
	 END
