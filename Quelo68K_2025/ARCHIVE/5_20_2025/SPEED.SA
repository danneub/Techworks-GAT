	 OPT	   MEX,CEX,NOPCS
	 XDEF	   SPEED
	 XREF	   MSC1
*
	 INCLUDE   FSYM.SA
	 INCLUDE   ESYM.SA
	 INCLUDE   IO.SA
*
	 SECTION   12
FFAXAC1  DC.L	   87149       .03388*157 AT 2-14
FFAXAC2  DC.W	   303	       .06032*157 AT 2-5
FFAXAC3  DC.W	   2739        .06815*157 AT 2-8
FFAXAC4  DC.W	   4913        .00382*157 AT 2-13
FFAXAC5  DC.W	   1760 	1760 AT 2-0
FFAXAC6  DC.W	   157		157 AT 2-0
FFAXAC7  DC.L	   67010560	4090 AT 2-14
FFAXAC8  DC.W	   451		1.1/10 AT 2-12
FQC1	 DC.W	   155	       .001188 AT 2-17
FVPC1	 DC.W	   8243 	32.2 AT 2-8
FVPC2	 DC.W	   819	       .05 AT 2-14
FVPC3	 DC.L	   1889280	1845 AT 2-10
FVPC4	 DC.W	   -170 	-5.31 AT 2-5
FVIC1	 DC.W	   919	       .0000137 AT 2-26
FVIC2	 DC.W	   228	       .00087 AT 2-18
FVIC3	 DC.L	   2139095040	510 AT 2-22
FVIC4	 DC.W	   14	       .0004255 AT 2-15
FVIC5	 DC.L	   -246065805	 -58.6666 AT 2-22
FVIC6	 DC.W	   4289        (.9199/168.66-.08/58.66)AT 2-20
FVIC7	 DC.W	   715	       .08/58.66 AT 2-19
FVIC8	 DC.L	   32766	1 AT 2-15  (LESS 2)
FVIC9	 DC.L	   -283346200	-67.555 AT 2-22
FVIC10	 DC.W	   3720 	(.919/194.22-.08/67.55) AT 2-20
FVIC11	 DC.W	   621		.08/67.55  AT 2-19
FVWC1	 DC.W	   27671	6080/3600 AT 2-14
FSINC1	 DC.W	   23040	360 AT 2-6
FSINC2	 DC.W	   11520	180 AT 2-6
FSINC3	 DC.W	   -11520	-180 AT 2-6
**********************************************************************
*	 THIS MODULE COMPUTES:					     *
*	       FXA				  (LBS)AT 2-3	     *
*	       VP DOT				  (FT/SEC SQ)AT 2-8  *
*	       PARK BRAKE BOOLEAN		  1=PARK BRAKE ON    *
*	       WIND VELOCITY			  (KNOTS) AT 2-8     *
*	       ROUGH AIR MAGNITUDE		  AT 2-11	     *
*	       VP(FT/SEC)			  AT 2-22,2-6	     *
*	       V CAL(FT/SEC)			  AT 2-22,2-6	     *
*	       1845/VP				  AT 2-4	     *
*	       DELTA TK 			  AT 2-9	     *
*	       DYNAMIC PRESSURE (LBS/FT SQ)	  AT 2-6	     *
*	       SINE/COSINE ANGLE OF ATTACK	  AT 2-15	     *
*	       SINE/COSINE BANK ANGLE		  AT 2-15	     *
*	       SINE/COSINE PITCH ANGLE		  AT 2-15	     *
*	       SINE/COSINE TRUE HEADING 	  AT 2-15	     *
*	       SINE/COSINE BANK INDICATER (A/O'S) 10V=MAX            *
*	       TRUE GROUND VELOCITY (FT/SEC)	  AT 2-21,2-5	     *
*	 (FUEW)---- NORTH/SOUTH GROUND VELOCITY   (FT/SEC)AT 2-21,2-5*
*	 (FVEW)---- EAST/WEST GROUND VELOCITY	  (FT/SEC)AT 2-21,2-5*
*	      MODULE IS SKIPPED IF FLIGHT FREEZE IS ACTIVATED	     *
*						 OCTOBER 27,1983     *
**********************************************************************
*
***********************************************************************
*  FXA(LBS)=-157*Q*(.03388+.06032*CL*CL+.06815*FLAPS+.00382*ABS(BETA) *
*			 -4090<FXA<4090 			      *
***********************************************************************
SPEED	 EQU *
	 MOVE.L    #$FE6000,A6		(I/O BASE ADRESS IN A6)
	 TST.B	   FFREEZE		 1=FLIGHT FREEZE
	 BEQ.S	   UNFREEZS		 BRANCH IF NOT FLIGHT FREEZE
	 TRAP	   #0			 REAL TIME RETURN TO EXEC
UNFREEZS MOVE.W    FFAXAC2,D6		.06032*157 AT 2-5
	 MULS	   FCL,D6		.06032*157*CL AT 2-18
	 ASR.L	   #8,D6		 2-10		     (MAX=27727)
	 MULS	   FCL,D6		.06032*157*CL*CL AT 2-23
	 ASR.L	   #1,D6		 2-22
	 MOVE.W    FFAXAC3,D7		.06815*157 AT 2-8
	 MULS	   FFLAPS,D7		.06815*157*FLAPS AT 2-22
	 ADD.L	   D7,D6		 157(.06032CL SQ.+.0681FLAPS)AT 2-22
	 MOVE.W    FBETA,D7		 BETA AT 2-9
	 BPL.S	   NEXT 		 BRANCH IF POSITIVE
	 NEG.W	   D7			 ALWAYS POSITIVE
NEXT	 MULS	   FFAXAC4,D7		 157*.00382*ABS BETA AT 2-22
	 ADD.L	   D6,D7		 PARTIAL CD*157 AT 2-22
	 ASR.L	   #8,D7		 2-14
	 ADD.L	   FFAXAC1,D7		 CD TOTAL*157 AT 2-14
	 ASR.L	   #6,D7		 2-8	   (MAX=27218)
	 MULS	   FQ,D7		 157*Q*CD AT 2-14
	 CMP.L	   FFAXAC7,D7		 -FXA-4090  AT 2-14
	 BMI.S	   NEXT1		 BRANCH IF -FXA<4090
	 MOVE.L    FFAXAC7,D7		 LIMIT IN D7 AT 2-14
NEXT1	 ASR.L	   #8,D7		 2-6
	 ASR.L	   #3,D7		 2-3
	 NEG.W	   D7			 FXA AT 2-3
	 MOVE.W    D7,FFXA		 FXA AT 2-3
**************************************************************************
*VP DOT(FT/SEC SQ)=32.2/WG[FXA+TN-5.31*DEL TK-(100+1760*BRAKE)*WOW*WOW]  *
*		-32.2*(1.1/10*STALL+SIN GAMMA)				 *
**************************************************************************
	 MOVE.W    FDELTATK,D6		 DELTA TK AT 2-9
	 MULS	   FVPC4,D6		 -5.31*DELTA TK AT 2-14
	 ASR.L	   #8,D6		 2-6
	 ASR.L	   #1,D6		 2-5
	 ADD.W	   ENTHRUST,D6		 THRUST -5.31*DEL TK AT 2-5
	 ASR.W	   #2,D6		 2-3
	 ADD.W	   D6,D7		(TN-5.31*DELTA TK+FXA)AT 2-3
	 MOVE.W    FFAXAC5,D0		 1760 AT 2-0
	 BTST.B    #IPARKBK,IMPSDI2(A6)  1=PARK BRAKE ON
	 SNE.B	   FBRAKE		 1=PARK BRAKE ON
	 BNE.S	   NEXT2		 BRANCH IF BRAKES ARE ON
	 CLR.W	   D0			 ZERO IN D0
NEXT2	 ADDI.W    #100,D0		 (100+1760*BRAKE)AT 2-0
	 MULS	   FWOW,D0		 WOW*(100+1760*BRAKE)AT 2-12
	 ASR.L	   #8,D0		 2-4
	 MULS	   FWOW,D0		 2-16
	 ASR.L	   #8,D0		 2-8
	 ASR.L	   #5,D0		 2-3
	 SUB.W	   D0,D7		 TN-5.31*DEL TK+FXA-GROUND FORCE AT 2-3
	 MULS	   FWGINV,D7		 1/WG*(      )AT 2-20
	 ASR.L	   #8,D7		 2-12
	 CLR.W	   D1			 ZERO IN D1
	 TST.B	   FSTALL		 1=STALLED
	 BEQ.S	   NEXT3		 BRANCH IF NOT STALLED
	 MOVE.W    FFAXAC8,D1		 1.1/10 AT 2-12
NEXT3	 MOVE.W    FSINGAMA,D2		 SIN GAMMA AT 2-15
	 ASR.W	   #3,D2		 2-12
	 ADD.W	   D2,D1		 (1.1/10*STALL+SIN GAMMA)AT 2-12
	 SUB.W	   D1,D7  (TN-DEL TK+FXA-GRD FORCE)/WG-(1.1/10*STALL+SIN GAMMA)
	 ASR.W	   #2,D7		 2-10
	 MULS	   FVPC1,D7		 32.2*(-------)AT 2-18
	 ASR.L	   #8,D7		 2-10
	 ASR.L	   #2,D7		 2-8
	 MOVE.W    FVPDOT,D6		 VP DOT (N-1)  AT 2-8
	 MOVE.W    D7,FVPDOT		 VP DOT (N)AT 2-8
************************************************************
*	      VP(FT/SEC)=INTEGRAL(VP DOT*DT)		   *
*	      0<VP<510					   *
*   NOTE:  VP IS STORED AS A LONG WORD AT 2-22		   *
*	   AND CAN ALSO BE USED AS A WORD AT 2-6	   *
************************************************************
	 SUB.W	   D6,D7		 VP DOT (N)-VPDOT (N-1) AT 2-8
	 ASR.W	   #1,D7		 1/2 AT 2-8
	 ADD.W	   FVPDOT,D7		 3/2 VPDOT(N)-1/2 VPDOT(N-1) AT 2-8
	 MULS	   FVPC2,D7		.05*VP DOT AT 2-22
	 ADD.L	   FVP,D7		 VP(N) AT 2-22
	 BPL.S	   NEXT4		 BRANCH IF VP>0
	 CLR.L	   D7			 ZERO SLICE VP
NEXT4	 CMP.L	   FVIC3,D7		 VP-510 AT 2-22
	 BMI.S	   NEXT5		 BRANCH IF VP<510
	 MOVE.L    FVIC3,D7		 LIMIT IN D7 AT 2-22
NEXT5	 MOVE.L    D7,FVP		 VP(N) AT 2-22(LONG)2-6(WORD)
************************************************************
*	 CALL UP SINE/COSINE ROUTINE AND STORE: 	   *
*		   SINE/COSINE ANGLE OF ATTACK		   *
*		   SINE/COSINE BANK ANGLE		   *
*		   SINE/COSINE PITCH ANGLE		   *
*		   SINE/COSINE TRUE HEADING		   *
*		   SINE/COSINE INDICATOR BANK ANGLE	   *
*	 STORE ATTITUDE INDICATER SIN/COS BANK(A/O'S)      *
************************************************************
	 MOVE.W    FALPHA,D0		 ANGLE OF ATTACK AT 2-9
	 ASR.W	   #3,D0		 2-6
	 SPL.B	   D7			 1=POSITIVE
	 BPL.S	   NEXT6		 BRANCH IF POSITIVE
	 NEG.W	   D0			 ALWAYS POSITIVE
NEXT6	 BSR	   MSC1 		 BRANCH TO SINE/COSINE ROUTINE
	 MOVE.W    D1,FCOSALPH		 COSINE ALPHA AT 2-15
	 TST.B	   D7			 1=POSITIVE
	 BNE.S	   NEXT7		 BRANCH IF POSITIVE
	 NEG.W	   D0
NEXT7	 MOVE.W    D0,FSINALPH		 SINE ALPHA AT 2-15
	 MOVE.W    FPHI,D0		 BANK ANGLE AT 2-6
	 SPL.B	   D7			 1=POSITIVE
	 BPL.S	   NEXT8		 BRANCH IF POSITIVE
	 NEG.W	   D0			 ALWAYS POSITIVE
NEXT8	 BSR	   MSC1 		 BRANCH TO SINE/COSINE ROUTINE
	 MOVE.W    D1,FCOSPHI		 COSINE BANK ANGLE AT 2-15
	 TST.B	   D7			 1=POSITIVE
	 BNE.S	   NEXT9		 BRANCH IF POSITIVE
	 NEG.W	   D0
NEXT9	 MOVE.W    D0,FSINPHI		 SINE OF BANK ANGLE AT 2-15
	 MOVE.W    FPHI,D0		 BANK ANGLE AT 2-6
	 ASL.W	   #1,D0		 2*(BANK ANGLE) AT 2-6
	 BPL.S	   NEXT10		 BRANCH IF POSITIVE
	 CMP.W	   FSINC3,D0		 2*BANK ANGLE+180
	 BPL.S	   NEXT11		 BRANCH IF GREATER THAN -180
	 ADD.W	   FSINC1,D0		 360+2*BANK ANGLE AT 2-6
	 BRA.S	   NEXT11		 BRANCH TO SUBROUTINE
NEXT10	 CMP.W	   FSINC2,D0		 BANK ANGLE -180 AT 2-6
	 BMI.S	   NEXT11		 BRANCH IF BANK ANGLE IS LESS THAN 180
	 SUB.W	   FSINC1,D0		 BANK ANGLE -360 AT 2-6
NEXT11	 BSR	   MSC1 		 BRANCH TO SINE/COSINE ROUTINE
	 MOVE.W    D1,FICOSPHI		 COSINE(INDICATER BANK ANGLE)AT 2-15
	 MOVE.W    D0,FISINPHI		 SINE OF(INDICATER BANK ANGLE)AT 2-15
	 MOVE.W    FTHETA,D0		 PITCH ANGLE AT 2-9
	 ASR.W	   #3,D0		 2-6
	 SPL.B	   D7			 1=POSITIVE
	 BPL.S	   NEXT12		 BRANCH IF POSITIVE
	 NEG.W	   D0			 ALWAYS POSITIVE
NEXT12	 BSR	   MSC1 		 BRANCH TO SINE/COSINE ROUTINE
	 MOVE.W    D1,FCOSTHET		 COSINE PITCH ANGLE AT 2-15
	 TST.B	   D7			 1=POSITIVE
	 BNE.S	   NEXT13		 BRANCH IF POSITIVE
	 NEG.W	   D0
NEXT13	 MOVE.W    D0,FSINTHET		 SINE PITCH ANGLE AT 2-15
	 MOVE.W    FPSI,D0		 TRUE HEADING AT 2-6
	 BSR	   MSC1
	 MOVE.W    D0,FSINPSI		 SINE HEADING AT 2-15
	 MOVE.W    D1,FCOSPSI		 COSINE HEADING AT 2-15
	 MOVE.W    FISINPHI,D0		 SINE INDICATER BANK AT 2-15
	 BCHG	   #15,D0		 COMPLEMENT SIGN BIT
	 BTST.B    #IATGFAIL,IFAIL1(A6)  1=FAILED
	 BNE.S	   NEXT14		 BRANCH IF FAILED
	 MOVEP.W   D0,OATTROLS(A6)	 SIN INDICATER BANK(A/0)
NEXT14	 MOVE.W    FICOSPHI,D0		 COS INDICATER BANK AT 2-15
	 BCHG	   #15,D0		 COMPLEMENT SIGN BIT
	 BTST.B    #IATGFAIL,IFAIL1(A6)  1=FAILED
	 BNE.S	   NEXT15		 BRANCH IF FAILED
	 MOVEP.W   D0,OATTROLC(A6)	 COS INDICATER BANK (A/0)
*********************************************************************
*  TRUE HORIZONTAL RELATIVE AIRPLANE VELOCITY=VP*COS GAMMA	    *
*  FUEW=VP*COS GAMMA*COS PSI-VW*COS(PSIW CORR)	(FT/SEC)AT 2-21,2-5 *
*  FVEW=VP*COS GAMMA*SIN PSI-VW*SIN(PSIW CORR)	(FT/SEC)AT 2-21,2-5 *
*	      WHERE:  0<VW<100 (KNOTS)				    *
*		      COS GAMMA=COS(THETA-ALPHA)		    *
*		      PSIW CORR=PSIW-NAV BIAS-MAGVAR)  (DEGREES)    *
*********************************************************************
NEXT15	 MOVE.W    FWINDVEL,D4		(12 BIT A/I)10 VOLTS=100 KNOTS
	 MULS	   #100,D4		 WIND VEL(KNOTS) AT 2-15
	 ASR.L	   #7,D4		 2-8
	 MOVE.W    D4,FVW		 WIND VELOCITY(KNOTS) AT 2-8
	 MULS	   FVWC1,D4		 WIND VELOCITY(FT/SEC)AT 2-22
	 ASR.L	   #8,D4		 2-14
	 ASR.L	   #7,D4		 2-7
	 MOVE.W    FCOSTHET,D7		 COSINE THETA AT 2-15
	 MULS	   FCOSALPH,D7		 COS THETA*COS ALPHA AT 2-30
	 MOVE.W    FSINTHET,D6		 SINE THETA AT 2-15
	 MULS	   FSINALPH,D6		 SIN THETA*SIN ALPHA AT 2-30
	 ADD.L	   D6,D7		 COS GAMMA AT 2-30
	 ASR.L	   #8,D7		 2-22
	 ASR.L	   #7,D7		 COS GAMMA AT 2-15
	 MULS	   FVP,D7		 VP*COS GAMMA AT 2-21
	 MOVE.L    D7,FVPGND	  TRUE HOR REL VELOCITY AT 2-21(LONG) 2-5(WORD)
	 ASR.L	   #8,D7		 2-13
	 ASR.L	   #7,D7		 VP*COS GAMMA AT 2-6
	 MOVE.W    FCOSPSI,D5		 COSINE PSI AT 2-15
	 MULS	   D7,D5		 VP*COS GAMMA*COS PSI AT 2-21
	 MOVE.W    FCOSPSIW,D3	 COS WIND DIRECTION W.R.T. NAV HEADING AT 2-15
	 MULS	   D4,D3		 COS(PSIW CORR)*WIND VELOCITY AT 2-22
	 ASR.L	   #1,D3		 2-21
	 SUB.L	   D3,D5		 FUEW AT 2-21
	 MOVE.L    D5,FUEW	       NORTH/SOUTH GROUND VELOCITY AT 2-21,2-5
	 MULS	   FSINPSI,D7		 VP*COS GAMMA*SIN PSI AT 2-21
	 MOVE.W    FSINPSIW,D2	 SIN WIND DIRECTION W.R.T. NAV HEADING AT 2-15
	 MULS	   D2,D4		 SIN(PSIW CORR)*WIND VELOCITY AT 2-22
	 ASR.L	   #1,D4		 2-21
	 SUB.L	   D4,D7		 FVEW AT 2-21
	 MOVE.L    D7,FVEW		 EAST/WEST GROUND VELOCITY AT 2-21,2-5
************************************************************
*	       FVPINV(SEC/FT)=1845/VP			   *
*	      1<VP IN THIS COMPUTATION			   *
************************************************************
	 MOVE.W    FVP,D7		 VP AT 2-6
	 MOVE.L    FVPC3,D6		 1845 AT 2-10
	 CMPI.W    #64,D7		(VP-1)AT 2-6
	 BMI.S	   NEXT16		 BRANCH IF VP<1
	 DIVS	   D7,D6		 1845/VP AT 2-4
	 BRA.S	   NEXT17
NEXT16	 ASR.L	   #6,D6		 1845 AT 2-4
NEXT17	 MOVE.W    D6,FVPINV		 1845/VP AT 2-4
**********************************************************************
* V CAL(FT/SEC)=VP(1-.0000137*HP-.00087*DEL TK+.0004255*VP*ROUGH AIR)*
*								     *
*	      INDICATED AIRSPEED A/0:0V=0 MPH	 (NORMAL ENGINE)     *
*				     0.8V=40 MPH (NORMAL ENGINE)     *
*				     +10V=155 MPH(NORMAL ENGINE)     *
*				     0V=0 KNOTS(CON SPEED PROP)      *
*				     0.8V=40 KNOTS(CON SPEED PROP)   *
*				     +10 V=155 KNOTS(CON SPEED PROP) *
*	 NOTE:STORAGE OF A/O IS SKIPPED IF IAS IS FAILED	     *
**********************************************************************

	 MOVE.W    FVIC1,D7		.0000137 AT 2-26
	 MULS	   FHP,D7		.0000137*HP AT 2-27
	 MOVE.W    FOAT,D0		(12 BIT A/D)10 VOLTS=50 DEGREES
	 MULS	   #50,D0		 DELTA TK AT 2-15
	 ASR.L	   #6,D0		 DELTA TK AT 2-9
	 MOVE.W    D0,FDELTATK		 DELTA TK AT 2-9
	 MULS	   FVIC2,D0		.00087*DELTA TK AT 2-27
	 ADD.L	   D0,D7		.0000137*HP+.00087*TK AT 2-27
	 ASR.L	   #6,D7		 2-21
	 MOVE.W    FRUFAIR,D5		 VP*ROUGH AIR AMPLITUDE AT 2-6
	 MULS	   FVIC4,D5		.0004255*VP*ROUGH AIR AT 2-21
	 ADD.L	   D5,D7		 2-21
	 ASR.L	   #5,D7		 2-16	       (MAX=.497*65536)=32572
	 MULS	   FVP,D7    VP*(.0000137*HP+.00087*DELTA TK+ROUGH AIR)AT 2-22
	 MOVE.L    FVP,D6		 VP AT 2-22
	 SUB.L	   D7,D6		 2-22
	 MOVE.L    D6,FVCAL		 VCAL AT 2-22(LONG WORD) 2-6(WORD)
	 MOVE.L    FVIC5,D2		 -58.66 AT 2-22
	 MOVE.W    FVIC6,D3		 (.919/168.66-.08/58.66) AT 2-20
	 MOVE.W    FVIC7,D4		 .08/58.66 AT 2-19
*	 BTST.B    #IENGFIR,IINSTMDI(A6)	 1=CON SPEED PROP
*	 BEQ.S	   NERT 	       BRANCH IF NOT CON SPEED PROP
*	 MOVE.L    FVIC9,D2		  -67.55 AT 2-22
*	 MOVE.W    FVIC10,D3		 (.919/194.22-.08/67.55) AT 2-20
*	 MOVE.W    FVIC11,D4		  .08/67.55 AT 2-19
NERT	 ADD.L	   D2,D6
	 BPL.S	   NEXT18		  BRANCH IF POSITIVE
	 CLR.L	   D6			 ZERO SLICE
NEXT18	 ASR.L	   #8,D6		 2-14
	 ASR.L	   #8,D6		 2-6
	 MULS	   D3,D6       (VCAL-58.66)*(.919/168.66-.08/58.66)AT 2-26
	 ASR.L	   #8,D6		2-18
	 MOVE.W    FVCAL,D5		VCAL AT 2-6 IN D5
	 MULS	   D4,D5		VCAL*(.08/58.66)AT 2-25
	 ASR.L	   #7,D5		2-18
	 ADD.L	   D6,D5		2-18
	 ASR.L	   #3,D5		2-15
	 CMP.L	   FVIC8,D5		LIMIT TO 155
	 BMI.S	   NEXT19		BRANCH IF LESS THAN 155
	 MOVE.L    FVIC8,D5		LIMIT IN D5 AT 2-15
NEXT19	 BCHG	   #15,D5		COMPLEMENT SIGN BIT
	 BTST.B    #IIASFAIL,IFAIL1(A6) 1=FAIL INDICATED AIRSPEED
	 BNE.S	   NEXT20		BRANCH IF FAILED
	 MOVEP.W   D5,OIASP(A6) 	IND AIRSPEED(A/0)10V=155 MPH
************************************************************
*		Q(LB/FT SQ)=.001188*V CAL*V CAL 	   *
************************************************************
NEXT20	 MOVE.W    FQC1,D7		.001188 AT 2-17
	 MULS	   FVCAL,D7		.001188*VCAL AT 2-23
	 ASR.L	   #8,D7		 2-15
	 ASR.L	   #1,D7		 2-14
	 MULS	   FVCAL,D7		.001188 VCAL SQ. AT 2-20
	 ASR.L	   #8,D7		 2-12
	 ASR.L	   #6,D7		 2-6
	 MOVE.W    D7,FQ		 DYNAMIC PRESSURE AT 2-6
	 TRAP	   #0			 REAL TIME RETURN TO EXEC
	 END
