	 OPT	   CEX,MEX,NOPCS
	 XDEF	   RMNKFG
	 SECTION   12
****************************************************************************
*
*			  KEYING FUNCTION GENERATOR
*			  -------------------------
*
*		      THIS MODULE MUST RUN AT 10 HEARTZ
*
****************************************************************************
	 INCLUDE   NSYM.SA
	 INCLUDE   IO.SA
*
RMNKFG	 EQU	   *
	 MOVE.L    #$FE6000,A6	       .	 LOAD I/O BASE ADDR
*
*			     INNER MARKER
*
	 TST.B	   NIMTONE	       LB	 IM TONE ENABLED ?
	 BEQ.S	   CLRIM	       .	 NO
	 MOVE.B    NAMRA,NIMKEY        .	 YES ALTERNATE KEY	   DO
	 MOVE.B    NAMRA,OAMRAKY(A6)   .	 IMB KEY VOLUME
	 BNE.S	   STRTMM	       .	 IF SO, GO CHECK MMB
CLRIM	 ST.B	   OAMRAKY(A6)	       .	 TURN OFF IMB
	 ST.B	   NIMKEY	       .	 TURN OFF IMB
*
*			     MIDDLE MARKER
*
STRTMM	 TST.B	   NMMTONE	       LB	 MM TONE ENABLED ?
	 BEQ.S	   CLRMM	       .	 NO
	 ADD.W	   #1,NMMCTR	       +00	 YES INCR COUNTER	   .1S
	 CMP.W	   #6,NMMCTR	       +00	 NEG ?			   .1S
	 BMI.S	   MMKEY	       .	 YES
	 CLR.W	   NMMCTR	       +00	 NO RESET COUNTER	   .1S
MMKEY	 CMP.W	   #0,NMMCTR	       +00	 0 ?			   .1S
	 BEQ.S	   CLRMM	       .	 YES
	 CMP.W	   #2,NMMCTR	       +00	 NO 2 ? 		   .1S
	 BEQ.S	   CLRMM	       .	 YES
	 MOVE.B    NMMRA,OMMRAKY(A6)   .	 TURN KEY ON		   DO
	 MOVE.B    NMMRA,NMMKEY        .	 SOFTWARE KEY
	 BRA.S	   STRTOM	       .	 FINISHED MM
CLRMM	 ST.B	   OMMRAKY(A6)	       .	 TURN KEY OFF		   DO
	 ST.B	   NMMKEY	       .	 TURN KEY OFF
*
*			     OUTER  MARKER
*
STRTOM	 TST.B	   NOMTONE	       LB	 OM TONE ENABLED ?
	 BEQ.S	   CLROM	       .	 NO
	 ADD.W	   #1,NOMCTR	       +00	 YES INCR COUNTER	   .1S
	 CMP.W	   #4,NOMCTR	       +00	 NEG ?			   .1S
	 BMI.S	   OMKEY	       .	 YES
	 CLR.W	   NOMCTR	       +00	 NO RESET COUNTER	   .1S
OMKEY	 CMP.W	   #0,NOMCTR	       +00	 0 ?			   .1S
	 BEQ.S	   CLROM	       .	 YES
	 MOVE.B    NOMRA,OOMRAKY(A6)   .	 TURN KEY ON		   DO
	 MOVE.B    NOMRA,NOMKEY        .	 TURN KEY ON
	 BRA.S	   STRTNAV1	       .	 FINISHED OM
CLROM	 ST.B	   OOMRAKY(A6)	       .	 TURN KEY OFF		   DO
	 ST.B	   NOMKEY	       .	 TURN KEY OFF
	 PAGE
*
*			     NAV1
*
STRTNAV1 TST.B	   NAV1TUN	       LB	 NAV1 TONE ENABLED ?
	 BEQ	   CLRNAV1	       .	 NO
	 ADD.W	   #1,NAV1CTR	       +00	 YES INCR COUNTER	   .1S
	 CMP.W	   #64,NAV1CTR	       +00				   .1S
	 BMI.S	   KEYNAV1	       .	 COUNTER REACH 64?
*
	 CLR.W	   NAV1CTR	       +00	 NO RESET COUNTER	   .1S
	 ADD.W	   #1,NVTC1CTR	       +00	 INCREMENT VTC COUNTER
	 CMP.W	   #5,NVTC1CTR	       +00	 COUNTER REACH 5?	   .1S
	 BMI.S	   KEYNAV1	       .	 NO: BRANCH
	 CLR.W	   NVTC1CTR	       +00	 ELSE RESET CTR TO 0	   .1S
*
KEYNAV1  CMP.W	   #1,NAV1CTR	       +00	 1 ?			   .1S
	 BNE.S	   WRDCHC1	       .	 NO START TESTING BITS
	 MOVE.L    NAV1STA,A0	       +00	 SA OF GOOD STA 	   ADDR
	 ADD.L	   #2,A0	       +00	 ADDRESS OF STA TYPE	   ADDR
	 CMP.W	   #$0068,(A0)	       BIN	 VTC ?
	 SEQ.B	   NVTC1	       LB	 EQUATE TO VORTAC TYPE
	 ADD.L	   #6,A0	       +00	 ADDRESS OF FIRST IDENT WD ADDR
	 MOVE.L    (A0)+,NV1IDNT1      BIN	 SAVE FIRST IDENT WORD
	 MOVE.L    (A0),NV1IDNT2       BIN	 SAVE SECND IDENT WORD
WRDCHC1  CMP.W	   #32,NAV1CTR	       +00	 NO CTR GT 32 ? 	   .1S
	 BPL.S	   LWD2NV1	       .	 YES LOOK AT 2ND L WORD
	 MOVE.L    NV1IDNT1,D0	       BIN	 NO FIRST 32 BITS	   BIN
	 ST.B	   NAV1KY	       .	 TURN OFF KEY		   DO
	 BPL.S	   B
	 MOVE.B    NAV1VOL,NAV1KY      .	 TURN ON KEY
B	 ASL.L	   #1,D0	       BIN	 SETUP NEXT BIT 	   BIN
	 MOVE.L    D0,NV1IDNT1	       BIN	 SAVE IT		   BIN
	 BRA.S	   DMEPAUSE
LWD2NV1  MOVE.L    NV1IDNT2,D0	       BIN	 LAST 32 BITS		   BIN
	 ST.B	   NAV1KY	       .	 TURN OFF KEY
	 BPL.S	   C
	 MOVE.B    NAV1VOL,NAV1KY      .	 TURN ON KEY
C	 ASL.L	   #1,D0	       BIN	 SETUP NEXT BIT 	   BIN
	 MOVE.L    D0,NV1IDNT2	       BIN	 SAVE IT		   BIN
DMEPAUSE TST.B	   NVTC1	       BIN	 VTC ?
	 BEQ.S	   CLRDME1	       .	 NO
	 CMP.W	   #4,NVTC1CTR	       +00	 YES BUT IS IT 5TH CYCLE ?
	 BMI.S	   CLRDME1	       .	 NO
	 ST.B	   NAV1KY	       LB	 NAV1 KEY OFF FOR 5TH CYCLE
	 PAGE
*
*
*			     DME1
*
*
	 BCLR.B    #ODM1SEP,NDMECTL    .	 TURN ON  DME1 FOR 5TH CYCLE
*
	 ADD.W	   #1,NDME1CTR	       +00	 YES INCR COUNTER	   .1S
	 CMP.W	   #64,NDME1CTR        +00	 NEG ?			   .1S
	 BMI.S	   KEYDME1	       .	 YES
	 CLR.W	   NDME1CTR	       +00	 NO RESET COUNTER	   .1S
KEYDME1  CMP.W	   #1,NDME1CTR	       +00	 1 ?			   .1S
	 BNE.S	   WRDCHC3	       .	 NO START TESTING BITS
	 MOVE.L    NAV1STA,A0	       +00	 SA OF NAV1 VOR STA(DME CYCLE)
	 ADD.L	   #8,A0	       +00	 ADDRESS OF FIRST IDENT WD ADDR
	 MOVE.L    (A0)+,ND1IDNT1      BIN	 SAVE FIRST IDENT WORD
	 MOVE.L    (A0),ND1IDNT2       BIN	 SAVE SECND IDENT WORD
WRDCHC3  CMP.W	   #32,NDME1CTR        +00	 NO CTR GT 32 ? 	   .1S
	 BPL.S	   LWD2DM1	       .	 YES LOOK AT 2ND L WORD
	 MOVE.L    ND1IDNT1,D0	       BIN	 NO FIRST 32 BITS	   BIN
	 ST.B	   NDM1KY	       .	 TURN OFF KEY		   DO
	 BPL.S	   BD
	 MOVE.B    NAV1VOL,NDM1KY      .	 TURN ON KEY
BD	 ASL.L	   #1,D0	       BIN	 SETUP NEXT BIT 	   BIN
	 MOVE.L    D0,ND1IDNT1	       BIN	 SAVE IT		   BIN
	 BRA.S	   STRTNAV2
LWD2DM1  MOVE.L    ND1IDNT2,D0	       BIN	 LAST 32 BITS		   BIN
	 ST.B	   NDM1KY	       .	 TURN OFF KEY
	 BPL.S	   CD
	 MOVE.B    NAV1VOL,NDM1KY      .	 TURN ON KEY
CD	 ASL.L	   #1,D0	       BIN	 SETUP NEXT BIT 	   BIN
	 MOVE.L    D0,NV1IDNT2	       BIN	 SAVE IT		   BIN
	 BRA.S	   STRTNAV2
CLRNAV1  ST.B	   NAV1KY
	 CLR.W	   NAV1CTR	       .	 RESET NAV IDENT CYCLE CTR
CLRDME1  ST.B	   NDM1KY
	 CLR.W	   NDME1CTR	       .	 RESET DME IDENT CYCLE CTR
	 PAGE
*
*			     NAV2
*
STRTNAV2 TST.B	   NAV2TUN	       LB	 NAV2 TONE ENABLED ?
	 BEQ	   CLRNAV2	       .	 NO: BRANCH
	 ADD.W	   #1,NAV2CTR	       +00	 YES: INCR COUNTER	    .1S
	 CMP.W	   #64,NAV2CTR	       +00	 NEG ?			   .1S
	 BMI.S	   KEYNAV2	       .	 YES
*
	 CLR.W	   NAV2CTR	       +00	 NO RESET COUNTER	   .1S
	 ADD.W	   #1,NVTC2CTR	       +00	 INCREMENT VTC COUNTER
	 CMP.W	   #5,NVTC2CTR	       +00	 VTC COUNTER = 5?
	 BMI.S	   KEYNAV2	       .	 NO: BRANCH
	 MOVE.W    #0,NVTC2CTR	       .	 YES: RESET VTC COUNTER
*
KEYNAV2  CMP.W	   #1,NAV2CTR	       +00	 1 ?			   .1S
	 BNE.S	   WRDCHC2	       .	 NO START TESTING BITS
	 MOVE.L    NAV2STA,A0	       +00	 SA OF GOOD STA 	   ADDR
	 ADD.L	   #2,A0	       +00	 ADDRESS OF STA TYPE	   ADDR
	 CMP.W	   #$0068,(A0)	       .	 VTC ?
	 SEQ.B	   NVTC2	       LB	 EQUATE TO VORTAC TYPE
	 ADD.L	   #6,A0	       +00	 ADDRESS OF FIRST IDENT WD ADDR
	 MOVE.L    (A0)+,NV2IDNT1      .	 SAVE FIRST IDENT WORD
	 MOVE.L    (A0),NV2IDNT2       .	 SAVE SECND IDENT WORD
WRDCHC2  CMP.W	   #32,NAV2CTR	       +00	 NO CTR GT 32 ? 	   .1S
	 BPL.S	   LWD2NV2	       .	 YES LOOK AT 2ND L WORD
	 MOVE.L    NV2IDNT1,D0	       .	 NO FIRST 32 BITS	   BIN
	 ST.B	   NAV2KY	       .	 TURN OFF KEY
	 BPL.S	   D
	 MOVE.B    NAV2VOL,NAV2KY      .	 TURN ON KEY
D	 ASL.L	   #1,D0	       .	 SETUP NEXT BIT 	   BIN
	 MOVE.L    D0,NV2IDNT1	       .	 SAVE IT		   BIN
	 BRA.S	   DMEPAUS2
LWD2NV2  MOVE.L    NV2IDNT2,D0	       .	 LAST 32 BITS		   BIN
	 ST.B	   NAV2KY	       .	 TURN OFF KEY
	 BPL.S	   E
	 MOVE.B    NAV2VOL,NAV2KY      .	 TURN ON KEY
E	 ASL.L	   #1,D0	       .	 SETUP NEXT BIT 	   BIN
	 MOVE.L    D0,NV2IDNT2	       .	 SAVE IT		   BIN
DMEPAUS2 TST.B	   NVTC2	       .	 VTC ?
	 BEQ.S	   CLRDME2	       .	 NO
	 CMP.W	   #4,NVTC2CTR	       +00	 YES BUT IS IT 5TH CYCLE ?
	 BMI.S	   CLRDME2	       .	 NO
	 ST.B	   NAV2KY	       .	 NAV2 KEY OFF FOR 5TH CYCLE
	 PAGE
*
*
*			     DME2
*
*
	 BCLR.B    #ODM2SEP,NDMECTL    .	 TURN ON DME2 FOR 5TH CYCLE
*
	 ADD.W	   #1,NDME2CTR	       +00	 YES INCR COUNTER	   .1S
	 CMP.W	   #64,NAV2CTR	       +00	 NEG ?			   .1S
	 BMI.S	   KEYDME2	       .	 YES
	 CLR.W	   NDME2CTR	       +00	 NO RESET TO 0		   .1S
KEYDME2  CMP.W	   #1,NAV2CTR	       +00	 1 ?			   .1S
	 BNE.S	   WRDCHC4	       .	 NO START TESTING BITS
	 MOVE.L    NAV2STA,A0	       +00	 SA OF NAV2 VOR STA	   ADDR
	 ADDQ.L    #8,A0	       +00	 ADDRESS OF FIRST IDENT WD ADDR
	 MOVE.L    (A0)+,ND2IDNT1      .	 SAVE FIRST IDENT WORD
	 MOVE.L    (A0),ND2IDNT2       .	 SAVE SECND IDENT WORD
WRDCHC4  CMP.W	   #32,NDME2CTR        +00	 NO CTR GT 32 ? 	   .1S
	 BPL.S	   LWD2DM2	       .	 YES LOOK AT 2ND L WORD
	 MOVE.L    ND2IDNT1,D0	       .	 NO FIRST 32 BITS	   BIN
	 ST.B	   NDM2KY	       .	 TURN OFF KEY
	 BPL.S	   DD
	 MOVE.B    NAV2VOL,NDM2KY      .	 TURN ON KEY
DD	 ASL.L	   #1,D0	       .	 SETUP NEXT BIT 	   BIN
	 MOVE.L    D0,ND2IDNT1	       .	 SAVE IT		   BIN
	 BRA.S	   STRTADF	       .
LWD2DM2  MOVE.L    ND2IDNT2,D0	       .	 LAST 32 BITS		   BIN
	 ST.B	   NDM2KY	       .	 TURN OFF KEY
	 BPL.S	   ED
	 MOVE.B    NAV2VOL,NDM2KY      .	 TURN ON KEY
ED	 ASL.L	   #1,D0	       .	 SETUP NEXT BIT 	   BIN
	 MOVE.L    D0,ND2IDNT2	       .	 SAVE IT		   BIN
	 BRA.S	   STRTADF
CLRNAV2  ST.B	   NAV2KY
	 CLR.W	   NAV2CTR	       .	 RESET NAV IDENT CYCLE CTR
CLRDME2  ST.B	   NDM2KY
	 CLR.W	   NDME2CTR	       .	 RESET DME IDENT CYCLE CTR
	 PAGE
*
*
*
*			     ADF
*
STRTADF  TST.B	   NADF1TUN	       LB	 ADF  TONE ENABLED ?
	 BEQ.S	   CLRNADF	       .	 NO
	 ADD.W	   #1,NADF1CTR	       +00	 YES INCR COUNTER	   .1S
	 CMP.W	   #64,NADF1CTR        +00	 NEG ?			   .1S
	 BMI.S	   KEYADF	       .	 YES
	 CLR.W	   NADF1CTR	       +00	 NO RESET COUNTER	   .1S
KEYADF	 CMP.W	   #1,NADF1CTR	       +00	 1 ?			   .1S
	 BEQ.S	   SETUPADF	       .	 YES SETUP IDENT CORES
	 CMP.W	   #32,NADF1CTR        +00	 NO CTR GT 32 ? 	   .1S
	 BPL.S	   LWD2ADF	       .	 YES LOOK AT 2ND L WORD
	 MOVE.L    NA1IDNT1,D0	       BIN	 NO FIRST 32 BITS	   BIN
	 ST.B	   NLF1KY	       .	 TURN OFF KEY
	 BPL.S	   F
	 MOVE.B    NLF1VOL,NLF1KY      .	 TURN ON KEY
F	 ASL.L	   #1,D0	       BIN	 SETUP NEXT BIT 	   BIN
	 MOVE.L    D0,NA1IDNT1	       BIN	 SAVE IT		   BIN
	 BRA.S	   ENDKFG	       .	 FINISHED
LWD2ADF  MOVE.L    NA1IDNT2,D0	       BIN	 LAST 32 BITS		   BIN
	 ST.B	   NLF1KY	       .	 TURN OFF KEY
	 BPL.S	   G
	 MOVE.B    NLF1VOL,NLF1KY      .	 TURN ON KEY
G	 ASL.L	   #1,D0	       BIN	 SETUP NEXT BIT 	   BIN
	 MOVE.L    D0,NA1IDNT2	       BIN	 SAVE IT		   BIN
	 BRA.S	   ENDKFG	       .	 FINISHED
SETUPADF MOVE.L    NADF1STA,A0	       +00	 SA OF GOOD STA 	   ADDR
	 ADD.L	   #8,A0	       +00	 ADDRESS OF FIRST IDENT WD ADDR
	 MOVE.L    (A0)+,NA1IDNT1      BIN	 SAVE FIRST IDENT WORD	   BIN
	 MOVE.L    (A0),NA1IDNT2       BIN	 SAVE SCND  IDENT WORD	   BIN
	 BRA.S	   ENDKFG	       .	 FINISHED
CLRNADF  ST.B	   NLF1KY	       LB	 TURN KEY OFF		   DO
*
ENDKFG	 EQU	   *
	 PAGE
*
*	 CHECK FOR TUNED TO SAME STATION
*
*	 TST.B	   NAV1TUN
*	 BEQ.S	   BYE
*	 TST.B	   NAV2TUN
*	 BEQ.S	   BYE
*	 BTST.B    #INAV1ID,INAV1F3(A6)
*	 BEQ.S	   BYE
*	 BTST.B    #INAV2ID,INAV2F3(A6)
*	 BEQ.S	   BYE
*	 BTST.B    #IMONNAV1,IMPSDI2(A6)
*	 BEQ.S	   BYE
*	 BTST.B    #IMONNAV2,IMPSDI2(A6)
*	 BEQ.S	   BYE
*
*	 ST.B	   NAV2KY	       .	 CLEAR KEYS IF TUNED
*	 ST.B	   NDM2KY	       .	 TO SAME STATION
*
BYE	 BTST.B    #INAV1ID,INAV1F3(A6) .	 SEE IF IDENT SWITCH ON
	 BNE.S	   N1IDNT	       .	 BRANCH IF ON
	 MOVE.B    #$FF,NAV1KY	       .	 ELSE TURN OFF KEY
N1IDNT	 MOVE.B    NAV1KY,ONV1RNKY(A6) .	 OUTPUT NAV 1 KEY
*
	 BTST.B    #INAV2ID,INAV2F3(A6) .	 SEE IF IDENT SWITCH ON
	 BNE.S	   N2IDNT	       .	 BRANCH IF ON
	 MOVE.B    #$FF,NAV2KY	       .	 ELSE TURN OFF KEY
N2IDNT	 MOVE.B    NAV2KY,ONV2RNKY(A6) .	 OUTPUT NAV 2 KEY
*
	 MOVE.B    NDM1KY,ODM1RNKY(A6) .	 OUTPUT DME 1 KEY
	 MOVE.B    NDM2KY,ODM2RNKY(A6) .	 OUTPUT DME 2 KEY
	 MOVE.B    NLF1KY,OLF1RNKY(A6) .	 OUTPUT ADF 1 KEY
	 MOVE.B    NDMECTL,ODMECTL(A6) .	 OUTPUT TEMP BYTE TO ODMECTL
*
	 TRAP	   #0
	 END
