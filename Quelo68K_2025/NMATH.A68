	 OPT	   MEX,CEX,NOPCS
**********************************************************************
*								     *
*	 NAV MATH SUBROUTINE LIBRARY				     *
*								     *
**********************************************************************
	 XDEF	   AEPC 	       END POINT CHECK
	 XDEF	   NCDI 	       COURSE DEVIATION
	 XDEF	   NATAN2	       RESOLVE SIN/COS TO ANGLE
	 XDEF	   ATAN2	       ARC TAN
	 XDEF	   ESRD 	       ESRD DRIVER
*
	 XREF	   MSC1 	       MATH LIB SIN/COS
	 XREF	   MSIN1	       MATH LIB SIN
	 XREF	   DSMUL	       MATH LIB 32 * 16 = 32 MULTIPLY
	 XREF	   SQRT 	       MATH LIB 32 BIT SQUARE ROOT
	 PAGE
	 SECTION   12
**********************************************************************
*								     *
*	 ESRD	   SYNCRO DRIVE 				     *
*								     *
*	 INPUTS:   D0.W - INPUT ANGLE  +06  DEG  +-180.0	     *
*								     *
*	 OUTPUTS:  D0.W - OUTPUT X     +06  DEG  +-180.0	     *
*		   D1.W - OUTPUT Y     +06  DEG  +-180.0	     *
*								     *
**********************************************************************
ESRD	 EQU	   *
	 MOVE.W    D0,-(SP)	       +06	 SAVE INPUT ANGLE
*
	 ADD.W	   #7680,D0	       +06	 ADD 120 DEG
	 JSR	   AEPC 			 LIMIT TO +- 180
	 NEG.W	   D0		       +06	 NEGATE
	 JSR	   MSIN1			 CALL SIN
	 BCHG	   #15,D0	       +15	 CONVERT TO OFFSET BINARY
	 MOVE.W    D0,D1	       +15	 LOAD Y FOR OUTPUT
*
	 MOVE.W    (SP)+,D0	       +06	 LOAD INPUT ANGLE
*
	 ADD.W	   #3840,D0	       +06	 ADD 60 DEG
	 JSR	   AEPC 			 LIMIT TO +- 180
	 NEG.W	   D0		       +06	 NEGATE
	 MOVE.W    D1,-(SP)	       +15	 SAVE Y OUTPUT
	 JSR	   MSIN1			 CALL SIN
	 MOVE.W    (SP)+,D1	       +15	 RELOAD Y OUTPUT
	 BCHG	   #15,D0	       +15	 CONVERT TO OFFSET BINARY
	 RTS
	 PAGE
**********************************************************************
*								     *
*	 END POINT CHECK					     *
*								     *
*	 INPUTS:   D0.W - INPUT ANGLE  +06  DEG  +-511.999	     *
*								     *
*	 OUTPUTS:  D0.W - OUTPUT ANGLE +06  DEG  +-180.000	     *
*								     *
**********************************************************************
AEPC	 EQU	   *
	 CMPI.W    #11520,D0	       +06	 COMPARE TO 180 DEG
	 BLT.S	   LT180			 BRANCH IF LESS THAN 180
*
	 SUB.W	   #23040,D0	       +06	 SUBTRACT 360 DEG
	 BRA.S	   END180			 BRANCH TO END
*
LT180	 TST.W	   D0		       +06	 COMPARE WITH ZERO DEG
	 BPL.S	   END180			 BRANCH IF GREATER THAN 0
*
	 CMPI.W    #-11520,D0	       +06	 COMPARE TO -180 DEG
	 BGT.S	   END180			 BRANCH IF GREATER THAN -180
*
	 ADD.W	   #23040,D0	       +06	 ADD 360 DEG
END180	 RTS
	 PAGE
**********************************************************************
*								     *
*	 COURSE DEV SUBROUTINE					     *
*								     *
*	 INPUT:    D0 - HEADING DIFF   +06  +- 360 DEG		     *
*								     *
*	 OUTPUTS:  D0 - DEVIATION      +06  +- 89.9		     *
*		   D1 - TO-FROM FLAG   +15			     *
*								     *
**********************************************************************
NCDI	 EQU	   *
	 MOVE.W    #$FFFF,D1	       +15	 SET "TO"  FLAG     +10   VOLT
	 NEG.W	   D0		       +06	 DEVIATION		   DEG
	 BPL.S	   POSIT1	       .	 POSITIVE?
	 CMP.W	   #-5760,D0	       +06	 GT-90 ?		   DEG
	 BPL.S	   RET1 	       .	 YES: RETURN
	 CMP.W	   #-17280,D0	       +06	 GT-270?		   DEG
	 BPL.S	   MIN1801	       .	 YES
	 ADD.W	   #23040,D0	       +06	 NO ADD 360		   DEG
	 RTS			       .
*
MIN1801  MOVE.W    #0,D1	       +15	 SET "FROM" FLAG       -10   VO
	 ADD.W	   #11520,D0	       +06	 YES ADD 180		   DEG
	 NEG.W	   D0		       +06	 -180-DIFF		   DEG
	 RTS			       .
*
POSIT1	 CMP.W	   #5760,D0	       +06	 LT  90 		   DEG
	 BMI.S	   RET1 	       .	 YES: RETURN
	 CMP.W	   #17280,D0	       +06	 NO:  GT 270 ?		   DEG
	 BPL.S	   MIN3601	       .	 YES:
	 MOVE.W    #0,D1	       +15	 SET "FROM" FLAG       -10   VO
	 SUB.W	   #11520,D0	       +06	 NO DIFF -180		   DEG
	 NEG.W	   D0		       +06	 180 -DIFF		   DEG
	 RTS
*
MIN3601  SUB.W	   #23040,D0	       +06	 DIFF-360		   DEG
RET1	 RTS
	 PAGE
**********************************************************************
*								     *
*	 SUBROUTINE FOR ATAN WHEN INPUTS ARE LONG WORDS 	     *
*								     *
*	 INPUTS:   D0.L - COS(ANGLE)   +23			     *
*		   D1.L - SIN(ANGLE)   +23			     *
*								     *
*	 OUTPUT:   D0.W - ANGLE        +06  +- 180 DEG		     *
*								     *
**********************************************************************
NATAN2	 EQU	   *
	 MOVE.L    D0,D6	       +23	 SAVE COS FOR SIGN
	 MOVE.L    D1,D7	       +23	 SAVE SIN FOR SIGN
	 TST.L	   D0		       +23	 TEST COS
	 BPL.S	   COSPOS2			 BRANCH IF POSITIVE
*
	 NEG.L	   D0		       +23	 NEGATE COS
*
COSPOS2  TST.L	   D1		       +23	 TEST SIN
	 BPL.S	   SINPOS2			 BRANCH IF POSITIVE
*
	 NEG.L	   D1				 NEGATE SIN
*
SINPOS2  CMP.L	   D0,D1	       +23	 IS (NUM > DEN)
	 BGT.S	   NEXT12			 BRANCH IF GREATER THAN
*
	 TST.L	   D6		       +18	 TEST COS (INPUT)
	 BMI.S	   BASE1802			 BRANCH IF NEGATIVE
*
	 MOVE.W    #0,D4	       +06	 BASE = 0 DEG
	 BRA.S	   NEXT32
*
BASE1802 MOVE.W    #11520,D4	       +06	 BASE = 180 DEG
	 BRA.S	   NEXT22
*
NEXT12	 EQU	   *
	 MOVE.W    #5760,D4	       +06	 BASE = 90 DEG
	 EXG.L	   D0,D1	       +23	 NUM < DEN
*
	 TST.L	   D6				 IS COS NEGATIVE ?
	 BMI.S	   NEXT32
*
NEXT22	 NEG.L	   D1		       +23	 MAKE NUM NEGATIVE
*
NEXT32	 EQU	   *
	 TST.L	   D0		       +23	 TEST FOR ZERO DEN
	 BNE.S	   NZERO2
*
	 BRA.S	   EQZERO2
*
NZERO2	 EQU	   *
**********************************************************************
*								     *
*	 NUMERATOR   =	 D1.L	       +23 (SMALLER NUMBER) DEG      *
*	 DENOMINATOR =	 D0.L	       +23 (LAREGER NUMBER) DEG      *
*								     *
*	 ABS(NUMERATOR)  <=  ABS(DENOMINATOR)			     *
*								     *
**********************************************************************
	 CLR.L	   D2				 D2= NO. OF SHIFTS
LOOP1	 CMP.L	   #32767,D0	       +23	 D0.L<= $7FFF TO FIT IN WORD
	 BLT.S	   NEXT42
*
	 ASR.L	   #1,D0
	 ADD.L	   #1,D2			 D2 = N
	 BRA.S	   LOOP1
*
NEXT42	 EQU	   *
*
LOOPN1	 MOVE.L    D1,D3			 SAVE D1 = NUM
	 BEQ.S	   NEXT43			 IF D1=0, DON'T SHIFT !
	 ASL.L	   #1,D1
	 BVS.S	   NEXT43			 STOP IF  MSB SIGN CHANGES
	 ADD.L	   #1,D2			 D2 = (M+N)
	 BRA.S	   LOOPN1
*
NEXT43	 MOVE.L    D3,D1			 RECOVER NO. BEFORE LAST SHIFT
*
LOOP2	 CMP.L	   #15,D2
	 BLE.S	   NEXT52			 GO AND DIVIDE
	 ASR.L	   #1,D1			 RIGHT SHIFT MUM
	 SUB.L	   #1,D2			 D2=(M+N) <= 15
	 BRA.S	   LOOP2
*
NEXT52	 DIVS	   D0,D1			 D2=(M+N) <=15	QUO SCALE
*
	 BVC.S	   NOVFLOW2
*
EQZERO2  TST.L	   D1		 +22 OR +28	 CHECK IF NUM IS POSITIVE ?
	 BPL.S	   NINETY2
	 MOVE.W    #-5760,D0	       +06	 ANGLE = -90 DEG
	 BRA.S	   ENDATAN4
NINETY2  MOVE.W    #5760,D0	       +06	 ANGLE = +90 DEG
	 BRA.S	   ENDATAN4
*
NOVFLOW2 MOVE.W    D1,D0	     (M+N)	 5<= (M+N) <=15
*
LOOP3	 CMP.L	   #12,D2			 D2 = (M+N)
	 BLE.S	   NEXT62
	 ASR.W	   #1,D0			 MAKE (M+N) <= 12
	 SUB.L	   #1,D2
	 BRA.S	   LOOP3
*
NEXT62	 EQU	   *
LOOP4	 CMP.L	   #12,D2
	 BEQ.S	   NEXT72			 IF (M+N) = 12, DONE
	 ASL.W	   #1,D0
	 ADD.L	   #1,D2
	 BRA.S	   LOOP4
*
NEXT72	 EQU	   *				 NOW (M+N) = 12
*
	 MOVE.W    D4,-(SP)			 SAVE BASE
	 BSR.S	   ATAN2	       +08	 D0.W=ANGLE FROM TABLE AT +08
	 ASR.W	   #2,D0	       +08-02+06 RESCALE
*
	 MOVE.W    (SP)+,D4			 RESTORE BASE
	 ADD.W	   D4,D0	       +06	 ADD BASE TO ANGLE
*
	 TST.L	   D7		       +23	 TEST SIN(ANGLE)
	 BPL.S	   ENDATAN4			 BRANCH IF POSITIVE
*
	 NEG.W	   D0		       +06	 CHANGE SIGN
ENDATAN4 EQU	   *
	 RTS
	 PAGE
**********************************************************************
*								     *
*	 ATAN2	ROUTINE 					     *
*								     *
*	 INPUT:    D0.W - X=TAN(Y)     +12	 +- 7.99	     *
*	 OUTPUT:   D0.W - Y	       +08	 +- 89.9 DEG	     *
*								     *
*	 ALGORITHM:						     *
*								     *
*	 SEARCH TABLE OF  9 VALUES AND FIND THE 		     *
*	 2 POINTS THAT X (TAN Y) FALLS BETWEEN (X1 AND X2).	     *
*	 LOOK UP Y1 AND Y2 DEPENDENT UPON X1 AND X2.		     *
*	 INTERPOLATE BETWEEN THE POINTS AS FOLLOWS.		     *
*								     *
*	 Y = ((Y2-Y1)/(X2-X1)) * (X-X1) + Y1			     *
*								     *
**********************************************************************
ATAN2	 EQU	   *
	 MOVE.W    D0,D5	       +12	 SAVE X FOR SIGN
	 TST.W	   D0		       +12
	 BEQ.S	   ENDATAN2	       .	 ATAN 0 = 0
	 BPL.S	   NOTNEG	       .	 CHECK SIGN
	 NEG.W	   D0		       .	 MAKE POSITIVE
NOTNEG	 MOVE.L    #TANADDR,A0	       .	 LOAD TABLE ADDR
*
LOOPX1	 MOVE.W    (A0)+,D1	       +12	 GET TABLE ADDR (POST INC)
	 CMP.W	   D0,D1	       +12	 COMPARE POINT IN TABLE WITH X
	 BMI.S	   LOOPX1	       .	 LOOP UNTIL X > TABLE VALUE
*
	 SUB.L	   #4,A0	       .	 POINT TO X1
	 MOVE.L    A0,A2	       .
	 ADD.L	   #34,A2	       .	 POINT TO Y1
	 MOVE.W    (A0)+,D1	       +12	 GET X1
	 MOVE.W    (A0)+,D2	       +12	 GET X2
	 MOVE.W    (A2)+,D3	       +08	 GET Y1
	 MOVE.W    (A2)+,D4	       +08	 GET Y2
	 SUB.W	   D3,D4	       +08	 Y2-Y1
	 SUB.W	   D1,D2	       +12	 X2-X1
	 EXT.L	   D4		       +08	 PREPARE FOR DIVIDE
	 ASL.L	   #8,D4	       +08+08+16 RESCALE
	 ASL.L	   #5,D4	       +16+05+21 RESCALE
	 DIVS	   D2,D4	       +21-12+09 M = (Y2-Y1)/(X2-X1)
	 SUB.W	   D1,D0	       +12	 X-X1
	 MULS	   D4,D0	       +09+12+21 M * (X-X1)
	 ASR.L	   #8,D0	       +21-08+13 RESCALE
	 ASR.L	   #5,D0	       +13-05+08 RESCALE
	 ADD.W	   D3,D0	       +08	 Y = M * (X-X1) + Y1
	 TST.W	   D5		       +08	 CHECK ORIGINAL SIGN OF X
	 BPL.S	   ENDATAN2	       .	 BRANCH IF PLUS
*
	 NEG.W	   D0		       +08	 ATAN(-X) = -ATAN(X)
ENDATAN2 RTS
	 PAGE
**********************************************************************
*								     *
*	 ATAN2 LFI TABLE     ONLY UPTO 45 DEG REQUIRED		     *
*								     *
**********************************************************************
TANADDR  DC.W	   0		       +12	 TAN Y
	 DC.W	   200
	 DC.W	   400
	 DC.W	   600
	 DC.W	   816
	 DC.W	   1000
	 DC.W	   1248
	 DC.W	   1400
	 DC.W	   1696
	 DC.W	   1950
	 DC.W	   2192
	 DC.W	   2450
	 DC.W	   2736
	 DC.W	   3000
	 DC.W	   3360
	 DC.W	   3700
	 DC.W	   4096
*
	 DC.W	   0		       +08	 ATAN Y
	 DC.W	   716
	 DC.W	   1428
	 DC.W	   2133
	 DC.W	   2884
	 DC.W	   3512
	 DC.W	   4338
	 DC.W	   4831
	 DC.W	   5758
	 DC.W	   6517
	 DC.W	   7207
	 DC.W	   7907
	 DC.W	   8638
	 DC.W	   9272
	 DC.W	   10077
	 DC.W	   10776
	 DC.W	   11520
~~ ...M68K D2.0C  3/12/85   ...Run on Dec 4, 2025  13:48:36
	 END
400
	 DC.W	   1696
	 DC.W	   1950
	 DC.W	   2192
	 DC.W	   2450
	 DC.W	   2736
	 DC.W	   3000
	 DC.W	   3360
	 DC.W	   3700
	 DC.W	   4096
*
	 DC.W	   0		       +08	 ATAN Y
	 DC.W	   71